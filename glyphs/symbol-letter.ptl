###### Letter-like symbols

### Currency
symbol-block 'Currency'
	create-glyph 'currency' : glyph-construction
		assign-unicode 0xA4
		local sw : adviceBlackness 3.5
		local radius : (RIGHTSB - SB) / 2
		include : OShape (parenMid + radius * 0.9) (parenMid - radius * 0.9) (MIDDLE - radius * 0.9) (MIDDLE + radius * 0.9) sw
		foreach j [range 0 4] : begin
			local angle : Math.PI * (j / 2 + 1 / 4)
			include : dispiro
				widths.center sw
				flat (MIDDLE + radius * 0.87 * [Math.cos angle]) (parenMid + radius * 0.87 * [Math.sin angle])
				curl (MIDDLE + radius * [Math.sqrt 2] * [Math.cos angle]) (parenMid + radius * [Math.sqrt 2] * [Math.sin angle])
	create-glyph 'dollar' : glyph-construction
		set-width WIDTH; assign-unicode '$'
		include glyphs.S AS_BASE
		include : dispiro
			widths.center
			flat MIDDLE (CAP - HALFSTROKE)
			curl MIDDLE (CAP - DESCENDER / 2) [heading UPWARD]
		include : dispiro
			widths.center
			flat MIDDLE (DESCENDER / 2) [heading UPWARD]
			curl MIDDLE HALFSTROKE
	create-glyph 'euro' : glyph-construction
		assign-unicode 0x20AC
		include glyphs.C
		include : HOverlayBar (SB - HALFSTROKE) [mix SB RIGHTSB 0.7] (CAP * 0.4)
		include : HOverlayBar (SB - HALFSTROKE) [mix SB RIGHTSB 0.7] (CAP * 0.6)
	
	create-glyph 'cent' : glyph-construction
		assign-unicode 0xA2
		include glyphs.c AS_BASE
		include : dispiro
			widths.center
			flat MIDDLE (XH - HALFSTROKE)
			curl MIDDLE (XH - DESCENDER / 2) [heading UPWARD]
		include : dispiro
			widths.center
			flat MIDDLE (DESCENDER / 2) [heading UPWARD]
			curl MIDDLE HALFSTROKE
	
	create-glyph 'yen' : glyph-construction
		assign-unicode 0xA5
		include glyphs.Y AS_BASE
		include : HOverlayBar SB RIGHTSB (CAP * 0.45)
		include : HOverlayBar SB RIGHTSB (CAP * 0.25)
	
	
	create-glyph 'sterlingbase' : glyph-construction
		include : dispiro
			widths.lhs
			g4 [mix SB RIGHTSB 0.95] (CAP - HOOK * 0.85)
			hookstart (CAP - O) true
			flat [mix SB RIGHTSB 0.2] [CAP - SMOOTHA]
			curl [mix SB RIGHTSB 0.2] [mix STROKE CAP 0.3]
			g4   SB STROKE
		include glyphs.underscore
	create-glyph 'sterling' : glyph-construction
		assign-unicode 0xA3
		set-width WIDTH
		include glyphs.sterlingbase
		include : HOverlayBar [mix SB RIGHTSB 0.05] [mix SB RIGHTSB 0.7] (CAP * 0.53)
	create-glyph 'liraSymbol' : glyph-construction
		assign-unicode 0x20A4
		set-width WIDTH
		include glyphs.sterlingbase
		include : HOverlayBar [mix SB RIGHTSB 0.05] [mix SB RIGHTSB 0.7] (CAP * 0.4)
		include : HOverlayBar [mix SB RIGHTSB 0.05] [mix SB RIGHTSB 0.7] (CAP * 0.63)
	
	create-glyph 'frenchFranc' : glyph-construction
		assign-unicode 0x20A3
		include glyphs.F
		include : HOverlayBar (SB * 0.5) (SB * 3 + STROKE * HVCONTRAST) [mix 0 XH 0.4]
	
	create-glyph 'peseta' : glyph-construction
		assign-unicode 0x20A7
		include glyphs.P
		include : HOverlayBar [mix SB 0 0.7] [mix RIGHTSB WIDTH 0.7] [mix ((CAP - STROKE) * BARPOS - HALFSTROKE) CAP (SMOOTHB / (SMOOTHA + SMOOTHB))]

### Other
symbol-block 'Letterlike Symbols'
	create-glyph 'numero' : glyph-construction
		assign-unicode 0x2116
		local fine : adviceBlackness 4.5
		include : OShape XH (fine * 2) (MIDDLE + fine / 2) [mix RIGHTSB WIDTH 0.5] fine (SMOOTHA * fine / STROKE) (SMOOTHB * fine / STROKE)
		include : NShape CAP (SB / 2) MIDDLE 4.5 5.5 0.7
		include : HBarBottom (MIDDLE + fine / 2) [mix RIGHTSB WIDTH 0.5] 0 fine
	
	alias 'Ohm' 0x2126 'Omega'
	turned 'Mho' 0x2127 'Ohm' MIDDLE (CAP / 2)
	
	create-glyph 'ell' : glyph-construction
		assign-unicode 0x2113
		include bMarks
		local fine : adviceBlackness 3.5
		local radius : Math.max fine (WIDTH * 0.15)
		local middle : MIDDLE - fine / 2
		local left : middle - radius * 1.25
		local right : middle + radius * 1.5
		local sma : SMOOTHA * 0.6
		local smb : SMOOTHB * 0.6
		include : dispiro
			widths.center fine
			g2   left [mix 0 CAP 0.33]
			quadcontrols 1 0.6
			g2   right [mix 0 CAP 0.8]
			arcvh
			g2   [mix middle right 0.5] (CAPO - fine / 2)
			archv
			flat middle (CAP - sma)
			curl middle (SMOOTHB * 0.5 + fine / 2)
			hookend (O + fine / 2)
			g2   (middle + radius * 2) SHOOK
	
	create-glyph 'estimated' : glyph-construction
		assign-unicode 0x212E
		include : SmallEShape CAP [adviceBlackness 4] 0.5
	
	turned 'turniota' 0x2129 'iota' MIDDLE (XH / 2)