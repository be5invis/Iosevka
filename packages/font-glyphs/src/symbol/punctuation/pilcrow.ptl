### Punctuation symbols
$$include '../../meta/macros.ptl'

import [mix linreg clamp fallback] from "@iosevka/util"

glyph-module

glyph-block Symbol-Punctuation-Pilcrow : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives

	create-glyph : glyph-proc
		set-width Width
		local sw : AdviceStroke 3.6

		include : union
			dispiro
				widths.center sw
				flat (RightSB - sw / 4) (CAP - sw / 2) [heading Leftward]
				curl (Middle - sw / 4) (CAP - sw / 2) [heading Leftward]
				archv
				g4.down.mid (SB + sw / 2) [mix (CAP * 0.5) (CAP - sw / 2) 0.5] [heading Downward]
				arcvh
				straight.right.end (Middle - sw / 4) (CAP * 0.5) [heading Rightward]
			spiro-outline
				straight.left.start (Middle - sw / 4) (CAP - sw / 2 - TINY)
				archv
				g4.down.mid (SB + sw / 2 + 0.2) [mix (CAP * 0.5) (CAP - sw / 2) 0.5]
				arcvh
				straight.right.end (Middle - sw / 4) (CAP * 0.5 + TINY)

		create-forked-glyph 'pilcrow.high' : glyph-proc
			include : VBar.m (Middle + sw / 4) 0 CAP sw
			include : VBar.m (RightSB - sw / 2) 0 CAP sw
		create-forked-glyph 'pilcrow.low' : glyph-proc
			include : VBar.m (Middle + sw / 4) Descender CAP sw
			include : VBar.m (RightSB - sw / 2) Descender CAP sw

	select-variant 'pilcrow' 0xB6

	create-glyph : glyph-proc
		create-forked-glyph 'revPilcrow.high' : glyph-proc
			include [refer-glyph 'pilcrow.high'] AS_BASE
			include : FlipAround Middle (CAP / 2) (-1) 1
		create-forked-glyph 'revPilcrow.low' : glyph-proc
			include [refer-glyph 'pilcrow.low'] AS_BASE
			include : FlipAround Middle (CAP / 2) (-1) 1

	select-variant 'revPilcrow' 0x204B

	create-glyph 'leftBullet' 0x204C : glyph-proc
		set-width Width
		local sw : AdviceStroke 3.6

		include : union
			dispiro
				widths.center sw
				flat (RightSB - sw / 4) (PlusTop - sw / 2) [heading Leftward]
				curl (Middle - sw / 4) (PlusTop - sw / 2) [heading Leftward]
				archv
				g4.down.mid (SB + sw / 2) SymbolMid [heading Downward]
				arcvh
				flat (Middle - sw / 4) (PlusBot + sw / 2) [heading Rightward]
				curl (RightSB - sw / 4) (PlusBot + sw / 2) [heading Rightward]
			spiro-outline
				straight.left.start (Middle - sw / 4) (PlusTop - sw / 2 - TINY) [heading Leftward]
				archv
				g4.down.mid (SB + sw / 2 + TINY) SymbolMid [heading Downward]
				arcvh
				straight.right.end (Middle - sw / 4) (PlusBot + sw / 2 + TINY) [heading Rightward]

		include : VBar.m (Middle + sw / 4)  PlusBot PlusTop sw
		include : VBar.m (RightSB - sw / 2) PlusBot PlusTop sw

	create-glyph 'rightBullet' 0x204D : glyph-proc
		include [refer-glyph 'leftBullet'] AS_BASE
		include : FlipAround Middle SymbolMid (-1) 1


