$$include '../../meta/macros.ptl'

import [mix linreg clamp fallback distP] from "@iosevka/util"
import [Box] from "@iosevka/geometry/box"

glyph-module

glyph-block Symbol-Pictograph-Bold-Symbol : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives

	create-glyph 'boltSymbol' 0x26A1 : glyph-proc
		local sw : [AdviceStroke 3] / 2
		local kInside 0.5
		local kOutside 2.5
		local l : mix SB RightSB 0.1
		local r : mix RightSB SB 0.1
		include : spiro-outline
			corner [mix l r 0.6] PictTop
			corner (l - sw * kInside) (SymbolMid - sw)
			corner (r - sw * kOutside) (SymbolMid - sw)
			corner [mix r l 0.6] PictBot
			corner (r + sw * kInside) (SymbolMid + sw)
			corner (l + sw * kOutside) (SymbolMid + sw)

	create-glyph 'cautionSign' 0x2621 : glyph-proc
		local sw   Stroke
		local box : new Box (CAP - 0.5 * sw) (0.5 * sw) SB RightSB
		local angleSize : 1.5 * sw

		include : lift-@ : dispiro [widths.center]
			flat (@x1 = box.l) (@y1 = box.t)
			~~~ [alsoThru.curl.distTillEnd   angleSize important]
			virt (@x2 = box.r) (@y2 = [box.yp 0.95])
			~~~ [alsoThru.flat.distFromStart angleSize important]
			~~~ [alsoThru.curl.distTillEnd   angleSize important]
			virt (@x3 = box.l) (@y3 = [box.yp 0.05])
			~~~ [alsoThru.flat.distFromStart angleSize important]
			curl (@x4 = box.r) (@y4 = box.b)
