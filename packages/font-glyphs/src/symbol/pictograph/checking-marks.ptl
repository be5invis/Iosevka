$$include '../../meta/macros.ptl'

import [mix linreg clamp fallback] from "@iosevka/util"

glyph-module

glyph-block Symbol-Pictograph-Checking-Marks : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives
	glyph-block-import Symbol-Geometric-Shared : GeometricDim UnicodeWeightGrade

	for-width-kinds WideWidth1 : do
		define Geom : GeometricDim MosaicUnitWidth MosaicWidth
		define lightSw  : UnicodeWeightGrade 3    Geom.Scalar
		# define mediumSw : UnicodeWeightGrade 5    Geom.Scalar
		define heavySw  : UnicodeWeightGrade 9    Geom.Scalar

		define ptMaxWidth : MosaicWidth - SB
		define ptHeight0 : 1.5 * (Width - SB / 2) * [Math.pow (MosaicWidth / MosaicUnitWidth) (1 / 4)]
		define ptHeight  : ptHeight0 * ptHeight0 / [Math.hypot ptHeight0 : Math.min ptHeight0 ptMaxWidth]
		define ptWidth  : Math.min ptMaxWidth ptHeight
		define ptMidX : MosaicWidth / 2
		define ptSB : (MosaicWidth - ptWidth) / 2
		define ptRSB : MosaicWidth - ptSB
		define ptTop : SymbolMid + ptHeight / 2
		define ptBot : SymbolMid - ptHeight / 2

		foreach [{unicode sw} : items-of {{0x2713 lightSw} {0x2714 heavySw}}] : begin
			create-glyph [MangleName : NameUni unicode] [MangleUnicode unicode] : glyph-proc
				set-width MosaicWidth
				include : dispiro
					widths.center sw
					g4 ptSB ([mix ptBot ptTop 0.46] + 0.5 * sw)
					alsoThru 0.5 0.4
					g4 [mix ptSB ptRSB 0.4] [mix ptBot ptTop 0.05] [heading Downward]
				include : dispiro
					widths.center sw
					g4 [mix ptSB ptRSB 0.4] [mix ptBot ptTop 0.05] [heading Upward]
					alsoThru 0.5 0.6
					g4 ptRSB [mix ptBot ptTop 0.9]

		foreach [{unicode sw} : items-of {{0x2715 lightSw} {0x2716 heavySw}} ] : begin
			create-glyph [MangleName : NameUni unicode] [MangleUnicode unicode] : glyph-proc
				set-width MosaicWidth
				include : dispiro [widths.center sw] [g4 ptRSB ptTop] [g4 ptSB  ptBot]
				include : dispiro [widths.center sw] [g4 ptSB  ptTop] [g4 ptRSB ptBot]

		foreach [{unicode sw} : items-of {{0x2717 lightSw} {0x2718 heavySw}} ] : begin
			create-glyph [MangleName : NameUni unicode] [MangleUnicode unicode] : glyph-proc
				set-width MosaicWidth
				include : dispiro
					widths.center sw
					g4 [mix ptSB ptRSB 1.05] [mix SymbolMid ptTop 1.02]
					alsoThru 0.53 0.45
					g4 [mix ptSB ptRSB (-0.05)] [mix SymbolMid ptBot 1.05]
				include : dispiro
					widths.center sw
					g4 ptSB [mix ptBot ptTop 0.96]
					alsoThru 0.47 0.5
					g4 ptRSB [mix ptBot ptTop 0.05]

		create-glyph [MangleName 'halfCheck'] [MangleUnicode 0x237B] : glyph-proc
			set-width MosaicWidth
			include : refer-glyph : MangleName : NameUni 0x2713
			include : dispiro
				widths.center lightSw
				g4 [mix ptSB ptRSB 0.5] [mix ptBot ptTop 0.82]
				alsoThru 0.47 0.5
				g4 ptRSB [mix ptBot ptTop 0.42]

	do "Ext angle brackets"
		define Geom : GeometricDim Width Width
		define mediumSw : UnicodeWeightGrade 5    Geom.Scalar
		define heavySw  : UnicodeWeightGrade 9    Geom.Scalar
		define xHeavySw : UnicodeWeightGrade 11   Geom.Scalar

		define ExtBracketConfig : list
			list 0x276C mediumSw 1
			list 0x276D mediumSw 0
			list 0x276E heavySw  1
			list 0x276F heavySw  0
			list 0x2770 xHeavySw 1
			list 0x2771 xHeavySw 0
		foreach {unicode sw op} [items-of ExtBracketConfig] : begin
			create-glyph [NameUni unicode] unicode : glyph-proc
				local cor : DiagCor (PictTop - PictBot) (2 * (RightSB - SB))
				include : dispiro
					widths.center (sw * cor)
					corner [mix (SB + sw / 2) (RightSB - sw / 2) op] PictTop [heading Downward]
					corner [mix (SB + sw / 2) (RightSB - sw / 2) (1 - op)] SymbolMid [heading Downward]
					corner [mix (SB + sw / 2) (RightSB - sw / 2) op] PictBot [heading Downward]
