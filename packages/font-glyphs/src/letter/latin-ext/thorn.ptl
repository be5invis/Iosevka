$$include '../../meta/macros.ptl'

import [mix linreg clamp fallback SuffixCfg] from "@iosevka/util"

glyph-module

glyph-block Letter-Latin-Thorn : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives
	glyph-block-import Mark-Adjustment : LeaningAnchor
	glyph-block-import Letter-Shared-Shapes : OBarLeft LetterBarOverlay SerifFrame
	glyph-block-import Letter-Latin-Lower-P

	define xThornLeftStroke : SB * 1.25

	define [yThornBowlAsymmetricBot top slab] : top * 0.25 + [if slab (Stroke * 0.125) 0]
	define [yThornBowlAsymmetricTop top slab] : top - 0.7 * [yThornBowlAsymmetricBot top slab] + [if slab (-0.125) 0.25] * Stroke

	define [yThornBowlSymmetricBot top slab] : mix [yThornBowlAsymmetricBot top slab] (top - [yThornBowlAsymmetricTop top slab]) 0.5
	define [yThornBowlSymmetricTop top slab] : top - [yThornBowlSymmetricBot top slab]

	define [ThornShapeImpl top slabTop slabBot yBowlBot yBowlTop] : glyph-proc
		local turn : mix yBowlTop yBowlBot (ArchDepthB / (ArchDepthA + ArchDepthB))
		local turnRadius : (yBowlTop - yBowlBot) / 2

		include : dispiro
			widths.rhs
			flat xThornLeftStroke yBowlTop [heading Rightward]
			curl [arch.adjust-x.top (RightSB - turnRadius)] yBowlTop
			archv
			g4   (RightSB - O) turn
			arcvh
			flat [arch.adjust-x.bot (RightSB - turnRadius)] yBowlBot
			curl xThornLeftStroke yBowlBot [heading Leftward]

		include : VBar.l xThornLeftStroke 0 top
		if slabTop : begin
			local sf : SerifFrame top 0 xThornLeftStroke RightSB
			if slabBot
				then : include : composite-proc sf.lt.fullSide sf.lb.fullSide
				else : include   sf.lt.outer

		include : LeaningAnchor.Above.VBar.l xThornLeftStroke
		include : LeaningAnchor.Below.VBar.l xThornLeftStroke

	define [ThornAsymmetricShape top slabTop slabBot] : ThornShapeImpl top slabTop slabBot
		yThornBowlAsymmetricBot top slabBot
		yThornBowlAsymmetricTop top slabBot

	define [ThornSymmetricShape top slabTop slabBot] : ThornShapeImpl top slabTop slabBot
		yThornBowlSymmetricBot top slabBot
		yThornBowlSymmetricTop top slabBot

	define ThornConfig : SuffixCfg.weave
		object # symmetry
			""            ThornSymmetricShape
			asymmetric    ThornAsymmetricShape
		object # serifs
			serifless     { false false }
			motionSerifed { true  false }
			serifed       { true  true  }

	foreach { suffix { Body { doST doSB } } } [Object.entries ThornConfig] : do
		create-glyph "Thorn.\(suffix)" : glyph-proc
			include : MarkSet.capital
			include : Body CAP doST doSB

		if (Body !== ThornAsymmetricShape) : begin
			create-glyph "ThornStroke.\(suffix)" : glyph-proc
				include : MarkSet.capital
				include : ThornShapeImpl CAP doST doSB
					CAP - [yThornBowlAsymmetricTop CAP doSB]
					CAP - [yThornBowlAsymmetricBot CAP doST]
				include : LetterBarOverlay.l.in
					x   -- xThornLeftStroke
					top -- (CAP - [if doST Stroke 0])
					bot -- (CAP - [yThornBowlAsymmetricBot CAP doST])

		if (Body === ThornAsymmetricShape) : begin
			create-glyph "ThornStrokeBottom.\(suffix)" : glyph-proc
				include [refer-glyph "Thorn.\(suffix)"] AS_BASE ALSO_METRICS
				include : LetterBarOverlay.l.in
					x   -- xThornLeftStroke
					bot -- (0 + [if doSB Stroke 0])
					top -- (0 + [yThornBowlAsymmetricBot CAP doSB])

	select-variant 'Thorn' 0xDE
	select-variant 'ThornStroke' 0xA764
	select-variant 'ThornStrokeBottom' 0xA766

	create-glyph 'grek/Sho' 0x3F7 : glyph-proc
		include : MarkSet.capital
		include : ThornSymmetricShape CAP SLAB SLAB
