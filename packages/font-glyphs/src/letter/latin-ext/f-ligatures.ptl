$$include '../../meta/macros.ptl'

import [mix linreg clamp fallback] from "@iosevka/util"


glyph-module

glyph-block Letter-Latin-F-ligatures : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives
	glyph-block-import Letter-Latin-Long-S

	define fbar : XH * DesignParameters.fBarPosToXH + Stroke * DesignParameters.fbarStrokeAdj
	define shift (-(Width * 0.055 + SB * 0.5))
	define barr RightSB
	define hbarleft (SB + shift + Stroke * [if para.slopeAngle 0.5 0.25])

	create-glyph 'f_i.upright' : glyph-proc
		include : MarkSet.b
		include : new-glyph : glyph-proc
			include : refer-glyph "longs.bentHookSerifless"
			include : Translate shift 0
		include : VBar.r barr 0 fbar
		include : HBar.t hbarleft barr fbar
		if SLAB : include : tagged 'serifRB' : HSerif.mb (barr - [HSwToV HalfStroke]) 0 Jut

	create-glyph 'f_i.italic' : glyph-proc
		include : MarkSet.b
		include : new-glyph : glyph-proc
			include : refer-glyph "longs.flatHookTailed"
			include : Translate shift 0
		include : VBar.r barr 0 fbar
		include : HBar.t hbarleft barr fbar
		if SLAB : include : tagged 'serifRB' : HSerif.rb barr 0 SideJut

	create-glyph 'f_l.upright' : glyph-proc
		local m : Middle - JBalance - [HSwToV HalfStroke] + shift
		include : dispiro
			widths.rhs
			flat m 0 [heading Upward]
			curl m (Ascender - ArchDepthA)
			hookend Ascender
			g4 (barr + OXHook) (Ascender - Hook)
		include : VBar.r barr 0 (Ascender - Hook)
		include : dispiro
			widths.rhs
			flat ([Math.min (m - Stroke * 0.3) hbarleft] - TanSlope * HalfStroke) fbar
			curl ([mix SB barr 0.6] - TanSlope * HalfStroke) fbar
		if SLAB : begin
			include : tagged 'serifLB' : HSerif.mb (m + [HSwToV HalfStroke] + RBalance * 0.35) 0 (Jut + RBalance * 0.65)
			include : tagged 'serifRB' : HSerif.mb (barr - [HSwToV HalfStroke]) 0 Jut

	create-glyph 'f_l.italic' : glyph-proc
		include : new-glyph : glyph-proc
			include [refer-glyph 'longs.flatHookTailed'] AS_BASE
			include : Translate shift 0
		include : VBar.r barr 0 Ascender
		include : HBar.t hbarleft [mix SB barr 0.65] fbar
		if SLAB : include : tagged 'serifRB' : HSerif.rb barr 0 SideJut
