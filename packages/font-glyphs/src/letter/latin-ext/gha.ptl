$$include '../../meta/macros.ptl'

import [mix linreg clamp fallback SuffixCfg] from "@iosevka/util"


glyph-module

glyph-block Letter-Latin-Gha : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives
	glyph-block-import Letter-Shared-Shapes : RightwardTailedBar
	glyph-block-import Letter-Latin-Lower-Q : RDiagTailedBar
	glyph-block-import Mark-Adjustment : LeaningAnchor

	define TERMINAL-NORMAL  0
	define TERMINAL-TAILED  1
	define TERMINAL-DIAG    2

	define [GhaShape df terminal top bot _ada _adb fSlab] : glyph-proc
		local sw : Math.min df.mvs : AdviceStroke 2 : 0.75 * df.adws
		local gap : 0.375 * (df.width - 2 * df.leftSB - 2.5 * sw) - [HSwToV : 0.25 * sw]
		local divSub : (df.width - gap - sw) / Width
		local subDf : DivFrame divSub 2

		local ada : _ada * 0.75 * df.adws
		local adb : _adb * 0.75 * df.adws
		include : OShape top 0 subDf.leftSB subDf.rightSB sw ada adb
		include : match terminal
			[Just TERMINAL-NORMAL] : VBar.r             (df.rightSB - OX) bot top sw
			[Just TERMINAL-TAILED] : RightwardTailedBar (df.rightSB - OX) bot top sw
			[Just TERMINAL-DIAG]   : RDiagTailedBar     (df.rightSB - OX) bot top sw
		include : dispiro
			widths.lhs sw
			flat (subDf.rightSB - [HSwToV : 0.5 * sw])        (top - adb) [heading Rightward]
			curl (subDf.rightSB - [HSwToV : 0.5 * sw] + TINY) (top - adb) [heading Rightward]
			alsoThru 0.5 0.15
			g4   ((df.rightSB - OX) - [HSwToV sw]) top [widths.rhs sw]

		if fSlab : include : tagged 'serifRB' : union
			HSerif.rb ((df.rightSB - OX) - [HSwToV : 0.5 * sw]) bot (Jut        - [HSwToV : 0.5 * (Stroke - sw)]) sw
			HSerif.lb ((df.rightSB - OX) - [HSwToV : 0.5 * sw]) bot (MidJutSide - [HSwToV : 0.5 * (Stroke - sw)]) sw

		include : LeaningAnchor.Below.VBar.r (df.rightSB - OX) sw

	define GhaConfig : object
		straightSerifless       { TERMINAL-NORMAL false }
		straightBottomSerifed   { TERMINAL-NORMAL true  }
		tailedSerifless         { TERMINAL-TAILED false }
		diagonalTailedSerifless { TERMINAL-DIAG   false }

	foreach { suffix { terminal doSB } } [Object.entries GhaConfig] : do
		create-glyph "Gha.\(suffix)" : glyph-proc
			local df : include : DivFrame para.advanceScaleM 3
			include : df.markSet.capDesc
			include : GhaShape df terminal CAP Descender ArchDepthA ArchDepthB doSB

		create-glyph "gha.\(suffix)" : glyph-proc
			local df : include : DivFrame para.advanceScaleM 3
			include : df.markSet.p
			include : GhaShape df terminal XH Descender SmallArchDepthA SmallArchDepthB doSB

	select-variant 'Gha' 0x1A2 (follow -- 'gha')
	select-variant 'gha' 0x1A3
