$$include '../../meta/macros.ptl'

import [mix linreg clamp fallback] from "@iosevka/util"

glyph-module

glyph-block Letter-Cyrillic-E : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives
	glyph-block-import Letter-Latin-C : CLetterForm CConfig
	glyph-block-import Letter-Cyrillic-Iotified-A : Iotified
	glyph-block-import Letter-Latin-Upper-F : EFVJutLength

	foreach { suffix { sty styBot fMidSerif } } [Object.entries CConfig] : do
		define [CyrlEShepe df top ada adb] : glyph-proc
			local sw : Math.min df.mvs : AdviceStroke2 2 3 top df.adws
			local lf : CLetterForm df sty styBot top 0
				ada -- ada
				adb -- adb
				sw  -- sw
			include : lf.revFull

			local xMidBarLeft : if fMidSerif df.middle : mix df.leftSB df.rightSB 0.35
			include : HBar.m xMidBarLeft ((df.rightSB - OX) + O) (top / 2) sw

			if fMidSerif : begin
				local { jutTop jutBot jutMid } : EFVJutLength top 0.5 sw
				local swVJut : Math.min (VJutStroke * (sw / Stroke)) : VSwToH : 0.8 * ((df.rightSB - df.leftSB) - [HSwToV : 2 * sw])
				include : VBar.m xMidBarLeft (top / 2 - jutMid) (top / 2 + jutMid) swVJut

		create-glyph "cyrl/E.\(suffix)" : glyph-proc
			include : MarkSet.capital
			include : CyrlEShepe [DivFrame 1] CAP ArchDepthA ArchDepthB

		create-glyph "cyrl/e.\(suffix)" : glyph-proc
			include : MarkSet.e
			set-base-anchor 'cvDecompose' 0 0
			include : CyrlEShepe [DivFrame 1] XH SmallArchDepthA SmallArchDepthB

		define [CyrlYeShape df top ada adb] : glyph-proc
			local sw : Math.min df.mvs : AdviceStroke2 2 3 top df.adws
			local lf : CLetterForm df sty styBot top 0
				ada -- ada
				adb -- adb
				sw  -- sw
			include : lf.full

			local xMidBarRight : if fMidSerif df.middle : mix df.rightSB df.leftSB 0.35
			include : HBar.m ((df.leftSB + OX) - O) xMidBarRight (top / 2) sw

			if fMidSerif : begin
				local { jutTop jutBot jutMid } : EFVJutLength top 0.5 sw
				local swVJut : Math.min (VJutStroke * (sw / Stroke)) : VSwToH : 0.8 * ((df.rightSB - df.leftSB) - [HSwToV : 2 * sw])
				include : VBar.m xMidBarRight (top / 2 - jutMid) (top / 2 + jutMid) swVJut

		create-glyph "cyrl/Ye.\(suffix)" : glyph-proc
			include : MarkSet.capital
			include : CyrlYeShape [DivFrame 1] CAP ArchDepthA ArchDepthB

		create-glyph "cyrl/ye.\(suffix)" : glyph-proc
			include : MarkSet.e
			set-base-anchor 'cvDecompose' 0 0
			include : CyrlYeShape [DivFrame 1] XH SmallArchDepthA SmallArchDepthB

		define [IotifiedEShape fCapital df top ada adb] : glyph-proc
			local sw : Math.min df.mvs : AdviceStroke2 2 3 top : 0.75 * df.adws
			local gap : 0.375 * (df.width - 2 * df.leftSB - 2.5 * sw) - [HSwToV : 0.25 * sw]
			define divSub : (df.width - gap - sw) / Width
			define subDf : DivFrame divSub 2

			local lf : CLetterForm subDf sty styBot top 0
				ada -- (ada * 0.75 * df.adws)
				adb -- (adb * 0.75 * df.adws)
				sw  -- sw

			local shift : Width * (df.adws - divSub)
			include : with-transform [ApparentTranslate shift 0] : lf.full

			local xMidBarRight : [if fMidSerif subDf.middle : mix subDf.rightSB subDf.leftSB 0.35] + shift
			include : Iotified.full df top
				hBarRight -- xMidBarRight
				hBarY     -- (top / 2)
				fCapital  -- fCapital
				swRef     -- sw

			if fMidSerif : begin
				local { jutTop jutBot jutMid } : EFVJutLength top 0.5 sw
				local swVJut : Math.min (VJutStroke * (sw / Stroke)) : VSwToH : 0.8 * ((subDf.rightSB - subDf.leftSB) - [HSwToV : 2 * sw])
				include : VBar.m xMidBarRight (top / 2 - jutMid) (top / 2 + jutMid) swVJut

		create-glyph "cyrl/EIotified.\(suffix)" : glyph-proc
			local df : include : DivFrame para.advanceScaleM 3
			include : df.markSet.capital
			include : IotifiedEShape true df CAP ArchDepthA ArchDepthB

		create-glyph "cyrl/eIotified.\(suffix)" : glyph-proc
			local df : include : DivFrame para.advanceScaleM 3
			include : df.markSet.e
			include : IotifiedEShape false df XH SmallArchDepthA SmallArchDepthB

	select-variant 'cyrl/E'  0x42D
	select-variant 'cyrl/e'  0x44D
	select-variant 'cyrl/Ye' 0x404
	select-variant 'cyrl/ye' 0x454
	select-variant "cyrl/EIotified" 0x464 (follow -- 'cyrl/Ye')
	select-variant "cyrl/eIotified" 0x465 (follow -- 'cyrl/ye')
