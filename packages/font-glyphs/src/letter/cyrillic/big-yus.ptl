$$include '../../meta/macros.ptl'

import [mix linreg clamp fallback] from "@iosevka/util"

glyph-module

glyph-block Letter-Cyrillic-BigYus : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives
	glyph-block-import Letter-Shared-Shapes : SerifFrame
	glyph-block-import Letter-Cyrillic-Iotified-A : Iotified

	define [BigYusLegMaskShape df top pyBar _fine] : begin
		local fine : fallback _fine : Math.min df.mvs : AdviceStroke 3.3 df.adws
		local xMid : mix df.leftSB df.middle 0.3
		local yBar : mix 0 top pyBar
		return : spiro-outline
			corner (df.leftSB + O * 2)              0
			corner (df.width - (df.leftSB + O * 2)) 0
			corner (df.width - xMid)                yBar
			corner xMid yBar

	define [CyrBigYusShape df top pyBar _fine] : glyph-proc
		local fine : fallback _fine : Math.min df.mvs : AdviceStroke 3.3 df.adws
		local xMid : mix df.leftSB df.middle 0.3
		local xMid2 : Math.min (df.middle - [HSwToV fine]) : mix df.leftSB df.middle 0.75
		local yBar : mix 0 top pyBar

		include : union
			spiro-outline
				corner (0        + (df.leftSB + O * 2) + 0)             0
				corner (0        + (df.leftSB + O * 2) + [HSwToV fine]) 0
				corner (0        + xMid                + [HSwToV fine]) yBar
				corner (0        + xMid                + 0)             yBar
			spiro-outline
				corner (0        + xMid2               + [HSwToV fine]) yBar
				corner (0        + (df.leftSB + O * 2) + [HSwToV fine]) top
				corner (0        + (df.leftSB + O * 2) + 0)             top
				corner (0        + xMid2               + 0)             yBar
			spiro-outline
				corner (df.width - (df.leftSB + O * 2) - 0)             0
				corner (df.width - (df.leftSB + O * 2) - [HSwToV fine]) 0
				corner (df.width - xMid                - [HSwToV fine]) yBar
				corner (df.width - xMid                - 0)             yBar
			spiro-outline
				corner (df.width - xMid2               - [HSwToV fine]) yBar
				corner (df.width - (df.leftSB + O * 2) - [HSwToV fine]) top
				corner (df.width - (df.leftSB + O * 2) - 0)             top
				corner (df.width - xMid2               - 0)             yBar
			HBar.t xMid (df.width - xMid) yBar fine
			HBar.t
				0        + (df.leftSB + O * 2) + [HSwToV : 0.99 * fine]
				df.width - (df.leftSB + O * 2) - [HSwToV : 0.99 * fine]
				begin top
				begin fine
		include : tagged 'barM' : VBar.m df.middle 0 yBar fine

		if SLAB : begin
			local sf : SerifFrame.fromDf df top 0 (swSerif -- fine)
			include : tagged 'serifLB' sf.lb.outer
			include : tagged 'serifRB' sf.rb.outer
			if sf.enoughSpaceForFullSerifs : include : tagged 'serifMB' sf.mb.full

	define [CyrBlendedYusShape df top pyBar pyBar2 _fine] : glyph-proc
		local fine : fallback _fine : Math.min df.mvs : AdviceStroke 3.3 df.adws
		include : CyrBigYusShape df top pyBar fine
		eject-contour 'barM'

		local yBar2 : mix 0 [mix 0 top pyBar] pyBar2
		include : tagged 'barM' : VBar.m df.middle 0 yBar2 fine
		include : intersection
			HBar.t df.leftSB df.rightSB yBar2 fine
			BigYusLegMaskShape df top pyBar fine

	create-glyph 'cyrl/BigYus' 0x46A : glyph-proc
		local df : include : DivFrame para.advanceScaleM 3
		include : df.markSet.capital
		include : CyrBigYusShape df CAP 0.575

	create-glyph 'cyrl/bigYus' 0x46B : glyph-proc
		local df : include : DivFrame 1 3
		include : df.markSet.e
		include : CyrBigYusShape df XH 0.55

	create-glyph 'cyrl/BlendedYus' 0xA65A : glyph-proc
		local df : include : DivFrame para.advanceScaleM 3
		include : df.markSet.capital
		include : CyrBlendedYusShape df CAP 0.65 0.65

	create-glyph 'cyrl/blendedYus' 0xA65B : glyph-proc
		local df : include : DivFrame 1 3
		include : df.markSet.e
		include : CyrBlendedYusShape df XH 0.625 0.65

	define [CyrIotifiedBigYusShape fCapital df top pyBar] : glyph-proc
		local fine : Math.min df.mvs : AdviceStroke 3.3 (df.adws * [if fCapital 0.80 0.75])
		local gap : (df.rightSB - df.leftSB) / 3 - fine / 3
		local subDf : DivFrame [Math.min ((df.width - gap) / Width) (df.adws * [if fCapital 0.80 0.75])] 3
		include : CyrBigYusShape subDf top pyBar fine
		include : Translate (df.width - subDf.width) 0

		include : Iotified.outer df top
			hBarRight -- [mix df.leftSB df.rightSB (2 / 3)]
			hBarY     -- ([mix 0 top pyBar] - fine / 2)
			fCapital  -- fCapital
			swRef     -- fine

	create-glyph 'cyrl/BigYusIotified' 0x46C : glyph-proc
		local df : include : DivFrame [mix 1 para.advanceScaleM 2] 4.25
		include : df.markSet.capital
		include : CyrIotifiedBigYusShape true df CAP 0.575

	create-glyph 'cyrl/bigYusIotified' 0x46D : glyph-proc
		local df : include : DivFrame para.advanceScaleM 4.25
		include : df.markSet.e
		include : CyrIotifiedBigYusShape false df XH 0.55
