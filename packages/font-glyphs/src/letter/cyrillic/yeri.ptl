$$include '../../meta/macros.ptl'

import [mix linreg clamp fallback] from "@iosevka/util"

glyph-module

glyph-block Letter-Cyrillic-Yeri : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives
	glyph-block-import Letter-Shared-Metrics : BowlXDepth
	glyph-block-import Letter-Shared-Shapes : RightwardTailedBar
	glyph-block-import Letter-Shared-Shapes : LetterBarOverlay UpwardHookShape VerticalHook SerifFrame

	glyph-block-export YeriBarPos
	define YeriBarPos 0.55

	define Yeri : namespace
		define flex-params [CornerCommon] : glyph-proc
			local-parameter : top
			local-parameter : left   -- SB
			local-parameter : right  -- RightSB
			local-parameter : stroke -- Stroke
			local-parameter : jut    -- Jut
			local-parameter : pBar   -- YeriBarPos
			local-parameter : yStart -- top

			local bowl : [mix 0 top pBar] + stroke / 2
			local turnRadius : BowlXDepth bowl 0 left right stroke
			local ada : ArchDepthAOf ArchDepth : (right - left) + SB * 2
			local adb : ArchDepthBOf ArchDepth : (right - left) + SB * 2
			local fine : ShoulderFine * (stroke / Stroke)

			local yTurnBottomL : YSmoothMidL bowl 0 ada adb
			local yTurnBottomR : YSmoothMidR bowl 0 ada adb

			include : union [VBar.l left 0 yStart stroke] : dispiro
				widths.lhs stroke
				flat (left + Stroke * 0.2) 0 [heading Rightward]
				curl [arch.adjust-x.bot [Math.max ((left + Stroke * 0.2) + TINY + [Math.abs : stroke * TanSlope]) (right - turnRadius)] (sw -- stroke)] 0
				archv 8
				g4   (right - OX) yTurnBottomR
				arcvh 8
				flat [arch.adjust-x.top [Math.max ((left + Stroke * 0.2) + TINY + [Math.abs : stroke * TanSlope]) (right - turnRadius)] (sw -- stroke)] bowl
				curl (left + Stroke * 0.2) bowl [heading Leftward]

		export : define flex-params [UprightShape] : glyph-proc
			local-parameter : top
			local-parameter : left   -- SB
			local-parameter : right  -- RightSB
			local-parameter : stroke -- Stroke
			local-parameter : jut    -- Jut
			local-parameter : pBar   -- YeriBarPos
			local-parameter : yStart -- top

			include : CornerCommon.apply null $-flex-arguments
			if SLAB : begin
				include : tagged 'serifYeriLB' : HSerif.lb left 0 (jut - [HSwToV : 0.5 * stroke]) stroke
				include : tagged 'serifYeriLT' : HSerif.mt (left + [HSwToV : 0.5 * stroke]) top jut stroke

		export : define flex-params [ItalicShape] : glyph-proc
			local-parameter : top
			local-parameter : left   -- SB
			local-parameter : right  -- RightSB
			local-parameter : stroke -- Stroke
			local-parameter : jut    -- Jut
			local-parameter : pBar   -- YeriBarPos
			local-parameter : yStart -- top

			include : CornerCommon.apply null $-flex-arguments
			if SLAB : begin
				include : tagged 'serifYeriLT' : HSerif.lt left top (jut - [HSwToV : 0.5 * stroke]) stroke

		export : define flex-params [AutoItalicShape] : begin
			if para.isItalic
			: then : ItalicShape.apply null $-flex-arguments
			: else : UprightShape.apply null $-flex-arguments

		export : define flex-params [RoundShape] : glyph-proc
			local-parameter : top
			local-parameter : left   -- SB
			local-parameter : right  -- RightSB
			local-parameter : stroke -- Stroke
			local-parameter : jut    -- Jut
			local-parameter : pBar   -- YeriBarPos
			local-parameter : yStart -- top

			local bowl : [mix 0 top pBar] + stroke / 2
			local turnRadius : BowlXDepth bowl 0 left right stroke
			local ada : ArchDepthAOf ArchDepth : (right - left) + SB * 2
			local adb : ArchDepthBOf ArchDepth : (right - left) + SB * 2
			local fine : ShoulderFine * (stroke / Stroke)

			local yTurnBottomL : YSmoothMidL bowl 0 ada adb
			local yTurnBottomR : YSmoothMidR bowl 0 ada adb

			include : dispiro
				widths.lhs stroke
				flat left yStart [heading Downward]
				curl left [Math.min (yStart - TINY) yTurnBottomL]
				arch.lhs 0 (sw -- stroke)
				g4   (right - OX) yTurnBottomR
				arcvh
				flat [arch.adjust-x.top [mix left right 0.5] (sw -- stroke)] bowl
				curl (left + Stroke * 0.2) bowl [heading Leftward]

			if SLAB : begin
				include : tagged 'serifYeriLT' : HSerif.lt left top (jut - [HSwToV : 0.5 * stroke]) stroke

		export : define flex-params [CursiveShape] : glyph-proc
			local-parameter : top
			local-parameter : left   -- SB
			local-parameter : right  -- RightSB
			local-parameter : stroke -- Stroke
			local-parameter : jut    -- Jut
			local-parameter : pBar   -- YeriBarPos
			local-parameter : yStart -- top

			local bowl : [mix 0 top pBar] + stroke / 2
			local turnRadius : BowlXDepth bowl 0 left right stroke
			local ada : ArchDepthAOf ArchDepth : (right - left) + SB * 2
			local adb : ArchDepthBOf ArchDepth : (right - left) + SB * 2
			local fine : ShoulderFine * (stroke / Stroke)

			local yTurnBottomL : YSmoothMidL bowl 0 ada adb
			local yTurnBottomR : YSmoothMidR bowl 0 ada adb

			include : dispiro
				widths.lhs stroke
				flat left yStart [heading Downward]
				curl left [Math.min (yStart - TINY) yTurnBottomL]
				arch.lhs 0 (sw -- stroke)
				g4   (right - OX) yTurnBottomR
				arch.lhs bowl (sw -- stroke) (swAfter -- fine)
				g4.down.end (left + [HSwToV : stroke - fine]) yTurnBottomL [widths.lhs.heading fine Downward]

			if SLAB : begin
				include : tagged 'serifYeriLT' : HSerif.lt left top (jut - [HSwToV : 0.5 * stroke]) stroke

	define RevYeri : namespace
		export : define flex-params [Shape] : glyph-proc
			local-parameter : top
			local-parameter : left   -- SB
			local-parameter : right  -- RightSB
			local-parameter : stroke -- Stroke
			local-parameter : jut    -- Jut
			local-parameter : pBar   -- YeriBarPos
			local-parameter : yStart -- top

			local bowl : [mix 0 top pBar] + stroke / 2
			local turnRadius : BowlXDepth bowl 0 left right stroke
			local ada : ArchDepthAOf ArchDepth : (right - left) + SB * 2
			local adb : ArchDepthBOf ArchDepth : (right - left) + SB * 2
			local fine : ShoulderFine * (stroke / Stroke)

			local yTurnBottomL : YSmoothMidL bowl 0 ada adb
			local yTurnBottomR : YSmoothMidR bowl 0 ada adb

			include : union [VBar.r right 0 yStart stroke] : dispiro
				widths.rhs stroke
				flat (right - Stroke * 0.2) 0 [heading Leftward]
				curl [arch.adjust-x.bot [Math.min ((right - Stroke * 0.2) - TINY - [Math.abs : stroke * TanSlope]) (left + turnRadius)] (sw -- stroke)] 0
				archv 8
				g4   (left + OX) yTurnBottomL
				arcvh 8
				flat [arch.adjust-x.top [Math.min ((right - Stroke * 0.2) - TINY - [Math.abs : stroke * TanSlope]) (left + turnRadius)] (sw -- stroke)] bowl
				curl (right - Stroke * 0.2) bowl [heading Rightward]
			if SLAB : begin
				include : tagged 'serifYeriRB' : HSerif.rb right 0 (jut - [HSwToV : 0.5 * stroke]) stroke
				include : tagged 'serifYeriRT' : HSerif.mt (right - [HSwToV : 0.5 * stroke]) top jut stroke

		export : define flex-params [RoundShape] : glyph-proc
			local-parameter : top
			local-parameter : left   -- SB
			local-parameter : right  -- RightSB
			local-parameter : stroke -- Stroke
			local-parameter : jut    -- Jut
			local-parameter : pBar   -- YeriBarPos
			local-parameter : yStart -- top

			local bowl : [mix 0 top pBar] + stroke / 2
			local turnRadius : BowlXDepth bowl 0 left right stroke
			local ada : ArchDepthAOf ArchDepth : (right - left) + SB * 2
			local adb : ArchDepthBOf ArchDepth : (right - left) + SB * 2
			local fine : ShoulderFine * (stroke / Stroke)

			local yTurnBottomL : YSmoothMidL bowl 0 ada adb
			local yTurnBottomR : YSmoothMidR bowl 0 ada adb

			include : dispiro
				widths.rhs stroke
				flat right yStart [heading Downward]
				curl right [Math.min (yStart - TINY) yTurnBottomR]
				arch.rhs 0 (sw -- stroke)
				g4   (left + OX) yTurnBottomL
				arcvh
				flat [arch.adjust-x.top [mix left right 0.5] (sw -- stroke)] bowl
				curl (right - Stroke * 0.2) bowl [heading Rightward]

	define flex-params [CyrYerShape] : glyph-proc
		local-parameter : Base
		local-parameter : top
		local-parameter : left   -- SB
		local-parameter : right  -- RightSB
		local-parameter : stroke -- [AdviceStroke 2.75 : ((right - left) + SB * 2) / Width]
		local-parameter : jut    -- (Jut - [HSwToV : 0.5 * (Stroke - stroke)])
		local-parameter : pBar   -- YeriBarPos

		local xLeftBarLeftEdge : Math.max (right - (RightSB - SB)) : if SLAB
			[mix left right 0.35] - [HSwToV : 0.50 * stroke]
			[mix left right 0.20] - [HSwToV : 0.25 * stroke]
		local xTopBarLeftEnd : mix 0 left : if SLAB 0.250 0.375

		include : HBar.t xTopBarLeftEnd (xLeftBarLeftEdge + Stroke * 0.1) top stroke
		include : Base top
			left   -- xLeftBarLeftEdge
			right  -- right
			stroke -- stroke
			jut    -- jut
			pBar   -- pBar

		if SLAB : begin
			local swVJut : Math.min (VJutStroke * (stroke / Stroke)) : VSwToH : 0.625 * (xLeftBarLeftEdge - xTopBarLeftEnd)
			include : VSerif.dl xTopBarLeftEnd top VJut swVJut

	define flex-params [CyrNeutralYerShape] : glyph-proc
		local-parameter : Base
		local-parameter : top
		local-parameter : left   -- SB
		local-parameter : right  -- RightSB
		local-parameter : stroke -- [AdviceStroke 2.75 : ((right - left) + SB * 2) / Width]
		local-parameter : jut    -- (Jut - [HSwToV : 0.5 * (Stroke - stroke)])
		local-parameter : pBar   -- YeriBarPos

		local xLeftBarLeftEdge : Math.max (right - (RightSB - SB)) : if SLAB
			[mix left right 0.35] - [HSwToV : 0.50 * stroke]
			[mix left right 0.20] - [HSwToV : 0.25 * stroke]
		local xTopBarLeftEnd : mix 0 left : if SLAB 0.250 0.375

		include : Base top
			left   -- xLeftBarLeftEdge
			right  -- right
			stroke -- stroke
			jut    -- jut
			pBar   -- pBar
			yStart -- (top - TailY - 0.5 * stroke)
		eject-contour 'serifYeriLT'
		include : VerticalHook.l xLeftBarLeftEdge (top - TailY - 0.5 * stroke) (xTopBarLeftEnd - xLeftBarLeftEdge - 0.5 * stroke) (-TailY) stroke

	define [CyrYeryShape Base df top fTailed fBackYer] : glyph-proc
		local stroke : Math.min df.mvs : if fBackYer
			AdviceStroke 2.75 : df.adws * (7 / 9)
			AdviceStroke 2.00 : df.adws * (3 / 4)
		local jut : Jut - [HSwToV : 0.5 * (Stroke - stroke)]

		local xMiddleSym : mix 0 df.width : if fBackYer (5 / 9) (1 / 2)
		local xMiddle : Math.min (df.leftSB + (Width * [if fBackYer (7 / 6) 1] - SB * 2))
			mix (xMiddleSym + [HSwToV : 0.5 * stroke]) (df.rightSB - [HSwToV stroke]) 0.25

		include : if fBackYer
			CyrYerShape Base top
				left   -- df.leftSB
				right  -- xMiddle
				stroke -- stroke
				jut    -- jut
			Base top
				left   -- df.leftSB
				right  -- xMiddle
				stroke -- stroke
				jut    -- jut

		include : if fTailed
			RightwardTailedBar df.rightSB 0 top stroke
			VBar.r             df.rightSB 0 top stroke

		if SLAB : begin
			local useFullSerifs : Base === Yeri.UprightShape || (Base === Yeri.AutoItalicShape && [not para.isItalic])

			if useFullSerifs : include : tagged 'serifRT' : HSerif.mt (df.rightSB - [HSwToV : 0.5 * stroke]) top jut stroke
			if [not fTailed] : include : tagged 'serifRB' : if useFullSerifs
				HSerif.mb (df.rightSB - [HSwToV : 0.5 * stroke]) 0 jut stroke
				HSerif.rb df.rightSB 0 (jut - [HSwToV : 0.5 * stroke]) stroke

	define [ZhuangToneSixShape Base top] : glyph-proc
		local xLeft : [mix SB RightSB 0.20] - [HSwToV : 0.125 * Stroke]
		include : Base top (left -- xLeft) (right -- RightSB)
		eject-contour 'serifYeriLT'
		include : dispiro
			widths.lhs [AdviceStroke 5.5]
			corner xLeft top
			corner [mix 0 xLeft 0.25] (top - xLeft)

	glyph-block-export YeriConfig
	define YeriConfig : object
		corner        { Yeri.UprightShape Yeri.AutoItalicShape }
		round         { Yeri.RoundShape   Yeri.RoundShape      }
		cursive       { Yeri.CursiveShape Yeri.CursiveShape    }

	foreach { suffix { Uc Lc } } [Object.entries YeriConfig] : do
		create-glyph "cyrl/Yeri.\(suffix)" : glyph-proc
			local df : include : DivFrame 1
			include : df.markSet.capital
			include : Uc CAP
				left  -- df.leftSB
				right -- df.rightSB
		create-glyph "cyrl/yeri.\(suffix)" : glyph-proc
			local df : include : DivFrame 1
			include : df.markSet.e
			include : Lc XH
				left  -- df.leftSB
				right -- df.rightSB
		create-glyph "cyrl/YeriBar.\(suffix)" : glyph-proc
			local df : include : DivFrame 1
			include : df.markSet.capital
			include : Uc CAP
				left  -- df.leftSB
				right -- df.rightSB
			include : LetterBarOverlay.l.in
				x   -- df.leftSB
				bot -- (CAP * YeriBarPos + HalfStroke)
				top -- (CAP - [if SLAB Stroke 0])
		create-glyph "cyrl/yeriBar.\(suffix)" : glyph-proc
			local df : include : DivFrame 1
			include : df.markSet.e
			include : Lc XH
				left  -- df.leftSB
				right -- df.rightSB
			include : LetterBarOverlay.l.in
				x   -- df.leftSB
				bot -- (XH * YeriBarPos + HalfStroke)
				top -- (XH - [if SLAB Stroke 0])
		create-glyph "cyrl/Yer.\(suffix)" : glyph-proc
			local df : include : DivFrame para.advanceScaleT 2
			include : df.markSet.capital
			include : CyrYerShape Uc CAP
				left  -- df.leftSB
				right -- df.rightSB
		create-glyph "cyrl/yer.\(suffix)" : glyph-proc
			local df : include : DivFrame para.advanceScaleT 2
			include : df.markSet.e
			include : CyrYerShape Lc XH
				left  -- df.leftSB
				right -- df.rightSB
		create-glyph "cyrl/yerTall.\(suffix)" : glyph-proc
			local df : include : DivFrame para.advanceScaleT 2
			include : df.markSet.b
			include : CyrYerShape Lc Ascender
				left  -- df.leftSB
				right -- df.rightSB
				pBar  -- (YeriBarPos * (XH / Ascender))
		create-glyph "cyrl/YerNeutral.\(suffix)" : glyph-proc
			local df : include : DivFrame para.advanceScaleT 2
			include : df.markSet.capital
			include : CyrNeutralYerShape Uc CAP
				left  -- df.leftSB
				right -- df.rightSB
		create-glyph "cyrl/yerNeutral.\(suffix)" : glyph-proc
			local df : include : DivFrame para.advanceScaleT 2
			include : df.markSet.e
			include : CyrNeutralYerShape Lc XH
				left  -- df.leftSB
				right -- df.rightSB
		create-glyph "ZhuangToneSix.\(suffix)" : glyph-proc
			include : MarkSet.capital
			include : ZhuangToneSixShape Uc CAP
		create-glyph "zhuangToneSix.\(suffix)" : glyph-proc
			include : MarkSet.e
			include : ZhuangToneSixShape Lc XH

	define YeryConfig : object
		corner        { Yeri.UprightShape Yeri.AutoItalicShape false }
		round         { Yeri.RoundShape   Yeri.RoundShape      false }
		cursive       { Yeri.CursiveShape Yeri.CursiveShape    false }
		cornerTailed  { Yeri.UprightShape Yeri.AutoItalicShape true  }
		roundTailed   { Yeri.RoundShape   Yeri.RoundShape      true  }
		cursiveTailed { Yeri.CursiveShape Yeri.CursiveShape    true  }

	foreach { suffix { Uc Lc fTailed } } [Object.entries YeryConfig] : do
		if [not fTailed] : create-glyph "cyrl/Yery.\(suffix)" : glyph-proc
			local df : include : DivFrame para.advanceScaleM 3
			include : df.markSet.capital
			include : CyrYeryShape Uc df CAP fTailed false
		create-glyph "cyrl/yery.\(suffix)" : glyph-proc
			local df : include : DivFrame para.advanceScaleM 3
			include : df.markSet.e
			include : CyrYeryShape Lc df XH fTailed false
		if [not fTailed] : create-glyph "cyrl/YeryBack.\(suffix)" : glyph-proc
			local df : include : DivFrame para.advanceScaleMM 3
			include : df.markSet.capital
			include : CyrYeryShape Uc df CAP fTailed true
		create-glyph "cyrl/yeryBack.\(suffix)" : glyph-proc
			local df : include : DivFrame para.advanceScaleMM 3
			include : df.markSet.e
			include : CyrYeryShape Lc df XH fTailed true

	select-variant 'cyrl/Yer'  0x42A
	select-variant 'cyrl/yer'  0x44A
	select-variant 'cyrl/yer.BGR' (shapeFrom -- 'cyrl/yer')
	select-variant 'cyrl/yerTall' 0x1C86 (follow -- 'cyrl/yer')

	select-variant 'cyrl/Yery' 0x42B
	select-variant 'cyrl/yery' 0x44B
	select-variant 'cyrl/Yeri' 0x42C
	select-variant 'cyrl/yeri' 0x44C
	select-variant 'cyrl/yeri.BGR' (shapeFrom -- 'cyrl/yeri')
	select-variant 'cyrl/YeriBar' 0x48C (follow -- 'cyrl/Yeri')
	select-variant 'cyrl/yeriBar' 0x48D (follow -- 'cyrl/yeri')
	select-variant 'cyrl/YerNeutral' 0xA64E (follow -- 'cyrl/Yer')
	select-variant 'cyrl/yerNeutral' 0xA64F (follow -- 'cyrl/yer')
	select-variant 'cyrl/YeryBack' 0xA650
	select-variant 'cyrl/yeryBack' 0xA651

	select-variant 'ZhuangToneSix' 0x184 (follow -- 'cyrl/Yer')
	select-variant 'zhuangToneSix' 0x185 (follow -- 'cyrl/yer')

	create-glyph 'cyrl/Be' 0x411 : glyph-proc
		include : MarkSet.capital
		include : Yeri.UprightShape CAP
		include : HBar.m SB [mix SB RightSB 0.9] (CAP - HalfStroke)
		if SLAB : begin
			include : VSerif.dr [mix SB RightSB 0.9] CAP VJut

	alias 'latn/Be' 0x182 'cyrl/Be'

	create-glyph 'latn/De' 0x18B : glyph-proc
		include : MarkSet.capital
		include : RevYeri.Shape CAP
		include : HBar.t [mix RightSB SB 0.9] (RightSB + O) CAP
		if SLAB : begin
			include : VSerif.dl [mix RightSB SB 0.9] CAP VJut

	create-glyph 'cyrl/DeKomi' 0x500 : glyph-proc
		include : MarkSet.capital
		include : RevYeri.Shape CAP

	create-glyph 'cyrl/DjeKomi' 0x502 : glyph-proc
		local df : include : DivFrame para.advanceScaleMM 3
		include : df.markSet.capital

		include : RevYeri.RoundShape CAP df.leftSB (df.middle + [HSwToV : 0.5 * df.mvs]) df.mvs
		include : UpwardHookShape
			left   -- (df.middle - [HSwToV : 0.5 * df.mvs])
			right  -- df.rightSB
			ybegin -- CAP
			yend   -- (CAP / 2 + HalfStroke)
			ada    -- (ArchDepthA * 0.6 * df.adws)
			adb    -- (ArchDepthB * 0.6 * df.adws)
			sw     -- df.mvs
		if SLAB : begin
			include : HSerif.mt df.middle CAP Jut df.mvs
			local sf2 : [SerifFrame.fromDf df (CAP / 2 + HalfStroke) 0].slice 1 2
			include sf2.rt.full
