$$include '../../meta/macros.ptl'

import [mix linreg clamp fallback] from "@iosevka/util"
import [DependentSelector] from "@iosevka/glyph/relation"

glyph-module

glyph-block Letter-Cyrillic-Yat : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives
	glyph-block-import Mark-Adjustment : ExtendAboveBaseAnchors
	glyph-block-import Letter-Shared : CreateDependentComposite
	glyph-block-import Letter-Latin-Upper-F : EFVJutLength
	glyph-block-import Letter-Cyrillic-Yeri : YeriConfig YeriBarPos
	glyph-block-import Letter-Cyrillic-Iotified-A : Iotified

	define [yYatDefaultCrossbarPos top pBar stroke] : [mix (top * pBar + stroke / 2) [if SLAB (top - stroke) top] [if SLAB 0.625 0.5]] + (OverlayStroke * (stroke / Stroke)) / 2

	define [YatShape] : with-params [df Yeri top [pBar YeriBarPos] [fCapital false] [stroke [Math.min df.mvs : AdviceStroke 2.75 df.adws]] [yCrossbarPos [yYatDefaultCrossbarPos top pBar stroke]] [xCrossbarLeftExt nothing]] : glyph-proc
		local xYeriLeft : Math.max (df.rightSB - (RightSB - SB)) : if SLAB
			Just ([mix df.leftSB df.rightSB 0.35] - [HSwToV : 0.5 * stroke])
			Just  [mix df.leftSB df.rightSB 0.20]
		local xYeriRight : df.rightSB - O * 2

		include : Yeri top
			left   -- xYeriLeft
			right  -- xYeriRight
			pBar   -- pBar
			stroke -- stroke
			jut    -- (Jut - [HSwToV : 0.5 * (Stroke - stroke)])

		local xCrossbarLeftSym : mix 0 df.leftSB : if SLAB 0.25 0.375
		local xCrossbarLeft : fallback xCrossbarLeftExt xCrossbarLeftSym
		local xCrossbarRight : mix xCrossbarLeftSym (xYeriLeft + [HSwToV : 0.5 * stroke]) 2
		include : HBar.t xCrossbarLeft xCrossbarRight yCrossbarPos (OverlayStroke * (stroke / Stroke))

		if SLAB : begin
			local swVJut : Math.min (VJutStroke * (stroke / Stroke)) : VSwToH : 0.625 * (xYeriLeft - xCrossbarLeftSym)
			local { jutTop jutBot jutMid } : EFVJutLength yCrossbarPos ((pBar * top) / yCrossbarPos) stroke
			include : tagged 'serifDL' : VSerif.dl xCrossbarLeft  yCrossbarPos jutTop swVJut
			include : tagged 'serifDR' : VSerif.dr xCrossbarRight yCrossbarPos jutTop swVJut

			if [not fCapital] : begin
				eject-contour 'serifYeriLT'
				include : tagged 'serifLT' : HSerif.lt xYeriLeft top SideJut stroke

	define [IotifiedYatShape] : with-params [df Yeri top [pBar YeriBarPos] [fCapital false] [stroke [Math.min df.mvs : AdviceStroke 2.75 : df.adws * (7 / 9)]] [yCrossbarPos [yYatDefaultCrossbarPos top pBar stroke]]] : glyph-proc
		local gap : 0.25 * (df.width - 2 * df.leftSB - [if SLAB 2.5 4.5] * stroke)
		define divSub : Math.min (7 / 6) ((df.width - gap - stroke) / Width)
		define dfSub : DivFrame divSub 2

		local shift : Width * (df.adws - divSub)
		include : with-transform [ApparentTranslate shift 0] : new-glyph : glyph-proc
			include : YatShape dfSub Yeri top
				pBar          -- pBar
				fCapital      -- fCapital
				stroke        -- stroke
				yCrossbarPos  -- yCrossbarPos
				xCrossbarLeftExt -- (df.leftSB - shift)
			eject-contour 'serifDL'

		include : Iotified.[if fCapital 'full' 'ascender'] df top 0 (fCapital -- fCapital) (swRef -- stroke)

	foreach { suffix { Uc Lc } } [pairs-of YeriConfig] : do
		create-glyph "cyrl/Yat.\(suffix)" : glyph-proc
			local df : include : DivFrame para.advanceScaleT 2
			include : df.markSet.capital
			include : YatShape df Uc CAP
				pBar -- 0.5
				fCapital -- true

		create-glyph "cyrl/yat.upright.\(suffix)" : glyph-proc
			local df : include : DivFrame para.advanceScaleT 2
			include : df.markSet.b
			include : YatShape df Lc Ascender
				pBar -- ((YeriBarPos * XH) / Ascender)
				yCrossbarPos -- XH

		create-glyph "cyrl/yatTall.\(suffix)" : glyph-proc
			local df : include : DivFrame para.advanceScaleT 2
			include : df.markSet.b
			local asc : mix XH Ascender 2
			include : ExtendAboveBaseAnchors asc
			include : YatShape df Lc asc
				pBar -- ((YeriBarPos * XH) / asc)
				yCrossbarPos -- Ascender

		create-glyph "cyrl/YatIotified.\(suffix)" : glyph-proc
			local df : include : DivFrame para.advanceScaleMM 3
			include : df.markSet.capital
			include : IotifiedYatShape df Uc CAP
				pBar -- 0.5
				fCapital -- true

		create-glyph "cyrl/yatIotified.\(suffix)" : glyph-proc
			local df : include : DivFrame para.advanceScaleMM 3
			include : df.markSet.b
			include : IotifiedYatShape df Lc Ascender
				pBar -- ((YeriBarPos * XH) / Ascender)
				yCrossbarPos -- XH

		# Italic Yat
		create-glyph "cyrl/yat.italic/yeri.\(suffix)" : glyph-proc
			local df : include : DivFrame para.advanceScaleMM 3
			include : df.markSet.e

			local dfSub : df.slice 3 2
			local xMidBarLeft : dfSub.rightSB - [HSwToV df.mvs]

			include : Lc XH xMidBarLeft df.rightSB
				stroke -- df.mvs
				yStart -- (XH - dfSub.smallArchDepthB)
			eject-contour 'serifYeriLT'
			eject-contour 'serifYeriLB'

			DependentSelector.set currentGlyph : if (suffix === "corner") 'full' 'reduced'

		create-glyph "cyrl/tje.italic/yeri.\(suffix)" : glyph-proc
			local df : include : DivFrame [mix 1 para.advanceScaleMM 2] 4
			include : df.markSet.e

			local dfSub : df.slice 4 3 0
			local xMidBarLeft : dfSub.rightSB - [HSwToV df.mvs]

			include : Lc XH xMidBarLeft df.rightSB
				stroke -- df.mvs
				yStart -- (XH - SmallArchDepthB * 0.5 * df.adws)
			eject-contour 'serifYeriLT'
			eject-contour 'serifYeriLB'

			DependentSelector.set currentGlyph : if (suffix === "corner") 'full' 'reduced'

	select-variant 'cyrl/Yat' 0x462 (follow -- 'cyrl/Yeri')
	select-variant 'cyrl/yat.upright' (follow -- 'cyrl/yeri')
	select-variant 'cyrl/yatTall' 0x1C87 (follow -- 'cyrl/yeri')

	select-variant 'cyrl/YatIotified' 0xA652 (follow -- 'cyrl/Yeri')
	select-variant 'cyrl/yatIotified' 0xA653 (follow -- 'cyrl/yeri')

	select-variant 'cyrl/yat.italic/yeri' (follow -- 'cyrl/yeri')
	CreateDependentComposite 'cyrl/yat.italic' null 'cyrl/yat.italic/yeri' : object
		full    'cyrl/yat.italic/base/corner'
		reduced 'cyrl/yat.italic/base/cursive'

	select-variant 'cyrl/tje.italic/yeri' (follow -- 'cyrl/tje/rightHalf')
	CreateDependentComposite 'cyrl/tje.italic' null 'cyrl/tje.italic/yeri' : object
		full    'cyrl/tje.italic/base/corner'
		reduced 'cyrl/tje.italic/base/cursive'

	alias 'latn/yatSakha.italic' null 'cyrl/yat.italic'
