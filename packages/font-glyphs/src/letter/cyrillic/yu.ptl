$$include '../../meta/macros.ptl'

import [mix linreg clamp fallback] from "@iosevka/util"

glyph-module

glyph-block Letter-Cyrillic-Yu : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives
	glyph-block-import Letter-Cyrillic-Iotified-A : Iotified

	define SLAB-ALL        1
	define SLAB-LOWER      2
	define SLAB-BULGARIAN  3

	define [CyrYuShape df slabType top oTop ada adb] : glyph-proc
		local sw : Math.min df.mvs : AdviceStroke 2 (0.75 * df.adws)
		local gap : 0.375 * (df.rightSB - df.leftSB) - [HSwToV : 0.25 * sw] + sw / 16
		local subDf : DivFrame [Math.min ((df.width - gap) / Width) (0.75 * df.adws)] 2

		local shift : df.width - subDf.width
		include : with-transform [ApparentTranslate shift 0]
			OShape oTop 0 subDf.leftSB subDf.rightSB sw (ada * 0.75 * df.adws) (adb * 0.75 * df.adws)

		include : Iotified.[if (slabType === SLAB-BULGARIAN) 'ascender' 'full'] df top
			hBarRight -- (((subDf.leftSB + OX) + [HSwToV : 0.5 * sw]) + shift)
			hBarY     -- (oTop / 2)
			fCapital  -- (slabType === SLAB-ALL)
			swRef     -- sw

	create-glyph 'cyrl/Yu' 0x42E : glyph-proc
		local df : include : DivFrame para.advanceScaleM 3
		include : df.markSet.capital
		include : CyrYuShape df SLAB-ALL CAP CAP ArchDepthA ArchDepthB

	create-glyph 'cyrl/yu' 0x44E : glyph-proc
		local df : include : DivFrame para.advanceScaleM 3
		include : df.markSet.e
		include : CyrYuShape df SLAB-LOWER XH XH SmallArchDepthA SmallArchDepthB

	create-glyph 'cyrl/yu.BGR' : glyph-proc
		local df : include : DivFrame para.advanceScaleM 3
		include : df.markSet.b
		include : CyrYuShape df SLAB-BULGARIAN Ascender XH SmallArchDepthA SmallArchDepthB

	define SLAB-NONE       0
	define SLAB-FULL       1
	define SLAB-OUTWARD    2

	define [CyrRevYuShape df slabType top oTop ada adb fTailed] : glyph-proc
		local sw : Math.min df.mvs : AdviceStroke 2 (0.75 * df.adws)
		local gap : 0.375 * (df.rightSB - df.leftSB) - [HSwToV : 0.25 * sw] + sw / 16
		local subDf : DivFrame [Math.min ((df.width - gap) / Width) (0.75 * df.adws)] 2

		include : OShape oTop 0 subDf.leftSB subDf.rightSB sw (ada * 0.75 * df.adws) (adb * 0.75 * df.adws)

		local useItalicShape : para.isItalic && slabType !== SLAB-ALL
		include : Iotified.RevShape df top
			hBarLeft   -- ((subDf.rightSB - OX) - [HSwToV : 0.5 * sw])
			hBarY      -- (oTop / 2)
			slabTop    -- [if useItalicShape SLAB-NONE SLAB-FULL]
			slabBottom -- [if useItalicShape SLAB-OUTWARD SLAB-FULL]
			swRef      -- sw
			fTailed    -- fTailed

	define YuRevConfig : object
		straight  false
		tailed    true

	foreach { suffix fTailed } [Object.entries YuRevConfig] : do
		if [not fTailed] : create-glyph "cyrl/YuRev.\(suffix)" : glyph-proc
			local df : include : DivFrame para.advanceScaleM 3
			include : df.markSet.capital
			include : CyrRevYuShape df SLAB-ALL CAP CAP ArchDepthA ArchDepthB fTailed
		create-glyph "cyrl/yuRev.\(suffix)" : glyph-proc
			local df : include : DivFrame para.advanceScaleM 3
			include : df.markSet.e
			include : CyrRevYuShape df SLAB-LOWER XH XH SmallArchDepthA SmallArchDepthB fTailed

	select-variant 'cyrl/YuRev' 0xA654
	select-variant 'cyrl/yuRev' 0xA655
