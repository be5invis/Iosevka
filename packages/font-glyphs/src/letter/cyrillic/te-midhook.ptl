$$include '../../meta/macros.ptl'

import [mix linreg clamp fallback] from "@iosevka/util"

glyph-module

glyph-block Letter-Cyrillic-Te-MidHook : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives
	glyph-block-import Letter-Shared-Shapes : MidHook
	glyph-block-import Letter-Latin-Upper-T : TConfig

	define [Shape df top pArc slabTop slabBot] : glyph-proc
		local sw : Math.min df.mvs : AdviceStroke 2.75
		local left : [mix df.leftSB df.rightSB 0.3] + OX
		local right : df.rightSB - OX

		local xTopBarLeft : df.leftSB - SideJut
		local xTopBarRight : Math.max
			mix xTopBarLeft (left + [HSwToV : 0.5 * sw]) 2
			mix left right 0.475
		include : HBar.t xTopBarLeft xTopBarRight top sw

		include : VBar.l left 0 top sw
		include : MidHook.general
			left   -- (left + [HSwToV sw])
			right  -- right
			top    -- (top * HBarPos + sw / 4)
			ada    -- (ArchDepthA * pArc)
			adb    -- (ArchDepthB * pArc)
			sw     -- sw

		if slabTop : begin
			local swVJut : Math.min (VJutStroke * (sw / Stroke)) : VSwToH : 0.625 * (left - xTopBarLeft)
			include : VSerif.dl  xTopBarLeft top VJut swVJut
			include : VSerif.dr xTopBarRight top VJut swVJut

		if slabBot : include : intersection
			MaskLeft : mix (left + [HSwToV sw]) (right - [HSwToV sw]) 0.625
			HSerif.mb (left + [HSwToV : 0.5 * sw]) 0 ([Math.max Jut : MidJutCenter * (6 / 7) * df.adws] - [HSwToV : 0.5 * (Stroke - sw)]) sw

	foreach { suffix { adws doST doSB } } [Object.entries TConfig] : do
		create-glyph "cyrl/TeMidHook.\(suffix)" : glyph-proc
			local df : include : DivFrame adws
			include : df.markSet.capDesc
			include : Shape df CAP 1 doST doSB

		create-glyph "cyrl/teMidHook.upright.\(suffix)" : glyph-proc
			local df : include : DivFrame adws
			include : df.markSet.p
			include : Shape df XH (HBarPos ** 0.3) doST doSB

	select-variant 'cyrl/TeMidHook' 0xA68A (follow -- 'T')
	select-variant 'cyrl/teMidHook.upright' (follow -- 'T')
