$$include '../../meta/macros.ptl'

import [mix linreg clamp fallback] from "@iosevka/util"
import [Point] from "@iosevka/geometry/point"

glyph-module

glyph-block Letter-Cyrillic-Lje : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives
	glyph-block-import Letter-Shared-Shapes : LegShape
	glyph-block-import Letter-Cyrillic-Yeri : YeriConfig

	define [CyrLjeShape Yeri df top] : glyph-proc
		local xTopLeft : mix df.leftSB df.rightSB 0.025
		local jut : Jut * [Math.min 1 : 0.75 * df.adws]
		include : LegShape
			ztop -- [Point.fromXY Point.Type.Corner xTopLeft top]
			zbot -- [Point.fromXY Point.Type.Corner [mix df.leftSB 0 : if SLAB 1 0.75] 0]
			xb   -- xTopLeft
			fine -- df.mvs
		include : Yeri top
			left   -- (df.middle - [HSwToV : 0.5 * df.mvs])
			right  -- (df.rightSB - O)
			stroke -- df.mvs
			jut    -- jut
		include : HBar.t xTopLeft df.middle top df.mvs
		if SLAB : begin
			include : HSerif.lt xTopLeft top (jut - [HSwToV : 0.5 * df.mvs]) df.mvs

	foreach { suffix { Uc Lc } } [Object.entries YeriConfig] : do
		create-glyph "cyrl/Lje.\(suffix)"  : glyph-proc
			local df : include : DivFrame para.advanceScaleMM 3
			include : df.markSet.capital
			include : CyrLjeShape Uc df CAP

		create-glyph "cyrl/lje.\(suffix)" : glyph-proc
			local df : include : DivFrame para.advanceScaleMM 3
			include : df.markSet.e
			include : CyrLjeShape Lc df XH

	select-variant 'cyrl/Lje' 0x409
	select-variant 'cyrl/lje' 0x459
