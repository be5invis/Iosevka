$$include '../../meta/macros.ptl'

import [mix fallback SuffixCfg] from "@iosevka/util"
import [MathSansSerif] from "@iosevka/glyph/relation"

glyph-module

glyph-block Letter-Latin-Lower-G : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives
	glyph-block-import Mark-Adjustment : ExtendAboveBaseAnchors LeaningAnchor
	glyph-block-import Letter-Shared : CreateTurnedLetter
	glyph-block-import Letter-Shared-Shapes : OBarLeft OBarRight DToothlessRise DMBlend
	glyph-block-import Letter-Shared-Shapes : FlatHookDepth CurlyTail PalatalHook TopHook

	define [OverlayW bw] : glyph-proc
		define l : mix 0 SB 0.3
		define r : mix Width RightSB 0.3
		include : HBar.m l r currentGlyph.baseAnchors.overlay.y bw

	# overlay bar width for double-storey g
	define obwDoubleStorey : Math.min [AdviceStroke 5] (0.5 / 3 * (CAP - Stroke * 4))

	create-glyph 'g.doubleStorey' : glyph-proc
		include : MarkSet.p
		local obot : XH * DesignParameters.gBarPos - O - Stroke * 0.4
		include : OShape XH obot SB (RightSB - 0.3 * SB)

		local gleftx  ([mix 0 SB 0.85] + OX)
		local grightx ([mix Width RightSB 0.825] - OX)
		local groundy : Math.round : [mix Descender (XH * DesignParameters.gBarPos) 0.64] + HalfStroke
		local gtipy : [mix groundy (obot + Stroke + O) 1.005] - Stroke * CThin
		include : dispiro
			g4.left.start [mix SB RightSB 0.4] gtipy [widths 0 (Stroke * CThin)]
			g4 ([mix SB RightSB 0.0575] + Stroke * 0.85) [mix groundy gtipy 0.5] [widths.rhs (Stroke * [mix CThin 1 0.5])]
			flat [mix SB RightSB 0.435] groundy [widths.rhs Stroke]
			curl [mix RightSB SB 0.435] groundy
			archv 4
			g4 grightx [mix (Descender + O) groundy (1.06 * SmallArchDepthA / (SmallArchDepthA + SmallArchDepthB))]
			arch.rhs Descender
			g4 gleftx [mix (Descender + O) groundy (1.06 * SmallArchDepthB / (SmallArchDepthA + SmallArchDepthB))]
			arcvh
			g4 [mix SB RightSB 0.435] groundy [heading Rightward]
		local gm : mix SB (RightSB - 0.3 * SB) 0.5
		include : spiro-outline
			corner (RightSB + 0.25 * SB - OX) XH
			corner (RightSB + 0.25 * SB - OX) (XH - Stroke)
			corner gm  (XH - Stroke - O)
			corner gm  XH

		set-base-anchor 'overlay' Middle [mix (Descender + O) groundy 0.5]
		set-base-anchor 'strike' Middle (XH / 2)

	derive-composites "gBar.doubleStorey" null "g.doubleStorey" [OverlayW obwDoubleStorey]

	create-glyph 'g.openDoubleStorey' : glyph-proc
		include : MarkSet.p
		local obot : XH * DesignParameters.gBarPos - O - Stroke * 0.4
		include : OShape XH obot SB (RightSB - 0.3 * SB)

		local gleftx  ([mix 0 SB 0.85] + OX)
		local grightx ([mix Width RightSB 0.825] - OX)
		local groundy : Math.round : [mix Descender (XH * DesignParameters.gBarPos) 0.64] + HalfStroke
		local gtipy : [mix groundy (obot + Stroke + O) 1.005] - Stroke * CThin
		include : dispiro
			g4.left.start [mix SB RightSB 0.4] gtipy [widths 0 (Stroke * CThin)]
			g4 ([mix SB RightSB 0.0575] + Stroke * 0.85) [mix groundy gtipy 0.5] [widths.rhs (Stroke * [mix CThin 1 0.5])]
			flat [mix SB RightSB 0.435] groundy [widths.rhs Stroke]
			curl [mix RightSB SB 0.435] groundy
			archv 4
			g4 grightx [mix (Descender + O) groundy (1.06 * SmallArchDepthA / (SmallArchDepthA + SmallArchDepthB))]
			hookend Descender
			g4 gleftx [mix (Descender + O) groundy (1.06 * SmallArchDepthB / (SmallArchDepthA + SmallArchDepthB))]
		local gm : mix SB (RightSB - 0.3 * SB) 0.5
		include : spiro-outline
			corner (RightSB + 0.25 * SB - OX) XH
			corner (RightSB + 0.25 * SB - OX) (XH - Stroke)
			corner gm  (XH - Stroke - O)
			corner gm  XH

		set-base-anchor 'overlay' Middle [mix (Descender + O) groundy 0.5]
		set-base-anchor 'strike' Middle (XH / 2)

	derive-composites "gBar.openDoubleStorey" null "g.openDoubleStorey" [OverlayW obwDoubleStorey]

	define SingleStorey : namespace
		export : define [RoundHookT sink df yTop offset sw] : sink
			widths.rhs sw
			[if (sink == spiro-outline) corner flat] (df.rightSB - offset) yTop [heading Downward]
			curl (df.rightSB - offset) (Descender + SmallArchDepthA)
			hookend (Descender + offset) (sw -- sw)
			g4 (df.leftSB + offset) (Descender + SHook)
			if (sink == spiro-outline) {[corner df.leftSB XH]} {}

		export : define [RoundHook df y0] : RoundHookT dispiro df y0 0 df.mvs
		export : define [FlatHook df y0] : begin
			local hd : FlatHookDepth df (9 / 8)
			local xTerminal : Math.min (df.rightSB - hd.x - TINY) : mix df.leftSB df.rightSB (1 / 5)
			return : dispiro
				widths.rhs df.mvs
				flat df.rightSB y0 [heading Downward]
				curl df.rightSB (Descender + hd.y)
				arcvh.superness DesignParameters.tightHookSuperness
				flat (df.rightSB - hd.x) Descender
				curl xTerminal Descender
		export : define [CrossedHook df y0] : begin
			local fine : AdviceStroke 3.5 df.div
			return : dispiro
				widths.rhs df.mvs
				flat df.rightSB y0 [heading Downward]
				curl df.rightSB (Descender + SmallArchDepthA)
				CurlyTail.n fine Descender df.leftSB df.width (Descender + 0.5 * fine)
					yLoopTop -- (0.15 * Descender + 0.25 * fine)

		export : define [ScriptCutBody df top] : glyph-proc
			include : SeriflessBody df top
			include : spiro-outline
				corner df.rightSB                     top
				corner df.rightSB                     (top - Stroke / 2)
				corner (df.rightSB - [HSwToV Stroke]) (top - Stroke / 2)

		export : define [SeriflessBody df top] : glyph-proc
			include : OBarRight.shape (top -- top) (left -- df.leftSB) (right -- df.rightSB) (sw -- df.mvs)

		export : define [SerifedBody df top] : glyph-proc
			include : SeriflessBody df top
			include : tagged 'serifRT' : HSerif.rt df.rightSB top SideJut

		export : define [AutoSerifedBody df top] : glyph-proc
			include : SeriflessBody df top
			if SLAB : include : tagged 'serifRT' : HSerif.rt df.rightSB top SideJut

		export : define [EarlessCornerBody df top] : glyph-proc
			include : OBarLeft.toothless
				top -- top
				left -- df.leftSB
				right -- df.rightSB
				rise -- DToothlessRise
				mBlend -- DMBlend
				sw -- df.mvs
			include : FlipAround df.middle (top / 2)

		export : define [EarlessRoundedBody df top] : glyph-proc
			include : OBarLeft.rounded
				top -- top
				left -- df.leftSB
				right -- df.rightSB
				yTerminal -- (XH - SmallArchDepthA)
				sw -- df.mvs
			include : FlipAround df.middle (top / 2)

	define SingleStoreyConfig : SuffixCfg.weave
		object # hook
			singleStorey            SingleStorey.RoundHook
			singleStoreyFlatHook    SingleStorey.FlatHook
		object # ear/serif
			""                    { SingleStorey.AutoSerifedBody    0                      }
			serifless             { SingleStorey.SeriflessBody      0                      }
			serifed               { SingleStorey.SerifedBody        0                      }
			earlessCorner         { SingleStorey.EarlessCornerBody  DToothlessRise         }
			earlessCornerHTB      { SingleStorey.EarlessCornerBody  0                      }
			earlessRounded        { SingleStorey.EarlessRoundedBody (XH - SmallArchDepthA) }
			scriptCut             { SingleStorey.ScriptCutBody      (Stroke / 2)           }

	foreach { suffix { hookShape {bodyShape hookStart} } } [Object.entries SingleStoreyConfig] : do
		create-glyph "g.\(suffix)" : glyph-proc
			local df : include : DivFrame 1
			include : df.markSet.p
			set-base-anchor 'overlay' Middle (XH / 2)
			set-base-anchor 'strike' Middle (XH / 2)
			include : bodyShape df XH
			include : hookShape df (XH - hookStart)

		create-glyph "GScript.\(suffix)" : glyph-proc
			local df : include : DivFrame 1
			include : df.markSet.capDesc
			set-base-anchor 'overlay' Middle (CAP / 2)
			include : bodyShape df CAP
			include : hookShape df (CAP - hookStart)

		create-glyph "gScriptPalatalHook.\(suffix)" : glyph-proc
			local df : include : DivFrame para.diversityM 1
			include : df.markSet.p
			set-base-anchor 'overlay' Middle (XH / 2)
			local dfSub : DivFrame (0.75 * para.diversityM) 2
			include : bodyShape dfSub XH
			include : hookShape dfSub (XH - hookStart)
			include : PalatalHook.r
				x -- df.rightSB
				y -- 0
				xLink -- dfSub.rightSB
				refSw -- dfSub.mvs
				maskOut -- [intersection [MaskBelow 0] [MaskLeft dfSub.rightSB]]

		create-glyph "gBar.\(suffix)" : glyph-proc
			include [refer-glyph "g.\(suffix)"] AS_BASE ALSO_METRICS
			include : OverlayW [AdviceStroke2 2 4 XH]

		create-glyph "gScriptCrossedTail.\(suffix)" : glyph-proc
			include : MarkSet.p
			set-base-anchor 'overlay' Middle (XH / 2)
			define df : DivFrame 1
			include : bodyShape df XH
			include : SingleStorey.CrossedHook df (XH - hookStart)

	select-variant 'g' 'g'
	link-reduced-variant 'g/sansSerif' 'g' MathSansSerif
	CreateTurnedLetter 'turng' 0x1D77 'g' HalfAdvance [mix Descender XH 0.5]
	select-variant "gBar" 0x1E5 (follow -- 'g')
	select-variant 'g/single' null (shapeFrom -- 'g')

	select-variant 'g/hookTopBase' null (shapeFrom -- 'g')

	select-variant 'gScript' 0x261 (shapeFrom -- 'g') (follow -- 'gScript')
	select-variant 'GScript' 0xA7AC (follow -- 'gScript')
	select-variant 'gScriptPalatalHook' 0x1D83 (follow -- 'gScript')
	select-variant 'gScriptCrossedTail' 0xAB36

	alias 'cyrl/de.BGR' null 'g/single'
	alias 'cyrl/de.SRB' null 'g/single'

	derive-glyphs 'gHookTop' 0x260 "g/hookTopBase" : function [src gr] : glyph-proc
		include [refer-glyph src] AS_BASE ALSO_METRICS
		include : TopHook.rBarOuter RightSB 0 XH
		include : LeaningAnchor.Above.VBar.r RightSB

	glyph-block-import Letter-Blackboard : BBS BBD BBBarRight
	create-glyph 'mathbb/g' 0x1D558 : glyph-proc
		include : MarkSet.p
		include : intersection
			OShapeOutline.NoOvershoot XH 0 SB (RightSB - BBD - [HSwToV BBS])
			VBar.l (SB + BBD + OX) 0 XH BBS
		include : OBarRight.shape
			left -- SB
			right -- RightSB - BBD
			sw -- BBS
		include : SingleStorey.RoundHookT dispiro [DivFrame 1] XH 0 BBS
		include : HBar.t (RightSB - BBD) RightSB XH BBS
		include : intersection
			VBar.r (RightSB - BBD) Descender XH BBS
			SingleStorey.RoundHookT spiro-outline [DivFrame 1] XH 1 BBS
