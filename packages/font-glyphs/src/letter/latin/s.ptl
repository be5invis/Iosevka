$$include '../../meta/macros.ptl'

import [mix linreg clamp fallback] from "@iosevka/util"
import [DependentSelector MathSansSerif] from "@iosevka/glyph/relation"

glyph-module

glyph-block Letter-Latin-S : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives
	glyph-block-import Letter-Shared : CreateAccentedComposition CreateDependentComposite
	glyph-block-import Letter-Blackboard : BBS BBD
	glyph-block-import Letter-Shared-Shapes : DToothlessRise FlatHookDepth
	glyph-block-import Letter-Shared-Shapes : ArcStartSerifWidth ArcStartSerifDepth
	glyph-block-import Letter-Shared-Shapes : SerifedArcStart SerifedArcEnd
	glyph-block-import Letter-Shared-Shapes : ArcStartSerif ArcEndSerif
	glyph-block-import Letter-Shared-Shapes : PalatalHook RetroflexHook CyrDescender CurlyTail

	define SLAB-NONE      0
	define SLAB-CLASSICAL 1
	define SLAB-INWARD    2
	define CURLY-TAIL     10

	define SOBot  OX

	glyph-block-export AdviceSArchDepth
	define [AdviceSArchDepth y sign _stroke] : begin
		# Handle with extreme care.
		local stroke : fallback _stroke Stroke
		local strokeFactor : stroke * [clamp 1 2 : linreg 126 1 137 1.025 stroke] * [clamp 0 1 : Math.pow (Width / HalfUPM) 0.5]
		local widthFactor : RightSB - SB
		local ss : y * 0.22 + 0.12 * strokeFactor + 0.05 * widthFactor
		return : ss + sign * TanSlope * SmoothAdjust

	define [SStrokeDollarInterruptGap deltaX top bot gap stroke refSwEss] : begin
		local ess : refSwEss * gap / Stroke
		define archDepth : AdviceSArchDepth (top - bot) (-1) stroke

		return : dispiro
			g4.down.mid (deltaX + SB - Width) (top - archDepth) [widths.lhs gap]
			alsoThru.g2 0.5 0.5 [widths.center ess]
			g4.down.mid (deltaX + RightSB - Width - SOBot) (bot + archDepth) [widths.rhs gap]

	define [SCurlyTail sw] : begin
		local fine : AdviceStroke2 3 4 XH
		local rinner : ((XH * 0.5) - fine * 2 - sw * 0.5) / 4
		local m1 : RightSB - SOBot - [HSwToV sw]
		local x2 : RightSB - sw * 0.3
		local y2 : -XH * 0.05

		return : CurlyTail fine rinner m1 0 (SB + SOBot) x2 y2

	define [SStrokeImpl top bot st sb stroke refSwEss] : begin
		local ess : refSwEss * stroke / Stroke
		define archDepth : AdviceSArchDepth (top - bot) (-1) stroke

		return : dispiro
			match st
				[Just SLAB-CLASSICAL] : SerifedArcStart.RtlLhs RightSB Middle top stroke Hook
				[Just SLAB-INWARD] : list
					g4 RightSB (top - DToothlessRise) [widths.lhs stroke]
					g4 (Middle - CorrectionOMidX * stroke) (top - O)
					archv
				__ : list
					g4 RightSB (top - Hook) [widths.lhs stroke]
					hookstart (top - O) (sw -- stroke) (swItalicAdj -- Stroke)

			g4   SB (top - archDepth)
			alsoThru.g2 0.5 0.5 [widths.center ess]
			g4   (RightSB - SOBot) (bot + archDepth) [widths.rhs stroke]

			match sb
				[Just SLAB-CLASSICAL] : SerifedArcEnd.RtlRhs SB Middle bot stroke Hook
				[Just SLAB-INWARD] : list
					arcvh
					g4 (Middle + CorrectionOMidX * stroke) (bot + O)
					g4 SB (bot + DToothlessRise)
				__ : list
					hookend (bot + O) (sw -- stroke) (swItalicAdj -- Stroke)
					g4   (SB + SOBot) (bot + Hook)

	define [RevSStroke st sb stroke ess] : begin
		local archDepth : AdviceSArchDepth CAP 0.5 stroke
		return : dispiro
			match st
				[Just SLAB-CLASSICAL] : SerifedArcStart.LtrRhs SB Middle CAP stroke Hook
				[Just SLAB-INWARD] : list
					g4 SB (CAP - DToothlessRise) [widths.rhs stroke]
					g4 (Middle - CorrectionOMidX * stroke) (CAP - O)
					archv
				__ : list
					g4   SB (CAP - Hook) [widths.rhs stroke]
					hookstart (CAP - O) (sw -- stroke) (swItalicAdj -- Stroke)
			g4   RightSB (CAP - archDepth)
			alsoThru.g2 0.5 0.5 [widths.center ess]
			g4   (SB + SOBot) archDepth [widths.lhs stroke]
			match sb
				[Just SLAB-CLASSICAL] : SerifedArcEnd.LtrLhs RightSB Middle 0 stroke Hook
				[Just SLAB-INWARD] : list
					arcvh
					g4 (Middle + CorrectionOMidX * stroke) O
					g4 RightSB DToothlessRise
				__ : list
					hookend O (sw -- stroke) (swItalicAdj -- Stroke)
					g4   (RightSB + OX - SOBot) Hook

	define [SmallSStrokeImpl st sb stroke refSwEss] : begin
		define ess : refSwEss * stroke / Stroke
		define archDepth : AdviceSArchDepth XH (-1) stroke

		return : dispiro
			match st
				[Just SLAB-CLASSICAL] : SerifedArcStart.RtlLhs RightSB Middle XH stroke SHook
				[Just SLAB-INWARD] : list
					g4 RightSB (XH - DToothlessRise) [widths.lhs stroke]
					g4 (Middle - CorrectionOMidX * stroke) (XH - O)
					archv
				__ : list
						g4   (RightSB + OX) (XH - SHook) [widths.lhs stroke]
						hookstart (XH - O) (sw -- stroke) (swItalicAdj -- Stroke)

			g4   (SB - OX) (XH - archDepth)
			alsoThru.g2 0.5 0.5 [widths.center ess]
			g4   (RightSB + OX - SOBot) (archDepth) [widths.rhs stroke]

			match sb
				[Just CURLY-TAIL] : list
					arcvh
					SCurlyTail stroke
				[Just SLAB-CLASSICAL] : SerifedArcEnd.RtlRhs SB Middle 0 stroke SHook
				[Just SLAB-INWARD] : list
					arcvh
					g4 (Middle + CorrectionOMidX * stroke) O
					g4 SB DToothlessRise
				__ : list
					hookend O (sw -- stroke) (swItalicAdj -- Stroke)
					g4   (SB - OX + SOBot) SHook

	define [SStrokeAlt] : with-params [top hook swStart swEnd oXLeftTop offsetLT offsetRB offsetC] : begin
		define stroke : Math.max swStart swEnd
		define fine   : Math.min swStart swEnd
		define soStart : stroke - swStart
		define soEnd   : stroke - swEnd
		define archDepth : AdviceSArchDepth top (-1) stroke
		return : dispiro
			widths.lhs fine
			g4   (RightSB + OX / 4 - [HSwToV soStart]) (top - hook)
			hookstart (top - O - soStart) (sw -- fine) (swItalicAdj -- Stroke) (noAdjTerminalY -- true)
			g4   (SB + oXLeftTop + offsetLT.x) (top - archDepth + offsetLT.y)
			g2
				offsetC.x + [mix (SB + oXLeftTop + offsetLT.x) (RightSB - SOBot - offsetRB.x) 0.5]
				offsetC.y + [mix (top - archDepth)                (archDepth)                       0.5]
				widths.center stroke
			g4   (RightSB - SOBot - offsetRB.x) (archDepth + offsetRB.y) [widths.rhs fine]
			hookend (O + soEnd) (sw -- fine) (swItalicAdj -- Stroke) (noAdjTerminalY -- true)
			g4   (SB + SOBot + [HSwToV soEnd]) (hook) [widths.rhs fine]

	define [SmallSStrokePhoneticRight st top] : begin
		define stroke : AdviceStroke2 2 3 top
		define ess : AdviceStroke2 2.2 3.2 top
		define archDepth : AdviceSArchDepth top (-1) stroke
		return : dispiro
			match st
				[Just SLAB-CLASSICAL] : SerifedArcStart.RtlLhs RightSB Middle top stroke Hook
				[Just SLAB-INWARD] : list
					g4 RightSB (top - DToothlessRise) [widths.lhs stroke]
					g4 (Middle - CorrectionOMidX * stroke) (top - O)
					archv
				__ : list
					g4 RightSB (top - Hook) [widths.lhs stroke]
					hookstart (top - O) (sw -- stroke) (swItalicAdj -- Stroke)
			g4   SB (top - archDepth)
			alsoThru.g2 0.5 0.5 [widths.center ess]
			g4   (RightSB - SOBot) (archDepth) [widths.rhs stroke]
			arcvh
			flat (Middle - CorrectionOMidS) 0 [widths.rhs Stroke]
			curl SB 0

	define [RevSmallSStroke st sb stroke ess] : begin
		define archDepth : AdviceSArchDepth XH 0.75 stroke
		return : dispiro
			match st
				[Just SLAB-CLASSICAL] : SerifedArcStart.LtrRhs SB Middle XH stroke SHook
				[Just SLAB-INWARD] : list
					g4 SB (XH - DToothlessRise) [widths.rhs stroke]
					g4 (Middle - CorrectionOMidX * stroke) (XH - O)
					archv
				__ : list
					g4   (SB - OX) (XH - SHook) [widths.rhs stroke]
					hookstart (XH - O) (sw -- stroke) (swItalicAdj -- Stroke)
			g4   (RightSB + OX) (XH - archDepth)
			alsoThru.g2 0.5 0.5 [widths.center ess]
			g4   (SB - OX + SOBot) (archDepth) [widths.lhs stroke]
			match sb
				[Just SLAB-CLASSICAL] : SerifedArcEnd.LtrLhs RightSB Middle 0 stroke SHook
				[Just SLAB-INWARD] : list
					arcvh
					g4 (Middle + CorrectionOMidX * stroke) O
					g4 RightSB DToothlessRise
				__ : list
					hookend O (sw -- stroke) (swItalicAdj -- Stroke)
					g4   (RightSB + OX - SOBot) SHook

	glyph-block-export SAutoSlabStart
	define [SAutoSlabStart st top sw hook] : match st
		[Just SLAB-CLASSICAL] : ArcStartSerif.R RightSB top sw hook
		[Just SLAB-INWARD] : ArcStartSerif.InwardR RightSB top sw hook
		__ : glyph-proc

	glyph-block-export SAutoSlabEnd
	define [SAutoSlabEnd st bot sw hook] : match st
		[Just SLAB-CLASSICAL] : ArcEndSerif.L SB bot  sw hook
		[Just SLAB-INWARD] : ArcEndSerif.InwardL SB bot  sw hook
		__ : glyph-proc

	define [RevSAutoSlabStart st top sw hook] : match st
		[Just SLAB-CLASSICAL] : ArcStartSerif.L SB top sw hook
		[Just SLAB-INWARD] : ArcStartSerif.InwardL SB top sw hook
		__ : glyph-proc

	define [RevSAutoSlabEnd st bot sw hook] : match st
		[Just SLAB-CLASSICAL] : ArcEndSerif.R RightSB bot  sw hook
		[Just SLAB-INWARD] : ArcEndSerif.InwardR RightSB bot  sw hook
		__ : glyph-proc

	define SConfig : object
		serifless               { SLAB-NONE      SLAB-NONE      }
		unilateralSerifed       { SLAB-CLASSICAL SLAB-NONE      }
		bilateralSerifed        { SLAB-CLASSICAL SLAB-CLASSICAL }
		unilateralInwardSerifed { SLAB-INWARD    SLAB-NONE      }
		bilateralInwardSerifed  { SLAB-INWARD    SLAB-INWARD    }

		bottomSerifed           { SLAB-NONE      SLAB-CLASSICAL }
		hybridSerifed1          { SLAB-INWARD    SLAB-CLASSICAL }

	foreach { suffix { doTS doBS } } [Object.entries SConfig] : do
		create-glyph "S.\(suffix)" : glyph-proc
			local sw : AdviceStroke2 2 3 CAP
			include : MarkSet.capital
			include : SStrokeImpl CAP 0 doTS doBS sw EssUpper
			include : SAutoSlabStart doTS CAP sw Hook
			include : SAutoSlabEnd   doBS 0   sw Hook

		create-glyph "smcpS.\(suffix)" : glyph-proc
			local sw : AdviceStroke2 2 3 CAP
			include : MarkSet.e
			include : SStrokeImpl XH 0 doTS doBS sw EssUpper
			include : SAutoSlabStart doTS XH sw Hook
			include : SAutoSlabEnd   doBS 0  sw Hook

		create-glyph "S/dollarKernelStd.\(suffix)" : glyph-proc
			set-width 0
			set-mark-anchor 'cvDecompose' Middle (CAP / 2)

			define top : CAP * 0.95
			define bot : CAP * 0.05
			local sw : AdviceStroke2 2 3 (top - bot)
			include : SStrokeImpl top bot doTS doBS sw EssUpper
			include : SAutoSlabStart doTS top sw Hook
			include : SAutoSlabEnd   doBS bot sw Hook

		create-glyph "S/dollarKernelCap.\(suffix)" : glyph-proc
			set-width 0
			set-mark-anchor 'cvDecompose' Middle (CAP / 2)

			define top : CAP * 0.88
			define bot : CAP * 0.12
			local sw : AdviceStroke2 2 3 (top - bot)
			include : SStrokeImpl top bot doTS doBS sw EssUpper
			include : SAutoSlabStart doTS top sw Hook
			include : SAutoSlabEnd   doBS bot sw Hook

		create-glyph "s.\(suffix)" : glyph-proc
			local sw : AdviceStroke2 2 3 XH
			include : MarkSet.e
			include : SmallSStrokeImpl doTS doBS sw EssLower
			include : SAutoSlabStart doTS XH sw Hook
			include : SAutoSlabEnd   doBS 0  sw Hook

		create-glyph "s/phoneticRight.\(suffix)" : glyph-proc
			local sw : AdviceStroke2 2 3 XH
			include : MarkSet.e
			include : SmallSStrokePhoneticRight doTS XH
			include : SAutoSlabStart doTS XH sw Hook

		create-glyph "revS.\(suffix)" : glyph-proc
			local sw : AdviceStroke2 2 3 CAP
			include : MarkSet.capital
			include : RevSStroke        doTS doBS sw EssUpper
			include : RevSAutoSlabStart doTS CAP  sw Hook
			include : RevSAutoSlabEnd   doBS 0    sw Hook

		create-glyph "revs.\(suffix)" : glyph-proc
			local sw : AdviceStroke2 2 3 XH
			include : MarkSet.e
			include : RevSmallSStroke   doTS doBS sw EssLower
			include : RevSAutoSlabStart doTS XH   sw Hook
			include : RevSAutoSlabEnd   doBS 0    sw Hook

		define [UpperSBaseWithAttach] : glyph-proc
			include : SAutoSlabStart doTS CAP Stroke Hook
			local stroke : include : SStrokeImpl CAP 0 doTS doBS [AdviceStroke2 2 3 CAP] EssUpper
			return stroke.lhsKnots.(stroke.lhsKnots.length - 1)

		define [LowerSBaseWithAttach] : glyph-proc
			include : SAutoSlabStart doTS XH [AdviceStroke2 2 3 XH] Hook
			local stroke : include : SmallSStrokeImpl doTS doBS [AdviceStroke2 2 3 XH] EssLower
			return stroke.lhsKnots.(stroke.lhsKnots.length - 1)

		if [not doBS] : create-glyph "SSwash.\(suffix)" : glyph-proc
			include : MarkSet.capDesc

			local start : include : UpperSBaseWithAttach
			local sw : AdviceStroke2 2 3 CAP
			include : dispiro
				widths.lhs [AdviceStroke 4.5]
				g4 start.x start.y
				alsoThru 0.15 0.6 important
				flat (RightSB - 1) Descender [widths.lhs sw]
				curl RightSB Descender

		if [not doBS] : create-glyph "sSwash.\(suffix)" : glyph-proc
			include : MarkSet.p

			local start : include : LowerSBaseWithAttach
			local sw : AdviceStroke2 2 3 XH
			include : dispiro
				widths.lhs [AdviceStroke 4.5]
				g4 start.x start.y
				alsoThru 0.15 0.6 important
				flat (RightSB - 1) Descender [widths.lhs sw]
				curl RightSB Descender

		create-glyph "sCurlyTail.\(suffix)" : glyph-proc
			local sw : AdviceStroke2 2 3 XH
			include : MarkSet.e
			include : SmallSStrokeImpl doTS CURLY-TAIL sw EssLower
			include : SAutoSlabStart doTS XH sw Hook
			include : SAutoSlabEnd   doBS 0  sw Hook

	select-variant 'S' 'S'
	link-reduced-variant 'S/sansSerif' 'S' MathSansSerif
	select-variant 's' 's'
	link-reduced-variant 's/sansSerif' 's' MathSansSerif
	select-variant 'smcpS' 0xA731 (follow -- 'S')
	select-variant 'revS' 0x1A7 (follow -- 'S')
	select-variant 'revs' 0x1A8 (follow -- 's')
	alias 'cyrl/Dze' 0x405 'S'
	alias 'cyrl/dze' 0x455 's'
	alias 'cyrl/ghe.italic' null 'revs'

	alias 'cyrl/DzeRev' 0xA644 'revS'
	alias 'cyrl/dzeRev' 0xA645 'revs'

	select-variant 'S/dollarKernelStd' (follow -- 'S')
	select-variant 'S/dollarKernelCap' (follow -- 'S')

	select-variant 'SSwash' 0x2C7E
	select-variant 'sSwash' 0x23F
	select-variant 'sCurlyTail' 0x1DF1E (follow -- 'sSwash')

	select-variant 'S/descBase' (shapeFrom -- 'S') (follow -- 'SRTail')
	select-variant 's/ascBase' (shapeFrom -- 's')
	select-variant 's/descBase' (shapeFrom -- 's') (follow -- 'sRTail')
	select-variant 'revs/descBase' (shapeFrom -- 'revs') (follow -- 'sRTail')

	select-variant 's/phoneticRight'

	derive-composites 'SRTail' 0xA7C5 'S/descBase'
		RetroflexHook.l SB 0 (yAttach -- DToothlessRise) (refSw -- [AdviceStroke2 2 3 CAP])
	derive-composites 'sRTail' 0x282 's/descBase'
		RetroflexHook.l SB 0 (yAttach -- DToothlessRise) (refSw -- [AdviceStroke2 2 3 XH])
	derive-composites 'cyrl/gheDescender.italic' null 'revs/descBase'
		CyrDescender.r RightSB 0 (yAttach -- DToothlessRise) (refSw -- [AdviceStroke2 2 3 XH])
	derive-composites 'cyrl/gheDHook.italic' null 'revs/descBase'
		PalatalHook.r RightSB 0 (yAttach -- DToothlessRise) (refSw -- [AdviceStroke2 2 3 XH])

	derive-composites 'cyrl/ghayn.italic' null 'revs'
		FlatSlashShape Middle (XH / 2) (OverlayStroke / 2) (-0.2) 0.75
	derive-composites 'cyrl/gheStrokeHook.italic' null 'cyrl/gheDHook.italic'
		FlatSlashShape Middle (XH / 2) (OverlayStroke / 2) (-0.2) 0.75

	derive-composites 'sPalatalHook' 0x1D8A 's' : PalatalHook.r
		x -- [mix (Middle + CorrectionOMidS) RightSB 0.75]
		y -- 0
		yAttach -- [mix O [AdviceSArchDepth XH (-1) [AdviceStroke2 2 3 XH]] : archv.yFromX 0.75]

	derive-composites "s/compLigLeft" null "s/ascBase" : glyph-proc
		eject-contour 'arcStartSerifR'
		local sw : ArcStartSerifWidth [AdviceStroke2 2 3 XH]
		local fine : sw * [mix CThinB 1 0.6]
		local hd : FlatHookDepth [DivFrame 1]
		local yStart : XH - [ArcStartSerifDepth SHook]
		local neckLength : 0.5 * Stroke
		local leftExt : 0.3 * (RightSB - SB) - [HSwToV : 0.25 * sw]
		include : dispiro
			widths.rhs fine
			flat (RightSB - [HSwToV fine]) yStart [heading Upward]
			curl (RightSB - [HSwToV fine]) (yStart + neckLength) [heading Upward]
			alsoThru.g2 0.5 0.5 [widths.center [mix fine sw 0.35]]
			g4 (RightSB - leftExt) [Math.min ([mix XH Ascender 0.5]) (Ascender - 1.375 * sw)] [widths.lhs : mix fine sw 0.7]
			arcvh
			flat RightSB     Ascender [widths.rhs.heading sw Rightward]
			curl (4 * Width) Ascender [widths.rhs.heading sw Rightward]

	derive-composites 'SCedilla' 0x15E 'S' 'cedillaExtShapeBelowOArc'
	derive-composites 'sCedilla' 0x15F 's' 'cedillaExtShapeBelowSOArc'

	create-glyph 'mathbb/S' 0x1D54A : glyph-proc
		include : MarkSet.capital
		define theta : Math.PI / 4
		foreach sign [items-of { 1 (-1) }] : begin
			include : SStrokeAlt
				top -- CAP
				hook -- Hook
				swStart -- BBS * [if (sign > 0) CThin 1]
				swEnd   -- BBS * [if (sign < 0) CThin 1]
				oXLeftTop -- O
				ess -- BBD
				offsetC -- {
					.x (0.5 * sign * BBD * [Math.sin theta])
					.y (0.5 * sign * BBD * [Math.cos theta])
				}
				offsetLT -- {
					.x [if (sign > 0) (BBD / 2) 0]
					.y ([if (sign > 0) 0.25 0.375] * sign * BBD * [Math.cos theta])
				}
				offsetRB -- {
					.x [if (sign < 0) (BBD / 2) 0]
					.y ([if (sign < 0) 0.25 0.375] * sign * BBD * [Math.cos theta])
				}

	create-glyph 'mathbb/s' 0x1D564 : glyph-proc
		include : MarkSet.e
		define theta : Math.PI / 5
		foreach sign [items-of { 1 (-1) }] : begin
			include : SStrokeAlt
				top -- XH
				hook -- SHook
				swStart -- BBS * [if (sign > 0) CThin 1]
				swEnd   -- BBS * [if (sign < 0) CThin 1]
				oXLeftTop -- (OX / 8)
				ess -- BBD
				offsetC -- {
					.x (0.5 * sign * BBD * [Math.sin theta])
					.y (0.5 * sign * BBD * [Math.cos theta])
				}
				offsetLT -- {
					.x [if (sign > 0) (BBD / 2) 0]
					.y ([if (sign > 0) 0.25 0.375] * sign * BBD * [Math.cos theta])
				}
				offsetRB -- {
					.x [if (sign < 0) (BBD / 2) 0]
					.y ([if (sign < 0) 0.25 0.375] * sign * BBD * [Math.cos theta])
				}



	define DollarConfig : object
		open           { 0.95 0.05 (Descender / 2) 0 "std" }
		through        { 0.95 0.05 (Descender / 2) 1 "std" }
		interrupted    { 0.95 0.05 (Descender / 2) 2 "std" }
		openCap        { 0.88 0.12              0  0 "cap" }
		throughCap     { 0.88 0.12              0  1 "cap" }
		interruptedCap { 0.88 0.12              0  2 "cap" }

	foreach { suffix { kTop kBot overflow fillType selector } } [Object.entries DollarConfig] : do
		create-glyph "dollar/bar.\(suffix)" : glyph-proc
			define top : CAP * kTop
			define bot : CAP * kBot
			define w : AdviceStroke 3
			define fine : AdviceStroke [StrokeWidthBlend 3 5.5]

			set-base-anchor 'cvDecompose' Middle (CAP / 2)

			include : dispiro
				widths.center w
				flat Middle (top - HalfStroke)
				curl Middle (CAP - overflow) [heading Upward]
			include : dispiro
				widths.center w
				flat Middle overflow [heading Upward]
				curl Middle (bot + HalfStroke)
			if (fillType === 1) : include : VBar.m Middle (bot + HalfStroke) (top - HalfStroke) fine
			if (fillType === 2) : begin
				local cofine : AdviceStroke 4
				local gap : (w / Stroke * EssUpper) + [Math.max ((top - bot) / 6) [AdviceStroke2 3 3 (top - bot)]]
				local yMid : mix bot top 0.5
				local yTermLeft : mix yMid (top - [AdviceSArchDepth (top - bot) (-1) w]) 0.5
				local yTermRight : mix yMid (bot + [AdviceSArchDepth (top - bot) (-1) w]) 0.5
				include : difference
					union
						dispiro
							flat Middle (top - HalfStroke) [widths.center.heading cofine Downward]
							curl Middle [mix bot top 0.5]  [widths.center.heading fine Downward]
						dispiro
							flat Middle (bot + HalfStroke) [widths.center.heading cofine Upward]
							curl Middle [mix bot top 0.5]  [widths.center.heading fine Upward]

					dispiro [corner 0 yTermLeft : widths.center gap] [corner Width yTermRight]

			DependentSelector.set currentGlyph selector

	select-variant 'dollar/bar' (follow -- 'dollar')

	CreateDependentComposite 'dollar' '$' "dollar/bar" : object
		'std' 'S/dollarKernelStd'
		'cap' 'S/dollarKernelCap'
