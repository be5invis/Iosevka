$$include '../../meta/macros.ptl'

import [mix linreg clamp fallback] from "@iosevka/util"
import [MathSansSerif] from "@iosevka/glyph/relation"

glyph-module

glyph-block Letter-Latin-Upper-I : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives
	glyph-block-import Letter-Shared : SetGrekUpperTonos CreateAccentedComposition CreateOgonekComposition
	glyph-block-import Mark-Adjustment : ExtendAboveBaseAnchors

	define [ISeriflessShape df top bot jut] : glyph-proc
		include : VBar.m df.middle bot top

	define [ISerifShape df top bot jut] : glyph-proc
		include : VBar.m df.middle bot top
		include : HSerif.mb df.middle bot (jut * df.div)
		include : HSerif.mt df.middle top (jut * df.div)
		set-base-anchor 'trailing' (df.middle + jut * df.div) bot

	define UpperIConfig : object
		'straight'     { para.diversityI  ISeriflessShape LongJut      }
		'serifless'    { para.diversityII ISeriflessShape LongJut      }
		'serifed'      { para.diversityI  ISerifShape     LongJut      }
		'shortSerifed' { para.diversityI  ISerifShape     MidJutCenter }

	foreach { suffix { div Body jut } } [Object.entries UpperIConfig] : do
		create-glyph "I.\(suffix)" : glyph-proc
			local df : include : DivFrame div
			include : df.markSet.capital
			include : Body df CAP 0 jut

		create-glyph "grek/Iota.\(suffix)" : glyph-proc
			include [refer-glyph "I.\(suffix)"] AS_BASE ALSO_METRICS
			include : SetGrekUpperTonos 0

		create-glyph "ISideways.\(suffix)" : glyph-proc
			local df : DivFrame (XH / Width) 2 (XH * 0.1 / SB)
			include : PointingTo Width XH Width 0 : function [] : glyph-proc
				include : Body df (Width - SB) 0 jut
				include : ApparentTranslate 0 (0.5 * SB)

		create-glyph "ILonga.\(suffix)" : glyph-proc
			local df : include : DivFrame div
			include : df.markSet.capital
			local top : CAP + Ascender - XH
			include : ExtendAboveBaseAnchors top
			include : Body df top 0 jut

	select-variant 'I' 'I'
	link-reduced-variant 'I/sansSerif' 'I' MathSansSerif
	select-variant 'grek/Iota' 0x399 (follow -- 'I')
	link-reduced-variant 'grek/Iota/sansSerif' 'grek/Iota' MathSansSerif (follow -- 'I/sansSerif')
	alias 'cyrl/IUkrainian' 0x406 'I'
	CreateAccentedComposition 'cyrl/Yi' 0x407 'I' 'dieresisAbove'
	alias 'cyrl/Palochka' 0x4C0 'I'

	select-variant 'ISideways' 0xA7F7 (follow -- 'I')
	select-variant 'ILonga' 0xA7FE (follow -- 'I')

	create-glyph 'smcpI' 0x26A : glyph-proc
		local df : include : DivFrame para.diversityI
		include : df.markSet.e
		include : ISerifShape df XH 0 MidJutCenter

	glyph-block-import Letter-Blackboard : BBS BBD BBBarCenter
	create-glyph 'mathbb/I' 0x1D540 : glyph-proc
		include : MarkSet.capital
		include : BBBarCenter Middle 0 CAP
		include : HBar.t    (Middle - BBD / 2 - Jut) (Middle + BBD / 2 + Jut) CAP BBS
		include : HBar.b (Middle - BBD / 2 - Jut) (Middle + BBD / 2 + Jut) 0   BBS

	CreateAccentedComposition 'smcpIBarOver' 0x1D7B 'smcpI' 'barOver'

	CreateOgonekComposition 'IOgonek' 0x12E 'I'
