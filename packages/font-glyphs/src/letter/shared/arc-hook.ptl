$$include '../../meta/macros.ptl'

import [mix linreg clamp fallback SuffixCfg] from "@iosevka/util"
import [DependentSelector] from "@iosevka/glyph/relation"

glyph-module

glyph-block Letter-Arc-Hook : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives
	glyph-block-import Letter-Shared-Shapes : FlatHookDepth

	glyph-block-export Toothed Toothless Rounded Flat

	define [ArcStartSerifWidth stroke] : clamp (stroke * 0.875) stroke VJutStroke
	define [ArcStartInwardSerifDepth hook stroke] : Math.max hook (DToothlessRise + stroke)

	define flex-params [Toothed] : namespace
		local-parameter : left
		local-parameter : right
		local-parameter : top      -- XH
		local-parameter : bottom   -- 0
		local-parameter : ada      -- SmallArchDepthA
		local-parameter : adb      -- SmallArchDepthB
		local-parameter : stroke   -- Stroke

		local-parameter : fine     -- ShoulderFine
		local-parameter : barSw    -- [ArcStartSerifWidth stroke]
		local-parameter : hook     -- Hook

		export : define yLT top
		export : define yRT top
		export : define yLB bottom
		export : define yRB bottom
		export : define xLArc  : left  + [HSwToV : stroke - fine]
		export : define xRArc  : right - [HSwToV : stroke - fine]
		export : define xLHook : left  + [HSwToV : barSw - fine]
		export : define xRHook : right - [HSwToV : barSw - fine]

		export : define [arcStartL] : begin
			return : list
				g4.up.start xLArc (top - ada) [widths.rhs.heading fine Upward]
				arch.rhs top (sw -- stroke) (swBefore -- fine)
		export : define [arcStartR] : begin
			return : list
				g4.up.start xRArc (top - adb) [widths.lhs.heading fine Upward]
				arch.lhs top (sw -- stroke) (swBefore -- fine)
		export : define [arcEndL] : begin
			return : list
				arch.rhs bottom (sw -- stroke) (swAfter -- fine)
				g4.up.end xLArc (bottom + adb) [widths.rhs.heading fine Upward]
		export : define [arcEndR] : begin
			return : list
				arch.lhs bottom (sw -- stroke) (swAfter -- fine)
				g4.up.end xRArc (bottom + ada) [widths.lhs.heading fine Upward]

		export : define [hookStartL] : begin
			return : list
				g4.up.start xLHook (top - hook) [widths.rhs.heading fine Upward]
				arch.rhs top (sw -- stroke) (swBefore -- fine)
		export : define [hookStartR] : begin
			return : list
				g4.up.start xRHook (top - hook) [widths.lhs.heading fine Upward]
				arch.lhs top (sw -- stroke) (swBefore -- fine)
		export : define [hookEndL] : begin
			return : list
				arch.rhs bottom (sw -- stroke) (swAfter -- fine)
				g4.up.end xLHook (bottom + hook) [widths.rhs.heading fine Upward]
		export : define [hookEndR] : begin
			return : list
				arch.lhs bottom (sw -- stroke) (swAfter -- fine)
				g4.up.end xRHook (bottom + hook) [widths.lhs.heading fine Upward]

		export : define [serifStartL] : begin
			return : tagged 'arcStartSerifL' : VBar.l left (top - hook) top barSw
		export : define [serifStartR] : begin
			return : tagged 'arcStartSerifR' : VBar.r right (top - hook) top barSw
		export : define [serifEndL] : begin
			return : tagged 'arcEndSerifL' : VBar.l left (bottom + hook) bottom barSw
		export : define [serifEndR] : begin
			return : tagged 'arcEndSerifR' : VBar.r right (bottom + hook) bottom barSw

	define flex-params [Toothless] : namespace
		local-parameter : left
		local-parameter : right
		local-parameter : top      -- XH
		local-parameter : bottom   -- 0
		local-parameter : ada      -- SmallArchDepthA
		local-parameter : adb      -- SmallArchDepthB
		local-parameter : stroke   -- Stroke

		local-parameter : rise     -- DToothlessRise
		local-parameter : barSw    -- [ArcStartSerifWidth stroke]
		local-parameter : hook     -- Hook

		local serifDepth : Math.max hook (rise + stroke)

		export : define yLT : top - rise
		export : define yRT : top - rise
		export : define yLB : bottom + rise
		export : define yRB : bottom + rise
		export : define xLArc  left
		export : define xRArc  right
		export : define xLHook left
		export : define xRHook right

		export : define [arcStartL] : begin
			return : list
				g4 xLArc (top - rise) [widths.rhs stroke]
				arch.rhs top (sw -- stroke) (blendPre -- {})
		export : define [arcStartR] : begin
			return : list
				g4 xRArc (top - rise) [widths.lhs stroke]
				arch.lhs top (sw -- stroke) (blendPre -- {})
		export : define [arcEndL] : begin
			return : list
				arch.rhs bottom (sw -- stroke) (blendPost -- {})
				g4 xLArc (bottom + rise)
		export : define [arcEndR] : begin
			return : list
				arch.lhs bottom (sw -- stroke) (blendPost -- {})
				g4 xRArc (bottom + rise)

		export : define [hookStartL] : begin
			return : list
				g2 xLHook (top - rise) [widths.rhs stroke]
				arch.rhs top (sw -- stroke) (blendPre -- {})
		export : define [hookStartR] : begin
			return : list
				g2 xRHook (top - rise) [widths.lhs stroke]
				arch.lhs top (sw -- stroke) (blendPre -- {})
		export : define [hookEndL] : begin
			return : list
				arch.rhs bottom (sw -- stroke) (blendPost -- {})
				g4 xLHook (bottom + rise)
		export : define [hookEndR] : begin
			return : list
				arch.lhs bottom (sw -- stroke) (blendPost -- {})
				g4 xRHook (bottom + rise)

		export : define [serifStartL] : begin
			return : tagged 'arcStartSerifL' : VBar.l left (top - serifDepth) (top - rise) barSw
		export : define [serifStartR] : begin
			return : tagged 'arcStartSerifR' : VBar.r right (top - serifDepth) (top - rise) barSw
		export : define [serifEndL] : begin
			return : tagged 'arcEndSerifL' : VBar.l left (bottom + serifDepth) (bottom + rise) barSw
		export : define [serifEndR] : begin
			return : tagged 'arcEndSerifR' : VBar.r right (bottom + serifDepth) (bottom + rise) barSw

	define flex-params [Rounded] : namespace
		local-parameter : left
		local-parameter : right
		local-parameter : top      -- XH
		local-parameter : bottom   -- 0
		local-parameter : ada      -- SmallArchDepthA
		local-parameter : adb      -- SmallArchDepthB
		local-parameter : stroke   -- Stroke

		local-parameter : barSw    -- [ArcStartSerifWidth stroke]
		local-parameter : hook     -- Hook

		export : define yLT : top - ada
		export : define yRT : top - adb
		export : define yLB : bottom + adb
		export : define yRB : bottom + ada
		export : define xLArc  left
		export : define xRArc  right
		export : define xLHook left
		export : define xRHook right

		export : define [arcStartL] : begin
			return : list
				g4.up.start left (top - ada) [widths.rhs.heading stroke Upward]
				arch.rhs top (sw -- stroke)
		export : define [arcStartR] : begin
			return : list
				g4.up.start right (top - adb) [widths.lhs.heading stroke Upward]
				arch.lhs top (sw -- stroke)
		export : define [arcEndL] : begin
			return : list
				arch.rhs bottom (sw -- stroke)
				g4.up.end left (bottom + adb) [heading Upward]
		export : define [arcEndR] : begin
			return : list
				arch.lhs bottom (sw -- stroke) (blendPost -- {})
				g4.up.end right (bottom + ada) [heading Upward]

		export : define [hookStartL] : begin
			return : list
				g4 left (top - hook) [widths.rhs.heading stroke Upward]
				hookstart top (sw -- stroke)
		export : define [hookStartR] : begin
			return : list
				g4 right (top - hook) [widths.lhs.heading stroke Upward]
				hookstart top (sw -- stroke)
		export : define [hookEndL] : begin
			return : list
				hookend bottom (sw -- stroke)
				g4 left (bottom + hook) [heading Upward]
		export : define [hookEndR] : begin
			return : list
				hookend bottom (sw -- stroke)
				g4 right (bottom + hook) [heading Upward]

		export : define [serifStartL] : return : glyph-proc
		export : define [serifStartR] : return : glyph-proc
		export : define [serifEndL] : return : glyph-proc
		export : define [serifEndR] : return : glyph-proc

	define flex-params [Flat] : namespace
		local-parameter : left
		local-parameter : right
		local-parameter : top      -- XH
		local-parameter : bottom   -- 0
		local-parameter : ada      -- SmallArchDepthA
		local-parameter : adb      -- SmallArchDepthB
		local-parameter : stroke   -- Stroke

		local-parameter : slab     -- 0
		local-parameter : barSw    -- [ArcStartSerifWidth stroke]
		local-parameter : hook     -- Hook
		local-parameter : midShift -- (right - left) * (-0.05)
		local-parameter : xShrink  -- [HSwToV : 0.25 * barSw]

		local serifShrink : Math.max 0 (xShrink - [HSwToV : 0.5 * barSw])
		local midX : mix left right 0.5

		export : define yLT top
		export : define yRT top
		export : define yLB bottom
		export : define yRB bottom
		export : define xLArc  left
		export : define xRArc  right
		export : define xLHook left
		export : define xRHook right

		export : define [arcStartL] : begin
			return : list
				corner left top [widths.rhs.heading stroke Rightward]
				curl [arch.adjust-x.top (midX + midShift) stroke] top
				archv
		export : define [arcStartR] : begin
			return : list
				corner right top [widths.lhs.heading stroke Leftward]
				curl [arch.adjust-x.top (midX - midShift) stroke] top
				archv
		export : define [arcEndL] : begin
			return : list
				arcvh
				flat [arch.adjust-x.bot (midX + midShift) stroke] bottom [widths.rhs stroke]
				corner left bottom [heading Leftward]
		export : define [arcEndR] : begin
			return : list
				arcvh
				flat [arch.adjust-x.bot (midX - midShift) stroke] bottom [widths.lhs stroke]
				corner right bottom [heading Rightward]
		export : define [hookStartL] : begin
			return : list
				corner (left + xShrink) top [widths.rhs.heading stroke Rightward]
				curl [arch.adjust-x.top (midX + midShift) stroke] top
				archv.superness DesignParameters.tightHookSuperness
		export : define [hookStartR] : begin
			return : list
				corner (right - xShrink) top [widths.lhs.heading stroke Leftward]
				curl [arch.adjust-x.top (midX - midShift) stroke] top
				archv.superness DesignParameters.tightHookSuperness
		export : define [hookEndL] : begin
			return : list
				arcvh.superness DesignParameters.tightHookSuperness
				flat [arch.adjust-x.bot (midX + midShift) stroke] bottom [widths.rhs stroke]
				corner (left + xShrink) bottom [heading Leftward]
		export : define [hookEndR] : begin
			return : list
				arcvh.superness DesignParameters.tightHookSuperness
				flat [arch.adjust-x.bot (midX - midShift) stroke] bottom [widths.lhs stroke]
				corner (right - xShrink) bottom [heading Rightward]

		export : define [serifStartL] : begin
			return : if slab
				tagged 'arcStartSerifL' : VBar.l (left + serifShrink) (top - hook) top barSw
				glyph-proc
		export : define [serifStartR] : begin
			return : if slab
				tagged 'arcStartSerifR' : VBar.r (right - serifShrink) (top - hook) top barSw
				glyph-proc
		export : define [serifEndL] : begin
			return : if slab
				tagged 'arcEndSerifL' : VBar.l (left + serifShrink) (bottom + hook) bottom barSw
				glyph-proc
		export : define [serifEndR] : begin
			return : if slab
				tagged 'arcEndSerifR' : VBar.r (right - serifShrink) (bottom + hook) bottom barSw
				glyph-proc
