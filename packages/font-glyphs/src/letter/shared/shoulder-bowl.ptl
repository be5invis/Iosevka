$$include '../../meta/macros.ptl'

import [mix linreg clamp fallback SuffixCfg] from "@iosevka/util"
import [DependentSelector] from "@iosevka/glyph/relation"
import [Box] from "@iosevka/geometry/box"

glyph-module

glyph-block Letter-Shoulder-Bowl : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives
	glyph-block-import Letter-Arc-Hook : ArcStartSerifWidth

	define flex-params [BowlT] : namespace
		local-parameter : arcCStart
		local-parameter : arcCEnd

		local-parameter : box
		local-parameter : ada   -- SmallArchDepthA
		local-parameter : adb   -- SmallArchDepthB
		local-parameter : sw    -- Stroke

		local-parameter : barSw -- [ArcStartSerifWidth sw]
		local-parameter : ox    -- OX


		define realAda : ArchDepthAClamped box.t box.b ada adb
		define realAdb : ArchDepthBClamped box.t box.b ada adb
		export : define ArcStart : arcCStart box (ada -- realAda) (adb -- realAdb) (sw -- sw) (barSw -- barSw)
		export : define ArcEnd   : arcCEnd   box (ada -- realAda) (adb -- realAdb) (sw -- sw) (barSw -- barSw)

		export : define shape : namespace
			export : define [arcL] : dispiro
				ArcStart.arcStartR
				flatside.ld box.l box.b box.t ada adb ox
				ArcEnd.arcEndR
			export : define [arcR] : dispiro
				ArcStart.arcStartL
				flatside.rd box.r box.b box.t ada adb ox
				ArcEnd.arcEndL
			export : define [hookL] : dispiro
				ArcStart.hookStartR
				flatside.ld box.l box.b box.t ada adb ox
				ArcEnd.hookEndR
			export : define [hookR] : dispiro
				ArcStart.hookStartL
				flatside.rd box.r box.b box.t ada adb ox
				ArcEnd.hookEndL

		export : define mask : namespace
			export : define [arcL] : spiro-outline
				ArcStart.arcStartR
				flatside.ld box.l box.b box.t ada adb ox
				ArcEnd.arcEndR
			export : define [arcR] : spiro-outline
				ArcStart.arcStartL
				flatside.rd box.r box.b box.t ada adb ox
				ArcEnd.arcEndL
			export : define [hookL] : spiro-outline
				ArcStart.hookStartR
				flatside.ld box.l box.b box.t ada adb ox
				ArcEnd.hookEndR
			export : define [hookR] : spiro-outline
				ArcStart.hookStartL
				flatside.rd box.r box.b box.t ada adb ox
				ArcEnd.hookEndL

	define flex-params [ShoulderT] : namespace
		local-parameter : arcC

		local-parameter : box
		local-parameter : ada   -- SmallArchDepthA
		local-parameter : adb   -- SmallArchDepthB
		local-parameter : sw    -- Stroke

		local-parameter : barSw -- [ArcStartSerifWidth sw]

		export : define Arc : arcC box (ada -- ada) (adb -- adb) (sw -- sw) (barSw -- barSw)

		export : define shape : namespace
			export : define [arcLT] : dispiro
				Arc.arcStartL
				flat box.r (box.t - adb)
				curl box.r  box.b [widths.rhs.heading sw Downward]
			export : define [arcRB] : dispiro
				flat box.l  box.t [widths.lhs.heading sw Downward]
				curl box.l (box.b + adb)
				Arc.arcEndR
			export : define [hookLT] : dispiro
				Arc.hookStartL
				flat box.r (box.t - adb)
				curl box.r  box.b [widths.rhs.heading sw Downward]
			export : define [hookRB] : dispiro
				flat box.l  box.t [widths.lhs.heading sw Downward]
				curl box.l (box.b + adb)
				Arc.hookEndR

		export : define mask : namespace
			export : define [arcLT] : spiro-outline
				Arc.arcStartL
				flat   box.r (box.t - adb)
				corner box.r  box.b
				corner Arc.xLArc box.b
			export : define [arcRB] : spiro-outline
				corner Arc.xRArc box.t
				corner box.l  box.t
				curl   box.l (box.b + adb)
				Arc.arcEndR
			export : define [hookLT] : spiro-outline
				Arc.hookStartL
				flat   box.r (box.t - adb)
				corner box.r  box.b
				corner Arc.xLHook box.b
			export : define [hookRB] : spiro-outline
				corner Arc.xRHook box.t
				corner box.l  box.t
				curl   box.l (box.b + adb)
				Arc.hookEndR


	do "tests"
		glyph-block-import Letter-Arc-Hook : Toothed Toothless Rounded Flat
		# Simplified impl of double storey a
		define [afunc ac1 ac2 code name] : begin
			create-glyph name code : glyph-proc
				local df : DivFrame 1
				local box : new Box XH 0 df.leftSB df.rightSB
				local ArcC1 : ac1 box (sw -- df.mvs) (hook -- AHook)

				local barTop : XH * OverlayPos * 1.02 + df.mvs * 0.425
				local box2 : new Box barTop 0 df.leftSB df.rightSB
				local Bowl : BowlT Flat ac2 box2 SmallArchDepthA SmallArchDepthB (stroke -- df.mvs)
				include : dispiro
					ArcC1.hookStartL
					flat df.rightSB (XH - SmallArchDepthB)
					curl df.rightSB Bowl.ArcEnd.yRB [heading Downward]
				include : Bowl.shape.arcL
				include : ArcC1.serifStartL

		# Simplified impl of b
		define [bfunc ac1 ac2 code name] : begin
			create-glyph name code : glyph-proc
				local df : DivFrame 1
				local box : new Box XH 0 df.leftSB df.rightSB
				local Bowl : BowlT ac1 ac2 box SmallArchDepthA SmallArchDepthB (stroke -- df.mvs)

				include : Bowl.shape.arcR
				include : VBar.l df.leftSB Bowl.ArcEnd.yLB Ascender df.mvs

		# Simplified impl of c
		define [cfunc ac1 ac2 code name] : begin
			create-glyph name code : glyph-proc
				local df : DivFrame 1
				local box : new Box XH 0 df.leftSB df.rightSB
				local Bowl : BowlT ac1 ac2 box SmallArchDepthA SmallArchDepthB (stroke -- df.mvs)

				include : Bowl.shape.hookL
				include : Bowl.ArcStart.serifStartR
				include : Bowl.ArcEnd.serifEndR

		# Simplified impl of single storey a
		define [aafunc ac1 ac2 code name] : begin
			create-glyph name code : glyph-proc
				local df : DivFrame 1
				local box : new Box XH 0 df.leftSB df.rightSB
				local Bowl : BowlT ac1 ac2 box SmallArchDepthA SmallArchDepthB (stroke -- df.mvs)

				include : Bowl.shape.arcL
				include : VBar.r df.rightSB Bowl.ArcEnd.yRB Bowl.ArcStart.yRT df.mvs

		# Simplified impl of e
		define [efunc ac1 ac2 code name] : begin
			create-glyph name code : glyph-proc
				local df : DivFrame 1
				local box : new Box XH 0 (df.leftSB + OX) (df.rightSB - OX)
				# local ArcC1 : ac1 (df.leftSB + OX) (df.rightSB - OX) XH 0 (stroke -- df.mvs)
				local ArcC2 : ac2 box (stroke -- df.mvs) (hook -- SHook)

				local barBottom : XH * DesignParameters.eBarPos - (df.mvs / 2)
				include : HBar.b
					df.leftSB + [HSwToV : 0.5 * df.mvs] + OX
					df.rightSB - [HSwToV : 0.5 * df.mvs] - OX
					* barBottom
					* df.mvs
				local path : include : dispiro
					widths.lhs df.mvs
					flat (df.rightSB - OX) barBottom [heading Upward]
					curl (df.rightSB - OX) (XH - SmallArchDepthB)
					arch.lhs XH (sw -- df.mvs)
					flatside.ld df.leftSB 0 XH SmallArchDepthA SmallArchDepthB 0
					ArcC2.hookEndR
				# include : ArcC1.serifStartR
				include : ArcC2.serifEndR

		# Simplified impl of ь (?)
		define [bbfunc ac1 ac2 code name] : begin
			create-glyph name code : glyph-proc
				local df : DivFrame 1
				define YeriBarPos 0.55
				local yArcTop : XH * YeriBarPos + df.mvs / 2

				local box : new Box yArcTop 0 df.leftSB df.rightSB
				local Bowl : BowlT ac1 ac2 box SmallArchDepthA SmallArchDepthB (stroke -- df.mvs)

				include : Bowl.shape.arcR
				include : VBar.l df.leftSB Bowl.ArcEnd.yLB XH df.mvs

		# mask test based on double storey a shape
		define [amaskfunc ac1 ac2 code name] : begin
			create-glyph name code : glyph-proc
				local df : DivFrame 1
				local box : new Box XH 0 df.leftSB df.rightSB
				local Shoulder : ShoulderT ac1 box SmallArchDepthA SmallArchDepthB (stroke -- df.mvs)

				local barTop : XH * OverlayPos * 1.02 + df.mvs * 0.425
				local box2 : new Box barTop 0 df.leftSB df.rightSB
				local Bowl : BowlT ac1 ac2 box2 SmallArchDepthA SmallArchDepthB (stroke -- df.mvs)
				include : difference
					union
						Shoulder.Arc.serifStartL
						Shoulder.mask.hookLT         # Ignore connection with bowl
					Bowl.mask.arcL

		# Simplified? impl of տ
		define [tiwnfunc ac1 ac2 code name] : begin
			create-glyph name code : glyph-proc
				local df : DivFrame 1 3.5
				local boxL : new Box XH 0 df.leftSB (df.middle + [HSwToV : 0.5 * df.mvs])
				local boxR : new Box XH 0 (df.middle - [HSwToV : 0.5 * df.mvs]) df.rightSB
				local UShoulder : ShoulderT ac2 boxL SmallArchDepthA SmallArchDepthB (stroke -- df.mvs)
				local NShoulder : ShoulderT ac1 boxR SmallArchDepthA SmallArchDepthB (stroke -- df.mvs)
				include : UShoulder.shape.arcRB
				include : VBar.m df.middle UShoulder.Arc.yRB NShoulder.Arc.yLT df.mvs
				include : NShoulder.shape.arcLT

		define mat : SuffixCfg.weave
			object
				"tht" { Toothed   0 }
				"thl" { Toothless 4 }
				"rnd" { Rounded   8 }
				"flt" { Flat      12 }
			object
				"tht" { Toothed   0 }
				"thl" { Toothless 1 }
				"rnd" { Rounded   2 }
				"flt" { Flat      3 }

		foreach { suffix { { ac1 di1 } { ac2 di2 } } } [Object.entries mat] : do
			afunc ac1 ac2 (0xE100 + di1 + di2) "aaaa-\(suffix)"
			bfunc ac1 ac2 (0xE110 + di1 + di2) "bbbb-\(suffix)"
			cfunc ac1 ac2 (0xE120 + di1 + di2) "cccc-\(suffix)"
			aafunc ac1 ac2 (0xE130 + di1 + di2) "dddd-\(suffix)"
			efunc ac1 ac2 (0xE140 + di1 + di2) "eeee-\(suffix)"
			bbfunc ac1 ac2 (0xE150 + di1 + di2) "iiii-\(suffix)"
			amaskfunc ac1 ac2 (0xE160 + di1 + di2) "jjjj-\(suffix)"
			tiwnfunc ac1 ac2 (0xE170 + di1 + di2) "nnnn-\(suffix)"
