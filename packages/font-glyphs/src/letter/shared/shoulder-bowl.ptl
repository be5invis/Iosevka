$$include '../../meta/macros.ptl'

import [mix linreg clamp fallback SuffixCfg] from "@iosevka/util"
import [DependentSelector] from "@iosevka/glyph/relation"

glyph-module

glyph-block Letter-Shoulder-Bowl : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives

	define flex-params [BowlT] : namespace
		local-parameter : arcCStart
		local-parameter : arcCEnd

		local-parameter : left
		local-parameter : right
		local-parameter : top      -- XH
		local-parameter : bottom   -- 0
		local-parameter : ada      -- SmallArchDepthA
		local-parameter : adb      -- SmallArchDepthB
		local-parameter : stroke   -- Stroke

		local-parameter : ox       -- OX


		define realAda : ArchDepthAClamped top bottom ada adb
		define realAdb : ArchDepthBClamped top bottom ada adb
		export : define ArcStart : arcCStart left right top bottom (ada -- realAda) (adb -- realAdb) (stroke -- stroke)
		export : define ArcEnd   : arcCEnd   left right top bottom (ada -- realAda) (adb -- realAdb) (stroke -- stroke)

		export : define shape : namespace
			export : define [arcL] : dispiro
				ArcStart.arcStartR
				flatside.ld left  bottom top ada adb ox
				ArcEnd.arcEndR
			export : define [arcR] : dispiro
				ArcStart.arcStartL
				flatside.rd right bottom top ada adb ox
				ArcEnd.arcEndL
			export : define [hookL] : dispiro
				ArcStart.hookStartR
				flatside.ld left  bottom top ada adb ox
				ArcEnd.hookEndR
			export : define [hookR] : dispiro
				ArcStart.hookStartL
				flatside.rd right bottom top ada adb ox
				ArcEnd.hookEndL

		export : define mask : namespace
			export : define [arcL] : spiro-outline
				ArcStart.arcStartR
				flatside.ld left  bottom top ada adb ox
				ArcEnd.arcEndR
			export : define [arcR] : spiro-outline
				ArcStart.arcStartL
				flatside.rd right bottom top ada adb ox
				ArcEnd.arcEndL

	define flex-params [ShoulderT] : namespace
		local-parameter : arcC

		local-parameter : left
		local-parameter : right
		local-parameter : top      -- XH
		local-parameter : bottom   -- 0
		local-parameter : ada      -- SmallArchDepthA
		local-parameter : adb      -- SmallArchDepthB
		local-parameter : stroke   -- Stroke

		export : define Arc : arcC left right top bottom (ada -- ada) (adb -- adb) (stroke -- stroke)

		export : define shape : namespace
			export : define [arcLT] : dispiro
				Arc.arcStartL
				flat right (top - adb)
				curl right  bottom
			export : define [arcRB] : dispiro
				flat left   top
				curl left  (bottom + adb)
				Arc.arcEndR
			export : define [hookLT] : dispiro
				Arc.hookStartL
				flat right (top - adb)
				curl right  bottom
			export : define [hookRB] : dispiro
				flat left   top
				curl left  (bottom + adb)
				Arc.hookEndR

		export : define mask : namespace
			export : define [arcLT] : spiro-outline
				Arc.arcStartL
				flat   right (top - adb)
				corner right  bottom
				corner Arc.xLStroke bottom
			export : define [arcRB] : spiro-outline
				corner Arc.xRStroke top
				corner left   top
				curl   left  (bottom + adb)
				Arc.arcEndR
			export : define [hookLT] : spiro-outline
				Arc.hookStartL
				flat   right (top - adb)
				corner right  bottom
				corner Arc.xLStroke bottom
			export : define [hookRB] : spiro-outline
				corner Arc.xRStroke top
				corner left   top
				curl   left  (bottom + adb)
				Arc.hookEndR


	do "tests"
		glyph-block-import Letter-Arc-Hook : Toothed Toothless Rounded Flat
		# Simplified impl of double storey a
		define [afunc ac1 ac2 code name] : begin
			create-glyph name code : glyph-proc
				local df : DivFrame 1
				local ArcC1 : ac1 df.leftSB df.rightSB XH 0 (stroke -- df.mvs) (hook -- AHook)
				local barTop : XH * OverlayPos * 1.02 + df.mvs * 0.425
				local Bowl : BowlT Flat ac2 df.leftSB df.rightSB barTop 0 SmallArchDepthA SmallArchDepthB (stroke -- df.mvs)
				include : dispiro
					ArcC1.hookStartL
					flat df.rightSB (XH - SmallArchDepthB)
					curl df.rightSB Bowl.ArcEnd.yRB [heading Downward]
				include : Bowl.shape.arcL
				include : ArcC1.serifStartL

		# Simplified impl of b
		define [bfunc ac1 ac2 code name] : begin
			create-glyph name code : glyph-proc
				local df : DivFrame 1
				local Bowl : BowlT ac1 ac2 df.leftSB df.rightSB XH 0 SmallArchDepthA SmallArchDepthB (stroke -- df.mvs)

				include : Bowl.shape.arcR
				include : VBar.l df.leftSB Bowl.ArcEnd.yLB Ascender df.mvs

		# Simplified impl of c
		define [cfunc ac1 ac2 code name] : begin
			create-glyph name code : glyph-proc
				local df : DivFrame 1
				local Bowl : BowlT ac1 ac2 df.leftSB df.rightSB XH 0 SmallArchDepthA SmallArchDepthB (stroke -- df.mvs)

				include : Bowl.shape.hookL
				include : Bowl.ArcStart.serifStartR
				include : Bowl.ArcEnd.serifEndR

		# Simplified impl of single storey a
		define [aafunc ac1 ac2 code name] : begin
			create-glyph name code : glyph-proc
				local df : DivFrame 1
				local Bowl : BowlT ac1 ac2 df.leftSB df.rightSB XH 0 SmallArchDepthA SmallArchDepthB (stroke -- df.mvs)

				include : Bowl.shape.arcL
				include : VBar.r df.rightSB Bowl.ArcEnd.yRB Bowl.ArcStart.yRT df.mvs

		# Simplified impl of e
		define [efunc ac1 ac2 code name] : begin
			create-glyph name code : glyph-proc
				local df : DivFrame 1
				# local ArcC1 : ac1 (df.leftSB + OX) (df.rightSB - OX) XH 0 (stroke -- df.mvs)
				local ArcC2 : ac2 (df.leftSB + OX) (df.rightSB - OX) XH 0 (stroke -- df.mvs) (hook -- SHook)

				local barBottom : XH * DesignParameters.eBarPos - (df.mvs / 2)
				include : HBar.b
					df.leftSB + [HSwToV : 0.5 * df.mvs] + OX
					df.rightSB - [HSwToV : 0.5 * df.mvs] - OX
					* barBottom
					* df.mvs
				local path : include : dispiro
					widths.lhs df.mvs
					flat (df.rightSB - OX) barBottom [heading Upward]
					curl (df.rightSB - OX) (XH - SmallArchDepthB)
					arch.lhs XH (sw -- df.mvs)
					flatside.ld df.leftSB 0 XH SmallArchDepthA SmallArchDepthB 0
					ArcC2.hookEndR
				# include : ArcC1.serifStartR
				include : ArcC2.serifEndR

		# Simplified impl of ь (?)
		define [bbfunc ac1 ac2 code name] : begin
			create-glyph name code : glyph-proc
				local df : DivFrame 1
				define YeriBarPos 0.55
				local yArcTop : XH * YeriBarPos + df.mvs / 2

				local Bowl : BowlT ac1 ac2 df.leftSB df.rightSB yArcTop 0 SmallArchDepthA SmallArchDepthB (stroke -- df.mvs)

				include : Bowl.shape.arcR
				include : VBar.l df.leftSB Bowl.ArcEnd.yLB XH df.mvs

		# mask test based on double storey a shape
		define [amaskfunc ac1 ac2 code name] : begin
			create-glyph name code : glyph-proc
				local df : DivFrame 1
				local Shoulder : ShoulderT ac1 df.leftSB df.rightSB XH 0 SmallArchDepthA SmallArchDepthB (stroke -- df.mvs)
				local barTop : XH * OverlayPos * 1.02 + df.mvs * 0.425
				local Bowl : BowlT ac1 ac2 df.leftSB df.rightSB barTop 0 SmallArchDepthA SmallArchDepthB (stroke -- df.mvs)
				include : difference
					union
						Shoulder.Arc.serifStartL
						Shoulder.mask.hookLT         # Ignore connection with bowl
					Bowl.mask.arcL

		# Simplified? impl of տ
		define [tiwnfunc ac1 ac2 code name] : begin
			create-glyph name code : glyph-proc
				local df : DivFrame 1 3
				local UShoulder : ShoulderT ac2 df.leftSB (df.middle + [HSwToV : 0.5 * df.mvs])  XH 0 SmallArchDepthA SmallArchDepthB (stroke -- df.mvs)
				local NShoulder : ShoulderT ac1 (df.middle - [HSwToV : 0.5 * df.mvs]) df.rightSB XH 0 SmallArchDepthA SmallArchDepthB (stroke -- df.mvs)
				include : UShoulder.shape.arcRB
				include : VBar.m df.middle 0 XH df.mvs
				include : NShoulder.shape.arcLT

		define mat : SuffixCfg.weave
			object
				"tht" { Toothed   0 }
				"thl" { Toothless 4 }
				"rnd" { Rounded   8 }
				"flt" { Flat      12 }
			object
				"tht" { Toothed   0 }
				"thl" { Toothless 1 }
				"rnd" { Rounded   2 }
				"flt" { Flat      3 }

		foreach { suffix { { ac1 di1 } { ac2 di2 } } } [Object.entries mat] : do
			afunc ac1 ac2 (0xE100 + di1 + di2) "aaaa-\(suffix)"
			bfunc ac1 ac2 (0xE110 + di1 + di2) "bbbb-\(suffix)"
			cfunc ac1 ac2 (0xE120 + di1 + di2) "cccc-\(suffix)"
			aafunc ac1 ac2 (0xE130 + di1 + di2) "dddd-\(suffix)"
			efunc ac1 ac2 (0xE140 + di1 + di2) "eeee-\(suffix)"
			bbfunc ac1 ac2 (0xE150 + di1 + di2) "iiii-\(suffix)"
			amaskfunc ac1 ac2 (0xE160 + di1 + di2) "jjjj-\(suffix)"
			tiwnfunc ac1 ac2 (0xE170 + di1 + di2) "nnnn-\(suffix)"
