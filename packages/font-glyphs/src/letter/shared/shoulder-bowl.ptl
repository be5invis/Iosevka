$$include '../../meta/macros.ptl'

import [mix linreg clamp fallback SuffixCfg] from "@iosevka/util"
import [DependentSelector] from "@iosevka/glyph/relation"

glyph-module

glyph-block Letter-Shoulder-Bowl : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives

	define flex-params [Bowl] : namespace
		local-parameter : arcClassStart
		local-parameter : arcClassEnd

		local-parameter : left
		local-parameter : right
		local-parameter : top      -- XH
		local-parameter : bottom   -- 0
		local-parameter : ada      -- SmallArchDepthA
		local-parameter : adb      -- SmallArchDepthB
		local-parameter : stroke   -- Stroke

		local-parameter : ox       -- OX


		define realAda : ArchDepthAClamped top bottom ada adb
		define realAdb : ArchDepthBClamped top bottom ada adb
		export : define ArcInstStart : arcClassStart left right top bottom (ada -- realAda) (adb -- realAdb) (stroke -- stroke)
		export : define ArcInstEnd   : arcClassEnd   left right top bottom (ada -- realAda) (adb -- realAdb) (stroke -- stroke)

		export : define shape : namespace
			export : define [lhs] : dispiro
				ArcInstStart.arcStartR
				flatside.ld left  bottom top ada adb ox
				ArcInstEnd.arcEndR
			export : define [rhs] : dispiro
				ArcInstStart.arcStartL
				flatside.rd right bottom top ada adb ox
				ArcInstEnd.arcEndL

		export : define mask : namespace
			export : define [lhs] : spiro-outline
				ArcInstStart.arcStartR
				flatside.ld left  bottom top ada adb ox
				ArcInstEnd.arcEndR
			export : define [rhs] : spiro-outline
				ArcInstStart.arcStartL
				flatside.rd right bottom top ada adb ox
				ArcInstEnd.arcEndL

	define flex-params [Shoulder] : namespace
		local-parameter : arcClass

		local-parameter : left
		local-parameter : right
		local-parameter : top      -- XH
		local-parameter : bottom   -- 0
		local-parameter : ada      -- SmallArchDepthA
		local-parameter : adb      -- SmallArchDepthB
		local-parameter : stroke   -- Stroke

		export : define ArcInst : arcClass left right top bottom (ada -- ada) (adb -- adb) (stroke -- stroke)

		export : define shape : namespace
			export : define [arcLT] : dispiro
				ArcInst.arcStartL
				flat right (top - adb)
				curl right  bottom
			export : define [arcRB] : dispiro
				flat left   top
				curl left  (bottom + adb)
				ArcInst.arcEndR
			export : define [hookLT] : dispiro
				ArcInst.hookStartL
				flat right (top - adb)
				curl right  bottom
			export : define [hookRB] : dispiro
				flat left   top
				curl left  (bottom + adb)
				ArcInst.hookEndR

		export : define mask : namespace
			export : define [arcLT] : spiro-outline
				corner ArcInst.xLStroke bottom
				ArcInst.arcStartL
				flat   right (top - adb)
				corner right  bottom
			export : define [arcRB] : spiro-outline
				corner left   top
				curl   left  (bottom + adb)
				ArcInst.arcEndR
				corner ArcInst.xRStroke top
			export : define [hookLT] : spiro-outline
				corner ArcInst.xLStroke bottom
				ArcInst.hookStartL
				flat   right (top - adb)
				corner right  bottom
			export : define [hookRB] : spiro-outline
				corner left   top
				curl   left  (bottom + adb)
				ArcInst.hookEndR
				corner ArcInst.xRStroke top


	do "tests"
		glyph-block-import Letter-Arc-Hook : Toothed Toothless Rounded Flat
		# Simplified impl of double storey a
		define [afunc ac1 ac2 code name] : begin
			create-glyph name code : glyph-proc
				local df : DivFrame 1
				local ArcC1 : ac1 df.leftSB df.rightSB XH 0 (stroke -- df.mvs) (hook -- AHook)
				local barTop : XH * OverlayPos * 1.02 + df.mvs * 0.425
				local BowlInst : Bowl Flat ac2 df.leftSB df.rightSB barTop 0 SmallArchDepthA SmallArchDepthB (stroke -- df.mvs)
				include : dispiro
					ArcC1.hookStartL
					flat df.rightSB (XH - SmallArchDepthB)
					curl df.rightSB BowlInst.ArcInstEnd.yRB [heading Downward]
				include : BowlInst.shape.lhs
				include : ArcC1.serifStartL

		# Simplified impl of b
		define [bfunc ac1 ac2 code name] : begin
			create-glyph name code : glyph-proc
				local df : DivFrame 1
				local BowlInst : Bowl ac1 ac2 df.leftSB df.rightSB XH 0 SmallArchDepthA SmallArchDepthB (stroke -- df.mvs)

				include : BowlInst.shape.rhs
				include : VBar.l df.leftSB BowlInst.ArcInstEnd.yLB Ascender df.mvs

		# Simplified impl of c
		define [cfunc ac1 ac2 code name] : begin
			create-glyph name code : glyph-proc
				local df : DivFrame 1
				local ArcC1 : ac1 df.leftSB df.rightSB XH 0 (stroke -- df.mvs)
				local ArcC2 : ac2 df.leftSB df.rightSB XH 0 (stroke -- df.mvs)
				include : dispiro
					ArcC1.hookStartR
					flatside.ld df.leftSB 0 XH SmallArchDepthA SmallArchDepthB 0
					ArcC2.hookEndR
				include : ArcC1.serifStartR
				include : ArcC2.serifEndR

		# Simplified impl of single storey a
		define [aafunc ac1 ac2 code name] : begin
			create-glyph name code : glyph-proc
				local df : DivFrame 1
				local BowlInst : Bowl ac1 ac2 df.leftSB df.rightSB XH 0 SmallArchDepthA SmallArchDepthB (stroke -- df.mvs)

				include : BowlInst.shape.lhs
				include : VBar.r df.rightSB BowlInst.ArcInstEnd.yRB BowlInst.ArcInstStart.yRT df.mvs

		# Simplified impl of e
		define [efunc ac1 ac2 code name] : begin
			create-glyph name code : glyph-proc
				local df : DivFrame 1
				# local ArcC1 : ac1 (df.leftSB + OX) (df.rightSB - OX) XH 0 (stroke -- df.mvs)
				local ArcC2 : ac2 (df.leftSB + OX) (df.rightSB - OX) XH 0 (stroke -- df.mvs) (hook -- SHook)

				local barBottom : XH * DesignParameters.eBarPos - (df.mvs / 2)
				include : HBar.b
					df.leftSB + [HSwToV : 0.5 * df.mvs] + OX
					df.rightSB - [HSwToV : 0.5 * df.mvs] - OX
					* barBottom
					* df.mvs
				local path : include : dispiro
					widths.lhs df.mvs
					flat (df.rightSB - OX) barBottom [heading Upward]
					curl (df.rightSB - OX) (XH - SmallArchDepthB)
					arch.lhs XH (sw -- df.mvs)
					flatside.ld df.leftSB 0 XH SmallArchDepthA SmallArchDepthB 0
					ArcC2.hookEndR
				# include : ArcC1.serifStartR
				include : ArcC2.serifEndR

		# Simplified impl of ÑŒ (?)
		define [bbfunc ac1 ac2 code name] : begin
			create-glyph name code : glyph-proc
				local df : DivFrame 1
				define YeriBarPos 0.55
				local yArcTop : XH * YeriBarPos + df.mvs / 2

				local BowlInst : Bowl ac1 ac2 df.leftSB df.rightSB yArcTop 0 SmallArchDepthA SmallArchDepthB (stroke -- df.mvs)

				include : BowlInst.shape.rhs
				include : VBar.l df.leftSB BowlInst.ArcInstEnd.yLB XH df.mvs

		# mask test based on double storey a shape
		define [amaskfunc ac1 ac2 code name] : begin
			create-glyph name code : glyph-proc
				local df : DivFrame 1
				local ShoulderInst : Shoulder ac1 df.leftSB df.rightSB XH 0 SmallArchDepthA SmallArchDepthB (stroke -- df.mvs)
				local barTop : XH * OverlayPos * 1.02 + df.mvs * 0.425
				local BowlInst : Bowl ac1 ac2 df.leftSB df.rightSB barTop 0 SmallArchDepthA SmallArchDepthB (stroke -- df.mvs)
				include : difference
					union
						ShoulderInst.ArcInst.serifStartL
						ShoulderInst.mask.hookLT         # Ignore connection with bowl
					BowlInst.mask.lhs

		define mat : SuffixCfg.weave
			object
				"tht" { Toothed   0 }
				"thl" { Toothless 4 }
				"rnd" { Rounded   8 }
				"flt" { Flat      12 }
			object
				"tht" { Toothed   0 }
				"thl" { Toothless 1 }
				"rnd" { Rounded   2 }
				"flt" { Flat      3 }

		foreach { suffix { { ac1 di1 } { ac2 di2 } } } [Object.entries mat] : do
			afunc ac1 ac2 (0xE100 + di1 + di2) "aaaa-\(suffix)"
			bfunc ac1 ac2 (0xE110 + di1 + di2) "bbbb-\(suffix)"
			cfunc ac1 ac2 (0xE120 + di1 + di2) "cccc-\(suffix)"
			aafunc ac1 ac2 (0xE130 + di1 + di2) "dddd-\(suffix)"
			efunc ac1 ac2 (0xE140 + di1 + di2) "eeee-\(suffix)"
			bbfunc ac1 ac2 (0xE150 + di1 + di2) "iiii-\(suffix)"
			amaskfunc ac1 ac2 (0xE160 + di1 + di2) "jjjj-\(suffix)"
