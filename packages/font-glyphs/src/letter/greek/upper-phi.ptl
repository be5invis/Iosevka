$$include '../../meta/macros.ptl'

import [mix linreg clamp fallback SuffixCfg] from "@iosevka/util"
import [DesignParameters] from "../../meta/aesthetics.mjs"

glyph-module

glyph-block Letter-Greek-Upper-Phi : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives
	glyph-block-import Mark-Adjustment : ExtendAboveBaseAnchors ExtendBelowBaseAnchors
	glyph-block-import Letter-Shared-Shapes : FlatHookDepth OBarLeft OBarRight
	glyph-block-import Letter-Latin-Lower-AE-OE : SubDfAndShift

	define [VarPhiRing fFlatTB df y1 y2] : glyph-proc
		local gap : Math.max df.mvs : 0.25 * (df.rightSB - df.leftSB)
		include : VBar.m df.middle y1 y2 df.mvs
		include : if fFlatTB
			OShapeFlatTB y2 y1 df.leftSB df.rightSB df.mvs (ArchDepthA * df.div) (ArchDepthB * df.div) gap
			OShape y2 y1 df.leftSB df.rightSB df.mvs (ArchDepthA * df.div) (ArchDepthB * df.div)

	define [CyrlEfSplitRing fFlatTB df y1 y2] : glyph-proc
		local { subDf } : SubDfAndShift 0 df OX
		local ada : subDf.archDepthA SmallArchDepth df.mvs
		local adb : subDf.archDepthB SmallArchDepth df.mvs

		include : VBar.m df.middle y1 y2 df.mvs
		include : with-transform [ApparentTranslate 0 y1] : union
			OBarRight.shape
				top       -- (y2 - y1)
				left      -- df.leftSB
				right     -- df.middle + [HSwToV : 0.5 * df.mvs]
				sw        -- df.mvs
				ada       -- ada
				adb       -- adb
			OBarLeft.shape
				top       -- (y2 - y1)
				left      -- df.middle - [HSwToV : 0.5 * df.mvs]
				right     -- df.rightSB
				sw        -- df.mvs
				ada       -- ada
				adb       -- adb

	define [StraightBar df bot y1 y2 top] : glyph-proc
		include : VBar.m df.middle bot (y1 + HalfStroke)
		include : VBar.m df.middle (y2 - HalfStroke) top

	define [CursiveBar df bot y1 y2 top] : glyph-proc
		local hd : FlatHookDepth df

		local xCrossLeft  : mix 0         df.leftSB   [mix 1 df.div 2]
		local xCrossRight : mix df.width  df.rightSB  [mix 1 df.div 2]

		local xBarLeft    : df.middle - [HSwToV HalfStroke]
		local xBarRight   : df.middle + [HSwToV HalfStroke]

		include : dispiro
			flat xCrossRight top [widths.lhs]
			curl [Math.min (xBarLeft + hd.x) (xCrossRight - TINY)] top
			archv
			flat xBarLeft [Math.max y2 (top - hd.y)]
			curl xBarLeft (y2 + O)

		include : dispiro
			flat xCrossLeft bot [widths.lhs]
			curl [Math.max (xBarRight - hd.x) (xCrossLeft + TINY)] bot
			archv
			flat xBarRight [Math.min y1 (bot + hd.y)]
			curl xBarRight (y1 - O)

	define [MtSerif df y] : tagged 'serifMT' : HSerif.lt df.middle y Jut
	define [MbSerif df y] : tagged 'serifMB' : HSerif.mb df.middle y Jut

	define [GrekCapitalPhiImpl fFlatTB df] : glyph-proc
		local y1 : mix [if SLAB Stroke 0] [if SLAB (CAP - Stroke) CAP] 0.125
		local y2 : mix [if SLAB Stroke 0] [if SLAB (CAP - Stroke) CAP] 0.875
		include : VarPhiRing fFlatTB df y1 y2
		include : StraightBar df 0 y1 y2 CAP

		if SLAB : begin
			include : tagged 'serifMT' : HSerif.mt df.middle CAP MidJutSide
			include : tagged 'serifMB' : HSerif.mb df.middle 0 MidJutSide

	create-glyph 'grek/Phi' 0x3A6 : glyph-proc
		local df : include : DivFrame para.diversityM 3
		include : df.markSet.capital
		include : GrekCapitalPhiImpl 0 df

	create-glyph 'cyrl/Ef' 0x424 : glyph-proc
		local df : include : DivFrame para.diversityM 3
		include : df.markSet.capital
		include : GrekCapitalPhiImpl 1 df

	create-glyph 'cyrl/Ef.BGR' : glyph-proc
		local df : include : DivFrame para.diversityM 3
		include : df.markSet.capital

		local top : CAP + LongJut - [if SLAB 0 HalfStroke]
		local bot : 0   - LongJut + [if SLAB 0 HalfStroke]

		include : ExtendAboveBaseAnchors top
		include : ExtendBelowBaseAnchors bot

		include : VarPhiRing 0 df 0 CAP
		include : StraightBar df bot 0 CAP top

		if SLAB : begin
			include : tagged 'serifMT' : HSerif.mt df.middle top MidJutSide
			include : tagged 'serifMB' : HSerif.mb df.middle bot MidJutSide

	create-glyph 'grek/varphi' 0x3D5 : glyph-proc
		local df : include : DivFrame para.diversityM 3
		include : df.markSet.bp

		include : VarPhiRing 0 df 0 XH
		include : StraightBar df Descender 0 XH Ascender

	create-glyph 'latn/phi' 0x278 : glyph-proc
		local df : include : DivFrame para.diversityM 3
		include [refer-glyph 'grek/varphi'] AS_BASE ALSO_METRICS

		if SLAB : begin
			include : tagged 'serifMT' : HSerif.mt df.middle Ascender Jut
			include : tagged 'serifMB' : HSerif.mb df.middle Descender Jut

	define CyrlLowerEfConfig : SuffixCfg.weave
		object # bowl
			""                  VarPhiRing
			splitBowl           CyrlEfSplitRing
		object # bar
			serifless          { StraightBar nothing nothing }
			topSerifed         { StraightBar MtSerif nothing }
			serifed            { StraightBar MtSerif MbSerif }
			cursive            { CursiveBar  nothing nothing }

	foreach { suffix { Bowl { Bar sMT sMB } } } [Object.entries CyrlLowerEfConfig] : do
		create-glyph "cyrl/ef.\(suffix)" : glyph-proc
			local df : include : DivFrame para.diversityM 3
			include : df.markSet.bp
			include : Bowl 1 df 0 XH
			include : Bar df Descender 0 XH Ascender
			if sMT : include : sMT df Ascender
			if sMB : include : sMB df Descender
		create-glyph "cyrl/ef.BGR.\(suffix)" : glyph-proc
			local df : include : DivFrame para.diversityM 3
			include : df.markSet.bp
			include : VarPhiRing 0 df 0 XH
			include : Bar df Descender 0 XH Ascender
			if sMT : include : sMT df Ascender
			if sMB : include : sMB df Descender

	select-variant 'cyrl/ef' 0x444
	select-variant 'cyrl/ef.BGR'
