$$include '../../meta/macros.ptl'

import [mix linreg clamp fallback] from "@iosevka/util"
import [DesignParameters] from "../../meta/aesthetics.mjs"

glyph-module

glyph-block Letter-Greek-Upper-Xi : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives
	glyph-block-import Letter-Latin-Upper-F : EFVJutLength

	create-glyph 'grek/Xi' 0x39E : glyph-proc
		include : MarkSet.capital
		local stroke : AdviceStroke2 2 3 CAP
		local xMidLeft  : mix SB RightSB 0.125
		local xMidRight : mix RightSB SB 0.125
		local yBar : mix 0 CAP DesignParameters.upperEBarPos
		include : HBar.t (SB + OX) (RightSB - OX) CAP stroke
		include : HBar.b (SB + OX) (RightSB - OX) 0   stroke
		include : HBar.m xMidLeft xMidRight yBar stroke
		if SLAB : begin
			local { jutTop jutBot jutMid } : EFVJutLength CAP DesignParameters.upperEBarPos stroke
			local swVJut : VJutStroke * (stroke / Stroke)
			include : VSerif.dl (SB      + OX) CAP jutTop swVJut
			include : VSerif.dr (RightSB - OX) CAP jutTop swVJut
			include : VSerif.ul (SB      + OX) 0   jutBot swVJut
			include : VSerif.ur (RightSB - OX) 0   jutBot swVJut
			local fine : Math.min swVJut : VSwToH : 0.4 * (xMidRight - xMidLeft)
			include : VBar.l xMidLeft  (yBar - jutMid) (yBar + jutMid) fine
			include : VBar.r xMidRight (yBar - jutMid) (yBar + jutMid) fine
