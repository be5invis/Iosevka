$$include '../../meta/macros.ptl'

import [mix linreg clamp fallback] from "@iosevka/util"
import [DependentSelector] from "@iosevka/glyph/relation"

glyph-module

glyph-block Letter-Armenian-Upper-Ini : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives
	glyph-block-import Letter-Shared-Shapes : nShoulder SerifFrame

	# Common Params
	define barPos : XH / 2
	define highBarPos XH
	define jut Jut

	do "Ini"
		create-glyph 'armn/Ini' 0x53B : glyph-proc
			local df : include : DivFrame 1
			include : df.markSet.capital
			local barPosB : barPos - df.mvs / 2
			include : nShoulder.shape
				top    -- highBarPos
				bottom -- barPosB
				left   -- (df.leftSB + [HSwToV df.mvs])
				right  -- df.rightSB
				ada    -- ArchDepthA
				adb    -- ArchDepthB
				stroke -- df.mvs
				fine   -- df.shoulderFine
			include : VBar.l df.leftSB 0 CAP df.mvs
			if SLAB : begin
				local sf : SerifFrame.fromDf df CAP 0
				local sf2 : SerifFrame.fromDf df highBarPos barPosB
				include : composite-proc sf.lt.full sf.lb.full sf2.rb.full
