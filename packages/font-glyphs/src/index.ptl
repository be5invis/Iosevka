import [Glyph] from "@iosevka/glyph"
import [GlyphStore] from "@iosevka/glyph/store"
import [GlyphBlock GlyphBuildExecutor] from "@iosevka/glyph/block"
import as Gr from "@iosevka/glyph/relation"
import as SpiroKit from "@iosevka/font-kits/spiro-kit"
import as BooleKit from "@iosevka/font-kits/boole-kit"
import [DesignParameters calculateMetrics setFontMetrics GenDivFrame] from "./meta/aesthetics.mjs"
import [$NamedParameterPair$] from "@iosevka/util"

extern isFinite

$$include './meta/macros.ptl'

export : define [buildGlyphs para recursive] : begin
	# Execution and dependency management
	local $Exec$ : new GlyphBuildExecutor recursive
	define [glyph-is-needed name] : [not recursive] || [recursive.glyphIsNeeded name]

	# Initialize glyph store
	local glyphStore : new GlyphStore

	# Font metrics
	define Metrics : calculateMetrics para
	define fontMetrics { .head {.} .hhea {.} .os2 {.} .post {.} }
	setFontMetrics para Metrics fontMetrics

	# Glyph metrics and parameters
	define DivFrame : GenDivFrame Metrics
	define MarkSet    [DivFrame 1 2].markSet

	# Anchor parameters
	define {AS_BASE ALSO_METRICS} {'AS-BASE' 'ALSO-METRICS'}

	# The callback used to create and save glyphs
	define [$createAndSaveGlyphImpl$ _1 _2 actions] : begin
		local saveGlyphName    null
		local unicode          null

		piecewise
			([typeof _1] === 'number' && _1) : begin
				set saveGlyphName : 'uni' + [_1.toString 16 :.padStart 4 '0' :.toUpperCase]
				set unicode          _1
			([typeof _1] === 'string' && _1) : begin
				set saveGlyphName    _1
				set unicode          _2
			true : begin
				set saveGlyphName    null
				set unicode          null

		if (saveGlyphName && [not : glyph-is-needed saveGlyphName]) : return nothing
		if para.verbose : console.log saveGlyphName

		local glyphObject [new Glyph saveGlyphName]
		glyphObject.setWidth Metrics.Width
		glyphObject.gizmo = Metrics.GlobalTransform
		glyphObject._m_dependencyManager = $Exec$.dependencyManager

		glyphObject.include actions true true

		$Exec$.setGlyphToBlockDependency glyphObject

		if saveGlyphName : begin
			if (saveGlyphName.0 != '.' && [glyphStore.queryByName saveGlyphName])
				throw : new Error "Glyph \(saveGlyphName) already exists"
			glyphStore.addGlyph saveGlyphName glyphObject
			if unicode : begin
				local u unicode
				if ([typeof unicode] === "string") : begin
					set u [unicode.codePointAt 0]
				glyphStore.encodeGlyph u glyphObject

		return glyphObject

	# Spiro kit and boole kit
	define SpiroFns : SpiroKit.SetupBuilders : object
		globalTransform Metrics.GlobalTransform
		Contrast        Metrics.Contrast
		Stroke          Metrics.Stroke
		CorrectionOMidX Metrics.CorrectionOMidX
		TINY            Metrics.TINY
		Superness       DesignParameters.superness
	define BooleFns : BooleKit.SetupBuilders : object
		globalTransform Metrics.GlobalTransform

	# Setup the capture
	define $$Capture$$ : object
		$createAndSaveGlyphImpl$
		$NamedParameterPair$
		$Exec$
		Metrics : Object.assign {.} Metrics
		para
		recursive
		glyphStore
		SpiroFns
		BooleFns
		DivFrame
		MarkSet
		AS_BASE
		ALSO_METRICS
		glyph-is-needed
		buildGlyphs
		fontMetrics

	### HERE WE GO
	run-glyph-module "./common/derivatives.mjs"
	run-glyph-module "./common/shapes.mjs"
	run-glyph-module "./space/index.mjs"
	run-glyph-module "./marks/index.mjs"

	# Unified letters
	run-glyph-module "./letter/shared.mjs"
	run-glyph-module "./letter/latin.mjs"
	run-glyph-module "./letter/greek.mjs"
	run-glyph-module "./letter/cyrillic.mjs"

	# Numbers
	run-glyph-module "./number/index.mjs"

	# Symbols
	run-glyph-module "./symbol/shared.mjs"
	run-glyph-module "./symbol/punctuation.mjs"
	run-glyph-module "./symbol/arrow.mjs"
	run-glyph-module "./symbol/geometric.mjs"
	run-glyph-module "./symbol/mosaic.mjs"
	run-glyph-module "./symbol/enclosure.mjs"
	run-glyph-module "./symbol/pictograph.mjs"
	run-glyph-module "./symbol/math.mjs"
	run-glyph-module "./symbol/letter.mjs"
	run-glyph-module "./symbol/braille.mjs"
	run-glyph-module "./symbol/ligation.mjs"

	# Auto-builds
	if [not recursive] : begin
		run-glyph-module "./auto-build/recursive-build.mjs"
		run-glyph-module "./auto-build/mark-doppelganger.mjs"
		run-glyph-module "./auto-build/accents.mjs"
		run-glyph-module "./auto-build/transformed.mjs"
		run-glyph-module "./auto-build/composite.mjs"

	$Exec$.executePendingBlocks

	Gr.linkSuffixPairGr glyphStore 'NWID' 'WWID' Gr.Nwid Gr.Wwid
	Gr.linkSuffixPairGr glyphStore 'lnum' 'onum' Gr.Lnum Gr.Onum
	Gr.linkSuffixGr     glyphStore 'aplForm'     Gr.AplForm

	return : object glyphStore fontMetrics
