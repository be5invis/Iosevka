$$include '../../meta/macros.ptl'

import [mix linreg clamp fallback] from '../../support/utils'

glyph-module

glyph-block Symbol-Mosaic-NotDef : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives

	create-glyph '.null' : glyph-proc
		set-width 0
		set currentGlyph.autoRefPriority (-9998)
		set currentGlyph.glyphRank   (9998)

	create-glyph 'space' ' ' : glyph-proc
		set currentGlyph.autoRefPriority (-100)
		local df : DivFrame para.diversityI
		set-width df.width

	create-glyph 'markDemoBaseSpace' 0xE00E : glyph-proc
		local df : DivFrame 1
		set-width df.width
		include : df.markSet.plus

	create-glyph 'markBaseSpace' 0xE00D : glyph-proc
		local df : DivFrame para.diversityI
		set-width df.width
		include : df.markSet.e

	create-glyph 'sp1' : glyph-proc
		local df : DivFrame 1
		set-width df.width

	create-glyph 'zwsp' 0x200B : glyph-proc
		set-width 0
		set currentGlyph.autoRefPriority (-9999)

	alias 'nonmarkingreturn' 0x000D 'zwsp'
	alias 'nbsp' 0xA0 'space'
	alias 'threePerEmsp' 0x2004 'space'
	alias 'fourPerEmsp' 0x2005 'space'
	alias 'sixPerEmsp' 0x2006 'space'
	alias 'enquad' 0x2000 'sp1'
	alias 'ensp' 0x2002 'sp1'
	alias 'brailleBlank' 0x2800 'sp1'
	alias 'figureSpace' 0x2007 'sp1'

	for-width-kinds WideWidth1
		create-glyph [MangleName 'emsp'] [MangleUnicode 0x2003] : glyph-proc
			set-width MosaicWidth
		alias [MangleName 'emquad'] [MangleUnicode 0x2001] [MangleName 'emsp']
