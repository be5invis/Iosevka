$$include '../../../meta/macros.ptl'

import [mix linreg clamp fallback] from '../../../support/utils'

glyph-module

glyph-block Symbol-Pictograph-Checking-Marks : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives

	for-width-kinds WideWidth1 : do
		define ptMaxWidth : MosaicWidth - SB
		define ptHeight0 : 1.5 * (Width - SB / 2) * [Math.pow (MosaicWidth / MosaicUnitWidth) (1 / 4)]
		define ptHeight  : ptHeight0 * ptHeight0 / [Math.hypot ptHeight0 : Math.min ptHeight0 ptMaxWidth]
		define ptWidth  : Math.min ptMaxWidth ptHeight
		define ptMidX : MosaicWidth / 2
		define ptSB : (MosaicWidth - ptWidth) / 2
		define ptRSB : MosaicWidth - ptSB
		define ptTopFar : SymbolMid + ptHeight * 2
		define ptTop : SymbolMid + ptHeight / 2
		define ptBot : SymbolMid - ptHeight / 2
		define ptBotFar : SymbolMid - ptHeight * 2

		define CornerShapeConfig : list
			list 0x231C ptTopFar SymbolMid (-MosaicWidth) (MosaicWidth / 2)
			list 0x231D ptTopFar SymbolMid (MosaicWidth / 2) (2 * MosaicWidth)
			list 0x231E SymbolMid ptBotFar (-MosaicWidth) (MosaicWidth / 2)
			list 0x231F SymbolMid ptBotFar (MosaicWidth / 2) (2 * MosaicWidth)

		foreach { unicode t b l r } [items-of CornerShapeConfig] : begin
			create-glyph [MangleName : NameUni unicode] [MangleUnicode unicode] : glyph-proc
				set-width MosaicWidth
				include : intersection
					Rect t b l r
					union
						VBarLeft ptSB ptBot ptTop GeometryStroke
						VBarRight ptRSB ptBot ptTop GeometryStroke
						HBarTop ptSB ptRSB ptTop GeometryStroke
						HBarBottom ptSB ptRSB ptBot GeometryStroke
