$$include '../../../meta/macros.ptl'

import [mix fallback SuffixCfg] from"../../../support/utils.mjs"
import [Dotless CvDecompose] from"../../../support/gr.mjs"

glyph-module

glyph-block Letter-Latin-Lower-Eszet : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives
	glyph-block-import Letter-Shared-Shapes : FlatHookDepth VerticalHook
	glyph-block-import Letter-Latin-S : AdviceSArchDepth

	define NO-TAIL    0
	define DESCENDING 1
	define TAILED     2

	define [Traditional fFlathook slab tail] : glyph-proc
		include : MarkSet.b
		local l : SB * 1
		local xHook : [mix SB RightSB 0.75] + HalfStroke * HVContrast
		local hd : FlatHookDepth [DivFrame 1]
		if fFlathook
		: then : include : dispiro
			flat xHook Ascender [widths.lhs]
			curl (l + hd.x) Ascender
			archv
			flat l (Ascender - hd.y)
			curl l 0 [heading Downward]
		: else : include : dispiro
			widths.lhs
			g4 xHook (Ascender - Hook)
			hookstart (Ascender - O)
			flat l XH
			curl l 0 [heading Downward]
		local t : mix 0 Ascender 0.7
		local tm : [mix Descender t 0.625] + HalfStroke
		local tl : [mix l RightSB 0.35] + HalfStroke * HVContrast
		include : HBar.t (l + 1) (RightSB - HalfStroke * 1.2 - OX) t
		include : dispiro
			widths.rhs
			flat tl tm [heading Rightward]
			curl (tl + 1) tm [heading Rightward]
			g2 (RightSB - OX * 1.5) [mix Descender tm 0.70]
			g2 (RightSB - OX * 1.5) [mix Descender tm 0.67]
			alsoThru 0.5 0.75
			g4 [mix SB RightSB 0.35] Descender
		include : dispiro
			widths.center (Stroke * 1.1)
			corner tl (tm - Stroke) [heading Upward]
			corner (RightSB - HalfStroke * 1.2 - OX) t [heading Upward]

		if slab : include : HSerif.lb SB 0 SideJut
		include : match tail
			[Just DESCENDING] : VBar.l l Descender 0
			[Just TAILED] : VerticalHook.l l 0 (-Hook * 1.2) Hook
			__ : glyph-proc


	define [Sulzbacher slab tail] : glyph-proc
		define ymiddle : [mix 0 Ascender 0.5] - HalfStroke
		define xFinal : Math.max
			mix (SB + Stroke * HVContrast) RightSB 0.1
			mix SB RightSB 0.3
		define xMiddle : Math.max xFinal (SB + Stroke * HVContrast * 1.2) (RightSB - ymiddle / 2 - Stroke * 0.75)
		define xMiddleBot : Math.max xMiddle (xFinal + 0.01 + TanSlope * Stroke)

		include : MarkSet.b
		include : dispiro
			widths.rhs
			flat SB 0 [heading Upward]
			curl SB XH
			arcvh
			g4 (Middle - CorrectionOMidS) (Ascender - O)
			archv
			g4 (RightSB + O * 2) [YSmoothMidR ymiddle Ascender]
			g4.left.end xMiddle ymiddle [heading Leftward]
		include : dispiro
			widths.rhs
			g4.right.start xMiddle (ymiddle + Stroke) [heading Rightward]
			archv
			g4   (RightSB - O) [YSmoothMidR (ymiddle + Stroke) 0]
			arcvh
			flat xMiddleBot 0 [heading Leftward]
			curl xFinal 0 [heading Leftward]

		if slab : include : HSerif.lb SB 0 SideJut
		include : match tail
			[Just DESCENDING] : VBar.l SB Descender 0
			[Just TAILED] : VerticalHook.l SB 0 (-Hook * 1.2) Hook
			__ : glyph-proc


	define [LongSSLig slab tail] : glyph-proc
		include : MarkSet.b

		define swOuter : AdviceStroke2 2 3 Ascender
		define strokeCoeff : StrokeWidthBlend 0 1 swInner
		define sEndX : Math.max
			mix (SB + Stroke * HVContrast) RightSB 0.1
			mix SB RightSB 0.3
		define swInner : AdviceStroke2 2.5 3 Ascender
		define ess: swInner * EssLower / Stroke
		define sTurnX : Math.max sEndX [mix SB RightSB 0.3]
		define sBotX : Math.max (sEndX + 1) [mix sEndX RightSB 0.30]
		define sTopX : sTurnX + (RightSB - sBotX)

		define sTopHookX : sTopX + 0.5 * swInner * HVContrast
		define archDepthATop : ArchDepthAOf (ArchDepth * (sTopHookX - SB - swInner * 0.5) / (RightSB - SB)) (Width - (RightSB - sTopHookX))
		define archDepthBTop : ArchDepthBOf (ArchDepth * (sTopHookX - SB - swInner * 0.5) / (RightSB - SB)) (Width - (RightSB - sTopHookX))
		define innerSmoothB : 1 * archDepthBTop # ArchDepthBOf (0.5 * swInner + (widthInner / Width) * [AdviceSArchDepth XH (-1) swInner]) widthInner


		include : dispiro
			widths.rhs swOuter
			flat SB 0 [heading Upward]
			curl SB (Ascender - archDepthATop)
			arcvh
			g4 ([mix SB sTopHookX 0.52] - CorrectionOMidX * swOuter) (Ascender - O) [widths.rhs : mix swOuter swInner 0.5]
			archv
			g4.down.mid sTopHookX (Ascender - archDepthBTop + TanSlope * swInner) [widths.rhs.heading swInner Downward]
			alsoThru.g2 0.5 0.50 [widths.center swInner]
			g4 sTurnX [mix innerSmoothB (Ascender - archDepthBTop) 0.5] [widths.lhs.heading swInner Downward]
			alsoThru.g2 0.5 0.50 [widths.center swInner]
			g4 (RightSB - OX) (innerSmoothB - 2 * TanSlope * swInner) [widths.rhs.heading swInner Downward]
			arcvh
			flat ([Math.max (sEndX + 0.1 + TanSlope * swInner) : Math.min (RightSB - innerSmoothB) [mix sEndX RightSB 0.375]] + CorrectionOMidX * swInner / 2 + TanSlope * swInner ) 0
			curl sEndX 0 [heading Leftward]

		if slab : include : HSerif.lb SB 0 SideJut
		include : match tail
			[Just DESCENDING] : VBar.l SB Descender 0
			[Just TAILED] : VerticalHook.l SB 0 (-Hook * 1.2) Hook (sw -- swOuter)
			__ : glyph-proc


	create-glyph 'eszet.traditional'                   : Traditional  false SLAB  NO-TAIL
	create-glyph 'eszet.traditionalDescending'         : Traditional  false false DESCENDING
	create-glyph 'eszet.traditionalTailed'             : Traditional  false false TAILED
	create-glyph 'eszet.traditionalFlatHook'           : Traditional  true  SLAB  NO-TAIL
	create-glyph 'eszet.traditionalFlatHookDescending' : Traditional  true  false DESCENDING
	create-glyph 'eszet.traditionalFlatHookTailed'     : Traditional  true  false TAILED
	create-glyph 'eszet.sulzbacher'                    : Sulzbacher         SLAB  NO-TAIL
	create-glyph 'eszet.sulzbacherDescending'          : Sulzbacher         false DESCENDING
	create-glyph 'eszet.sulzbacherTailed'              : Sulzbacher         false TAILED
	create-glyph 'eszet.longSSLig'                     : LongSSLig          SLAB  NO-TAIL
	create-glyph 'eszet.longSSLigDescending'           : LongSSLig          false DESCENDING
	create-glyph 'eszet.longSSLigTailed'               : LongSSLig          false TAILED

	select-variant 'eszet' 0xDF

glyph-block Letter-Latin-Upper-Eszet : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives


	create-glyph 'Eszet' 0x1E9E : glyph-proc
		include : MarkSet.capital

		define ymiddle : [mix 0 CAP 0.5] - HalfStroke
		define ymiddleCap : [mix 0 CAP 0.54] - HalfStroke
		define xFinal : Math.max
			mix (SB + Stroke * HVContrast) RightSB 0.1
			mix SB RightSB 0.3
		define xMiddle : Math.max xFinal (SB + Stroke * HVContrast * 1.2) (RightSB - ymiddle / 2 - Stroke * 0.75)
		define xMiddleBot : Math.max xMiddle (xFinal + 0.01 + TanSlope * Stroke)
		define rightTopX : RightSB + O

		include : dispiro
			widths.lhs
			flat rightTopX CAP [heading Leftward]
			curl Middle CAP
			archv
			flat SB XH
			curl SB 0 [heading Downward]
		include : dispiro
			widths.rhs
			g4   xMiddle (ymiddleCap + Stroke) [heading Rightward]
			archv
			g4   (RightSB - O * 2) [YSmoothMidR (ymiddleCap + Stroke) 0]
			arcvh
			flat xMiddleBot 0
			curl xFinal 0 [heading Leftward]
		include : dispiro
			widths.rhs
			g4 rightTopX (CAP - Stroke)
			g4 xMiddle (ymiddleCap + Stroke) [widths Stroke 0]

		if SLAB : include : HSerif.lb SB 0 SideJut
