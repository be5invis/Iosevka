$$include '../../../meta/macros.ptl'

import [mix barmixL linreg clamp fallback] from '../../../support/utils'
import [DesignParameters] from '../../../meta/aesthetics'

glyph-module

glyph-block Letter-Greek-Psi : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives

	define [PsiBaseShape df y1 y2 y3 y4 doBotSerif doTopSerif doSideSerifL doSideSerifR] : glyph-proc
		include : dispiro
			widths.lhs df.mvs
			flat df.leftSB y3 [heading Downward]
			curl df.leftSB (y2 + (SmoothB * df.div))
			arcvh
			g4   df.middle y2 [heading Rightward]
			archv
			flat df.rightSB (y2 + (SmoothA * df.div))
			curl df.rightSB y3 [heading Upward]
		include : VBar df.middle y2 y4 df.mvs
		include : VBar df.middle y1 (y2 + HalfStroke)

		if doSideSerifL : include : tagged 'serifLT' : LeftwardTopSerif df.leftSB y3 SideJut
		if doSideSerifR : include : tagged 'serifRT' : RightwardTopSerif df.rightSB y3 SideJut
		if doTopSerif  : include : tagged 'serifMT' : CenterTopSerif df.middle CAP MidJutSide
		if doBotSerif  : include : tagged 'serifMB' : CenterBottomSerif df.middle 0 MidJutSide

	create-glyph 'grek/Psi' 0x3A8 : glyph-proc
		local df : DivFrame para.diversityM 3
		set-width df.width
		include : df.markSet.capital
		include : PsiBaseShape df 0 (CAP * 0.3) CAP CAP SLAB (SLAB && (df.width > para.refJut * 7)) SLAB SLAB

	alias 'cyrl/Psi' 0x470 'grek/Psi'

	create-glyph 'smcpPsi' 0x1D2A : glyph-proc
		local df : DivFrame para.diversityM 3
		set-width df.width
		include : df.markSet.e
		include : PsiBaseShape df 0 (XH * 0.3) XH XH SLAB false SLAB SLAB


	create-glyph 'grek/psi' 0x3C8 : glyph-proc
		local df : DivFrame para.diversityM 3
		set-width df.width
		include : df.markSet.if
		include : PsiBaseShape df Descender 0 XH CAP false false SLAB false

	create-glyph 'cyrl/psi' 0x471 : glyph-proc
		local df : DivFrame para.diversityM 3
		set-width df.width
		include : df.markSet.p
		include : PsiBaseShape df Descender 0 XH XH false false SLAB false

