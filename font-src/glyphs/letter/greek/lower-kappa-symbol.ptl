$$include '../../../meta/macros.ptl'

import [mix barmixL linreg clamp fallback] from '../../../support/utils'

glyph-module

glyph-block Letter-Greek-Lower-Rho : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives

	define [DfKappasymbol] : DivFrame 1 3

	create-glyph "grek/kappaSymbol" 0x3F0 : glyph-proc
		local df : DfKappasymbol
		include : MarkSet.e
		define xLeft : df.leftSB + 0.25 * df.mvs * HVContrast
		define xRight : df.rightSB - 0.25 * df.mvs * HVContrast
		define yBottom : 0 + 0.2 * Stroke
		define yTop : XH - 0.2 * Stroke

		define deltaX : 0.001 * (xRight - xLeft)
		define deltaY : 0.001 * (yTop - yBottom)

		define swTerm : AdviceStroke 3
		define swTermThin : swTerm * CThinB

		define kArcX 0.08
		define kArcY 0.3
		define kSwMid 0.1

		include : dispiro [widths.center swTerm] [flat xLeft yBottom] [curl xRight yTop]

		include : dispiro [widths.center]
			straight.right.start df.leftSB (XH - HalfStroke) [heading Rightward]
			archv
			g2 (df.leftSB + 0.625 * HookX) (XH - SHook - HalfStroke) [widths.center df.mvs]
			alsoThru.g2 kArcX kArcY : widths
				mix (df.mvs / 2) (swTermThin / 2) kSwMid
				mix (df.mvs / 2) (swTerm / 2) kSwMid
			g2 (xLeft + deltaX) (yBottom + deltaY) [widths (swTermThin / 2) (swTerm / 2)]
			g2 xLeft yBottom
		include : dispiro [widths.center]
			straight.left.start df.rightSB (0 + HalfStroke) [heading Leftward]
			archv
			g2 (df.rightSB - 0.625 * HookX) (0 + SHook + HalfStroke) [widths.center df.mvs]
			alsoThru.g2 kArcX kArcY : widths
				mix (df.mvs / 2) (swTermThin / 2) kSwMid
				mix (df.mvs / 2) (swTerm / 2) kSwMid
			g2 (xRight - deltaX) (yTop - deltaY) [widths (swTermThin / 2) (swTerm / 2)]
			g2 xRight yTop

	derive-composites 'grek/kaiSymbol' 0x3D7 'grek/kappaSymbol'
		let [df : DfKappasymbol]
			VerticalHook (df.rightSB - 0.5 * df.mvs * HVContrast) 0 (-HookX) (Hook) (sw -- df.mvs) (yExtension -- Stroke)
