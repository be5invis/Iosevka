$$include '../../../meta/macros.ptl'

import [mix barmixL linreg clamp fallback] from '../../../support/utils'
import [DesignParameters] from '../../../meta/aesthetics'

glyph-module

glyph-block Letter-Greek-Lower-Gamma : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives
	glyph-block-import Letter-Blackboard : BBS BBD BBBarLeft


	create-glyph 'grek/gamma' 0x3B3 : glyph-proc
		include : MarkSet.p

		local xMid : [mix SB RightSB 0.46] + HalfStroke * HVContrast
		local xStart : SB + 0.4 * Stroke * HVContrast
		include : dispiro
			g4 xStart (XH - O) [widths.rhs]
			bezcontrols 0.33 0.17 1 0.62 6 important
			flat xMid 0
			curl xMid Descender [heading Downward]

		include : dispiro
			widths.rhs
			flat RightSB XH [heading Downward]
			curl RightSB (XH * 0.9) [heading Downward]
			quadcontrols 0 0.3 6
			g4   xMid 0 [widths.rhs : Stroke * 0.9]

	create-glyph 'mathbb/grek/gamma' 0x213D : glyph-proc
		include : MarkSet.p

		local cor : DiagCorDs (XH - Descender) (RightSB - SB) BBD
		local swEqv : 2 * BBS + BBD
		local xMid : [mix SB RightSB 0.46] + 0.46 * swEqv * HVContrast
		local xStart : SB + 0.4 * swEqv * HVContrast

		include : HBarTop    (xStart - BBD) (xStart + (cor - 1) * BBD) XH        BBS
		include : HBarBottom (xMid - BBD)   (xMid)                     Descender BBS

		foreach {ob ot} [items-of {{ 0 (cor - 1) } { (-1) (-1) }}] : include : dispiro
			g4 (xStart + ot * BBD) XH [widths.rhs.heading BBS Downward]
			bezcontrols 0.33 0.17 1 0.62 6 important
			flat (xMid + ob * BBD) 0
			curl (xMid + ob * BBD) Descender [heading Downward]

		include : dispiro
			widths.rhs BBS
			flat RightSB XH [heading Downward]
			curl RightSB (XH * 0.9) [heading Downward]
			quadcontrols 0 0.3 6
			g4   xMid 0 [widths.rhs : BBS * 0.9]