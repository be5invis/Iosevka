$$include '../../../meta/macros.ptl'

import [mix barmixL linreg clamp fallback] from '../../../support/utils'
import [DesignParameters] from '../../../meta/aesthetics'

glyph-module

glyph-block Letter-Greek-Upper-Gamma: begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives
	glyph-block-import Letter-Shared-Shapes : nShoulder
	glyph-block-import Letter-Blackboard : BBS BBD BBBarLeft

	define SLAB-AUTO    0
	define SLAB-MOTION  1

	define GammaBarLeft (SB * 1.5)
	define [GammaShape top slabType] : glyph-proc
		include : VBarLeft GammaBarLeft 0 top
		include : HBarTop (GammaBarLeft - O) (RightSB - OX) top
		match slabType
			[Just SLAB-AUTO] : if SLAB : begin
				include : LeftwardTopSerif GammaBarLeft top SideJut
				include : LeftwardBottomSerif (GammaBarLeft + HVContrast * HalfStroke) 0 Jut
				include : RightwardBottomSerif (GammaBarLeft + HVContrast * HalfStroke) 0 MidJutSide
				include : tagged 'serifRT' : DownwardRightSerif (RightSB - OX) top VJut
			[Just SLAB-MOTION] : begin
				include : tagged 'serifRT' : DownwardRightSerif (RightSB - OX) top VJut

	define GammaConfig : object
		standard      { SLAB-AUTO   }
		motionSerifed { SLAB-MOTION }

	foreach { suffix { slabType } } [Object.entries GammaConfig] : do
		create-glyph "grek/Gamma.\(suffix)" : glyph-proc
			include : MarkSet.capital
			include : GammaShape CAP slabType

		create-glyph "cyrl/ghe.upright.\(suffix)" : glyph-proc
			include : MarkSet.e
			include : GammaShape XH slabType

	select-variant 'grek/Gamma' 0x393
	select-variant 'cyrl/Ghe' 0x413 (shapeFrom -- 'grek/Gamma')
	select-variant 'cyrl/ghe.upright'
	alias 'grek/smcpGamma' 0x1D26 'cyrl/ghe.upright'

	with-related-glyphs 'cyrl/GheHook' 0x494 'cyrl/Ghe' : lambda [src sel] : glyph-proc
		include [refer-glyph src] AS_BASE ALSO_METRICS
		include : nShoulder
			left   -- (GammaBarLeft + Stroke)
			right  -- RightSB
			top    -- CAP * HBarPos + Stroke / 4
			bottom -- 0
			sma    -- SmoothA
			smb    -- SmoothB
		include : VerticalHook (RightSB - HalfStroke * HVContrast) 0 (-HookX) Hook

	with-related-glyphs 'cyrl/gheHook' 0x495 'cyrl/ghe.upright' : lambda [src sel] : glyph-proc
		include [refer-glyph src] AS_BASE ALSO_METRICS
		include : nShoulder
			left   -- (GammaBarLeft + Stroke)
			right  -- RightSB
			top    -- XH * HBarPos + Stroke / 4
			bottom -- 0
			sma    -- SmoothA * [Math.pow HBarPos 0.3]
			smb    -- SmoothB * [Math.pow HBarPos 0.3]
		include : VerticalHook (RightSB - HalfStroke * HVContrast) 0 (-HookX) Hook

	with-related-glyphs 'cyrl/Ghayn' 0x492 'cyrl/Ghe' : lambda [src sel] : glyph-proc
		include [refer-glyph src] AS_BASE ALSO_METRICS
		include : HOverlayBar (SB * 0.3) [mix (SB + Stroke) (RightSB - Stroke) 0.55] (CAP * (1 - OverlayPos))

	with-related-glyphs 'cyrl/ghayn.upright' null 'cyrl/ghe.upright' : lambda [src sel] : glyph-proc
		include [refer-glyph src] AS_BASE ALSO_METRICS
		include : HOverlayBar (SB * 0.3) [mix (SB + Stroke) (RightSB - Stroke) 0.55] (XH * (1 - OverlayPos))


	with-related-glyphs 'cyrl/GheDescender' 0x4F6 'cyrl/Ghe' : lambda [src sel] : composite-proc
		refer-glyph src
		CyrDescender (GammaBarLeft + Stroke * HVContrast) (sideJut -- MidJutCenter - HalfStroke * HVContrast)
		MarkSet.if

	with-related-glyphs 'cyrl/gheDescender.upright' null 'cyrl/ghe.upright' : lambda [src sel] : composite-proc
		refer-glyph src
		CyrDescender (GammaBarLeft + Stroke * HVContrast) (sideJut -- MidJutCenter - HalfStroke * HVContrast)
		MarkSet.p

	define [CyrGeShape top] : glyph-proc
		include : GammaShape top
		eject-contour 'serifRT'
		include : VBarRight (RightSB - OX) top (top + Accent)

	create-glyph 'cyrl/Ge' 0x490 : glyph-proc
		include : MarkSet.capital
		include : CyrGeShape CAP

	create-glyph 'cyrl/ge.upright' : glyph-proc
		include : MarkSet.e
		include : CyrGeShape XH


	create-glyph 'mathbb/grek/Gamma' 0x213E : glyph-proc
		include : MarkSet.capital
		include : BBBarLeft GammaBarLeft 0 CAP
		include : HBarTop GammaBarLeft RightSB CAP BBS
