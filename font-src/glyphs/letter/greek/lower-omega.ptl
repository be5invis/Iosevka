$$include '../../../meta/macros.ptl'

import [mix barmixL linreg clamp fallback] from '../../../support/utils'
import [DesignParameters] from '../../../meta/aesthetics'

glyph-module

glyph-block Letter-Greek-Lower-Omega : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives

	create-glyph 'grek/omega' 0x3C9 : glyph-proc
		local df : DivFrame para.diversityM 3
		set-width df.width
		include : df.markSet.e

		local fine : AdviceStroke 3.25 df.div
		local mfine : fine * CThin
		local x0 : mix df.leftSB df.rightSB 0.1
		local y0 : XH - O
		local y1 : mix 0 XH 0.8
		local x1 : df.leftSB + OX
		local y3 : XH / 2
		local y4 : XH * 0.65

		include : dispiro
			widths.lhs fine
			g4 x0 y0
			g4 x1 (XH / 2)
			arcvh 8
			g4 [mix x1 (df.middle + fine / 2 * HVContrast) 0.5] O [heading Rightward]
			archv 8
			flat (df.middle + (mfine - fine / 2) * HVContrast) y3 [widths.heading mfine 0 Upward]
			curl (df.middle + (mfine - fine / 2) * HVContrast) y4 [heading Upward]
		include : dispiro
			widths.rhs fine
			g4 (df.width - x0) y0
			g4 (df.width - x1) (XH / 2)
			arcvh 8
			g4 [mix (df.width - x1) (df.middle - fine / 2 * HVContrast) 0.5] O [heading Leftward]
			archv 8
			flat (df.middle - (mfine - fine / 2) * HVContrast) y3 [widths.heading 0 mfine Upward]
			curl (df.middle - (mfine - fine / 2) * HVContrast) y4 [heading Upward]

	create-glyph 'grek/pomega' 0x3D6 : glyph-proc
		local df : DivFrame para.diversityM 3
		set-width df.width
		include : df.markSet.e

		include : union
			HBar df.leftSB df.rightSB (XH - HalfStroke)
			difference
				refer-glyph 'grek/omega'
				MaskAbove XH

	create-glyph 'closeomega' 0x277 : glyph-proc
		local df : DivFrame para.diversityM 3
		set-width df.width
		include : [DivFrame df.div].markSet.e
		local fine : AdviceStroke 3.25
		local mfine : fine * CThin
		local x0 : df.middle - CorrectionOMidS
		local y0 : XH - O
		local y1 : mix 0 XH 0.45
		local x1 : df.leftSB + OX
		local y3 : XH / 2
		local y4 : XH * 0.65
		include : dispiro
			widths.rhs mfine
			flat (df.middle + (mfine - fine / 2) * HVContrast) y4 [heading Downward]
			curl (df.middle + (mfine - fine / 2) * HVContrast) y3 [heading Downward]
			arcvh 8
			g4 [mix x1 (df.middle + fine / 2 * HVContrast) 0.5] O [widths.heading 0 fine Leftward]
			archv 8
			g4 x1 y1
			arcvh
			g4 x0 y0
			archv
			g4 (df.width - x1) y1
			arcvh 8
			g4 [mix (df.width - x1) (df.middle - fine / 2 * HVContrast) 0.5] O [heading Leftward]
			archv 8
			flat (df.middle - (mfine - fine / 2) * HVContrast) y3 [widths.heading 0 mfine Upward]
			curl (df.middle - (mfine - fine / 2) * HVContrast) y4 [heading Upward]
