$$include '../../../meta/macros.ptl'

import [mix barmixL linreg clamp fallback] from '../../../support/utils'
import [DesignParameters] from '../../../meta/aesthetics'

glyph-module

glyph-block Letter-Greek-Lower-Upsilon : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives

	define [LowerUpsilonShape] : params [k top sma smb [sw Stroke]] : glyph-proc
		define xArcRight : RightSB - OX
		define yArcRight : [mix 0 top 0.6] - sw * (0.2 + TanSlope)
		define xArcEnd : [mix SB RightSB 0.82] + sw * 0.375
		define yArcEnd : top - O + Stroke * (0.1 - TanSlope)
		include : dispiro
			widths.lhs sw
			flat SB top [heading Downward]
			curl SB smb
			arcvh
			g4 ([mix SB RightSB k] + CorrectionOMidX * sw) O
			archv
			g4 xArcRight yArcRight
			g4 xArcEnd yArcEnd

	create-glyph 'grek/upsilon' 0x3C5 : glyph-proc
		include : MarkSet.e
		include : LowerUpsilonShape 0.5 XH SmallSmoothA SmallSmoothB
		if SLAB : include : LeftwardTopSerif SB XH SideJut

