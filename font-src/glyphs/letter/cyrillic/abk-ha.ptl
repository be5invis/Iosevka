$$include '../../../meta/macros.ptl'

import [mix linreg clamp fallback] from"../../../support/utils.mjs"

glyph-module

glyph-block Letter-Cyrillic-Abkhasian-Ha : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives

	define [CyrlAbkHaShape df top sm] : begin
		local ada : ArchDepthAOf sm df.width
		local adb : ArchDepthBOf sm df.width
		local ada2 : ArchDepthAOf (0.625 * sm) df.width
		local adb2 : ArchDepthBOf (0.625 * sm) df.width
		local xm : df.middle - [HSwToV : 0.5 * df.mvs]
		local y2 : top * 0.75
		return : dispiro
			g4.left.start (df.middle - CorrectionOMidX * df.mvs) (top - O) [widths.lhs df.mvs]
			archv
			flat (df.leftSB + OX) (top - ada)
			curl (df.leftSB + OX) adb
			arcvh
			g4 (df.middle + CorrectionOMidX * df.mvs) O
			archv
			straight.up.mid (df.rightSB - OX) [YSmoothMidR (y2 + df.mvs / 2) 0 ada2 adb2]
			arcvh
			g4 ([mix xm df.rightSB 0.5]  - CorrectionOMidX * df.mvs) y2
			archv
			straight.down.mid xm [YSmoothMidL (y2 + df.mvs / 2) 0 ada2 adb2]
			arcvh
			g4 (df.rightSB - O) O


	create-glyph 'cyrl/abk/Ha' 0x4A8 : glyph-proc
		define df : include : DivFrame para.diversityM 3
		include : df.markSet.capital
		include : CyrlAbkHaShape df CAP ArchDepth

	create-glyph 'cyrl/abk/ha' 0x4A9 : glyph-proc
		define df : include : DivFrame para.diversityM 3
		include : df.markSet.e
		include : CyrlAbkHaShape df XH SmallArchDepth
