$$include '../../../meta/macros.ptl'

import [mix linreg clamp fallback] from"../../../support/utils.mjs"

glyph-module

glyph-block Letter-Cyrillic-Te-MidHook : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives
	glyph-block-import Letter-Shared-Shapes : MidHook

	define [Shape top pArc slabTop slabBot] : glyph-proc
		local sw : AdviceStroke 2.75
		local left : [mix SB RightSB 0.20] + OX
		local right : RightSB - OX

		local xTopBarLeft : SB - SideJut
		local xTopBarRightSym : 2 * left + [HSwToV sw] - xTopBarLeft
		local xTopBarRight : Math.max xTopBarRightSym : mix left RightSB 0.475

		include : VBar.l left 0 top sw
		include : dispiro
			widths.rhs sw
			flat xTopBarLeft  top [if slabTop [heading Rightward] null]
			curl xTopBarRight top [if slabTop [heading Rightward] null]
		include : MidHook.general
			left   -- (left + [HSwToV sw])
			right  -- RightSB
			top    -- top * HBarPos + Stroke / 4
			ada    -- ArchDepthA * pArc
			adb    -- ArchDepthB * pArc

		if slabTop : begin
			local swVJut : Math.min [AdviceStroke 4.5] (0.625 * (left - xTopBarLeft))
			include : VSerif.dl  xTopBarLeft top VJut swVJut
			include : VSerif.dr xTopBarRight top VJut swVJut

		if slabBot : begin
			include : HSerif.mb (left + [HSwToV : 0.5 * sw]) 0 Jut

	define Config : object
		serifless     { false false }
		motionSerifed { true  false }
		serifed       { true  true  }

	foreach { suffix { doST doSB } } [Object.entries Config] : do

		create-glyph "cyrl/TeMidHook.\(suffix)" : glyph-proc
			include : MarkSet.capDesc
			include : Shape CAP 1 doST doSB

		create-glyph "cyrl/teMidHook.\(suffix)" : glyph-proc
			include : MarkSet.p
			include : Shape XH [Math.pow HBarPos 0.3] doST doSB

	select-variant 'cyrl/TeMidHook' 0xA68A (follow -- 'T')
	select-variant 'cyrl/teMidHook' 0xA68B (follow -- 'T')
