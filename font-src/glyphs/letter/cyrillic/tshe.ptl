$$include '../../../meta/macros.ptl'

import [mix barmixL linreg clamp fallback] from '../../../support/utils'
import [DesignParameters] from '../../../meta/aesthetics'
import '../../../support/point' as Point

glyph-module

glyph-block Letter-Cyrillic-Tshe : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives
	glyph-block-import Letter-Shared-Shapes : nShoulder

	create-glyph 'cyrl/Tshe' 0x40B : glyph-proc
		include : MarkSet.capital

		local sw : AdviceStroke 2.75
		local left : [mix SB RightSB 0.20] + OX
		local right : RightSB - OX

		local xTopBarLeft : SB - SideJut
		local xTopBarRightSym : 2 * left + HVContrast * sw - xTopBarLeft
		local xTopBarRight : Math.max xTopBarRightSym : mix left RightSB 0.475

		include : nShoulder
			left -- (left + sw * HVContrast)
			right -- right
			top -- [Math.min XH : if SLAB (CAP - 1.25 * VJut) XH]
			stroke -- sw
		include : VBarLeft left 0 CAP sw
		include : dispiro
			widths.rhs sw
			flat xTopBarLeft  CAP [if SLAB [heading Rightward] null]
			curl xTopBarRight CAP [if SLAB [heading Rightward] null]

		if SLAB : begin
			local swVJut : Math.min [AdviceStroke 4.5] (0.625 * (left - xTopBarLeft))
			include : CenterBottomSerif (left + 0.5 * HVContrast * sw) 0 Jut
			include : CenterBottomSerif (right - 0.5 * HVContrast * sw) 0 Jut
			include : DownwardLeftSerif  xTopBarLeft  CAP VJut swVJut
			include : DownwardRightSerif xTopBarRight CAP VJut swVJut
