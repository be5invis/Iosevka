$$include '../../../meta/macros.ptl'

import [mix barmixL linreg clamp fallback] from '../../../support/utils'
import [DesignParameters] from '../../../meta/aesthetics'
import '../../../support/point' as Point

glyph-module

glyph-block Letter-Cyrillic-Dje : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives
	glyph-block-import Letter-Shared-Shapes : nShoulder
	glyph-block-import Letter-Latin-Lower-H : HBarOverlay

	create-glyph 'cyrl/Dje' 0x402 : glyph-proc
		include : MarkSet.capital

		local sw : AdviceStroke 2.75
		local left : [mix SB RightSB 0.20] + OX
		local right : RightSB - OX

		local xTopBarLeft : SB - SideJut
		local xTopBarRightSym : 2 * left + HVContrast * sw - xTopBarLeft
		local xTopBarRight : Math.max xTopBarRightSym : mix left RightSB 0.475

		include : nShoulder
			left -- (left + sw * HVContrast)
			right -- right
			top -- [Math.min XH : if SLAB (CAP - 1.25 * VJut) XH]
			stroke -- sw
			bottom -- (Hook + sw / 2 + O)
			sma -- SmoothA
			smb -- SmoothB
		include : VBarLeft left 0 CAP sw
		include : dispiro
			widths.rhs sw
			flat xTopBarLeft  CAP [if SLAB [heading Rightward] null]
			curl xTopBarRight CAP [if SLAB [heading Rightward] null]

		include : VerticalHook (right - sw / 2 * HVContrast) (Hook + sw / 2 + O) [Math.max ((left - right) / 2 + sw / 2) (-Hook * 1.2)] Hook sw

		if SLAB : begin
			local swVJut : Math.min [AdviceStroke 4.5] (0.625 * (left - xTopBarLeft))
			include : LeftwardBottomSerif left 0 SideJut
			include : DownwardLeftSerif  xTopBarLeft  CAP VJut swVJut
			include : DownwardRightSerif xTopBarRight CAP VJut swVJut

	with-related-glyphs 'cyrl/dje' 0x452 'heng' : lambda [src sel] : glyph-proc
		include [refer-glyph src] AS_BASE
		include : HBarOverlay
