$$include '../../../meta/macros.ptl'

import [mix linreg clamp fallback] from '../../../support/utils'
import [DesignParameters] from '../../../meta/aesthetics'
import [Dotless CvDecompose] from "../../../support/gr"

glyph-module

glyph-block Letter-Latin-Upper-Q : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives
	glyph-block-import Letter-Blackboard : BBS BBD
	glyph-block-import Letter-Latin-O

	define TailDepth : 0 - Hook

	define [QStdBody top sw] : OShape top 0 SB RightSB sw SmoothA SmoothB
	define [QHorizontalTailedBody top sw] : begin
		define fine : mix ShoulderFine sw 0.125
		return : dispiro
			flat (Middle - sw * TanSlope + O) (sw - fine) [widths.lhs fine]
			curl (Middle + CorrectionOMidX * fine) (sw - fine)
			archv
			flat RightSB SmoothA [widths.lhs sw]
			curl RightSB (top - SmoothB)
			arcvh
			g4 (Middle - CorrectionOMidS) (top - O)
			archv
			flat SB (top - SmoothA)
			curl SB SmoothB
			arcvh
			straight.right.end (Middle + CorrectionOMidS) 0

	define [QStaraightTail] : begin
		local shift : StrokeWidthBlend 0 0.25
		return : dispiro
			widths.rhs
			flat (Middle - Stroke * HVContrast * shift - TailDepth * 0.25) TailDepth [heading Upward]
			curl (Middle - Stroke * HVContrast * shift) 0

	define [QCurlyTail] : begin
		local shift : StrokeWidthBlend 0.5 0.6
		local startx : Middle + (Stroke * shift) * HVContrast
		return : dispiro
			widths.rhs
			flat startx HalfStroke [heading Downward]
			curl startx 0 [heading Downward]
			arcvh
			flat [Math.min (Middle + HookX - 1) (startx - TailDepth - Stroke * HVContrast)] (Stroke + TailDepth)
			curl (Middle + HookX) (Stroke + TailDepth)

	define [QCrossingCurlyTail] : union
		VBar Middle HalfStroke [mix Descender HalfStroke 1.75] [AdviceStroke 3.5]
		dispiro
			widths.rhs
			flat (Middle + HalfStroke * HVContrast) HalfStroke [heading Downward]
			curl (Middle + HalfStroke * HVContrast) 0 [heading Downward]
			arcvh
			flat [Math.min (Middle + HookX - 1) (Middle - HalfStroke * HVContrast - TailDepth)] (Stroke + TailDepth)
			curl (Middle + HookX) (Stroke + TailDepth)

	define [QCrossing] : begin
		define k 1.5
		define cor : DiagCor (k * (RightSB - Middle)) (RightSB - Middle - OX)
		return : dispiro
			widths.center (cor * [AdviceStroke 4.25])
			flat RightSB 0 [heading Upward]
			curl (Middle + OX) (k * (RightSB - Middle)) [heading Upward]

	define [QVerticalCrossing] : begin
		return : VBar Middle [mix Descender HalfStroke 1.75] TailDepth [AdviceStroke 3.5]

	define [QHorizontalTail] : dispiro
		flat (Middle + CorrectionOMidS) 0 [widths.lhs : AdviceStroke 3]
		curl [mix RightSB Width 0.5] 0

	define detachedTailGap : Math.max (-0.25 * Descender) [AdviceStroke 12]
	define yObliqueTailStart : 0 - detachedTailGap - Stroke * 0.875
	define yObliqueTailEnd : [mix 0 Descender 0.75] - Stroke * 0.5
	define xDetachedTailEnd : mix RightSB Width 0.75
	define kBendStartPartLeftLength 0.5
	define xBendTailStart : mix Middle SB kBendStartPartLeftLength
	define yBendTailStart : mix yObliqueTailStart yObliqueTailEnd kBendStartPartLeftLength

	define [QDetachedTail] : dispiro
		widths.lhs Stroke
		corner Middle           yObliqueTailStart [heading Rightward]
		corner xDetachedTailEnd yObliqueTailEnd   [heading Rightward]

	define [QDetachedBendTail] : union [QDetachedTail] : dispiro
		widths.lhs Stroke
		corner xBendTailStart yBendTailStart    [heading Rightward]
		corner Middle         yObliqueTailStart [heading Rightward]

	define QConfig : object
		straight            { QStdBody               Stroke          QStaraightTail     }
		curlyTailed         { QStdBody               Stroke          QCurlyTail         }
		crossingCurlyTailed { QStdBody              [AdviceStroke 3] QCrossingCurlyTail }
		crossing            { QStdBody              [AdviceStroke 3] QCrossing          }
		verticalCrossing    { QStdBody              [AdviceStroke 3] QVerticalCrossing  }
		horizontalTailed    { QHorizontalTailedBody  Stroke          QHorizontalTail    }
		detachedTailed      { QStdBody               Stroke          QDetachedTail      }
		detachedBendTailed  { QStdBody               Stroke          QDetachedBendTail  }


	foreach { suffix { body swRing tailShape } } [Object.entries QConfig] : do
		create-glyph "Q.\(suffix)" : glyph-proc
			include : MarkSet.if
			include : body CAP swRing
			include : tailShape CAP swRing

		create-glyph "smcpQ.\(suffix)" : glyph-proc
			include : MarkSet.p
			include : body XH swRing
			include : tailShape XH swRing

	select-variant 'Q' 'Q'
	select-variant 'smcpQ' 0xA7AF (follow -- 'Q')
	alias 'cyrl/Qa' 0x51A 'Q'

	create-glyph 'mathbb/Q' 0x211A : glyph-proc
		define [QInner] : OShapeOutline.NoOvershoot CAP 0 SB RightSB BBS SmoothA SmoothB

		include : MarkSet.capital
		include : OShape CAP 0 SB RightSB BBS SmoothA SmoothB
		include : intersection
			QInner
			union
				VBarLeft  (SB + BBD) 0 CAP BBS
				VBarRight (RightSB - BBD) 0 CAP BBS

		define terminalX : Middle + HookX
		define tailDeltaX : BBD / 2
		define qTerminalY : BBS - Hook - BBD / 4
		define [QTail x] : dispiro
			widths.center BBS
			flat (x) (CAP / 2)
			curl (x) 0
			arcvh
			flat [Math.min (terminalX - 1) (x - qTerminalY)] qTerminalY
			curl terminalX qTerminalY
		include : difference
			union
				QTail (Middle - tailDeltaX)
				QTail (Middle + tailDeltaX)
			QInner