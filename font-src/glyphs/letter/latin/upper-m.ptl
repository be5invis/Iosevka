$$include '../../../meta/macros.ptl'

import [mix linreg clamp fallback] from '../../../support/utils'
import [DesignParameters] from '../../../meta/aesthetics'
import [Dotless CvDecompose] from "../../../support/gr"

glyph-module

glyph-block Letter-Latin-Upper-M : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives

	define FORM-FLAT     0
	define FORM-HANGING  1
	define FORM-TOBEFLIP 2

	define SLAB-NONE            0
	define SLAB-AUTO            1
	define SLAB-MOTION          2
	define SLAB-SMALL-CYRILLIC  3

	glyph-block-export MShape
	define [MShape] : params [top df form slabType slanted] : glyph-proc
		local sidesSlope : if slanted 0.04 0
		local xLeftTop  : df.leftSB  + top * sidesSlope
		local xRightTop : df.rightSB - top * sidesSlope

		local swSideBot : match form
			[Just FORM-HANGING] : begin Stroke
			__                  : AdviceStroke 3.5 df.div
		define cwSideTop : match form
			[Just FORM-HANGING] : begin 5
			__                  : begin 4
		local swSideTop : AdviceStroke cwSideTop df.div
		local halfSwMiddle : swSideTop / 2
		local middleY : match form
			[Just FORM-TOBEFLIP] : top * 0.4 / df.div
			[Just FORM-HANGING]  : top * 0.3 / df.div
			__                   : if SLAB Stroke 0

		include : dispiro
			flat df.leftSB 0 [widths.heading 0 swSideBot Upward]
			curl [mix df.leftSB xLeftTop 0.2] (top * 0.2) [heading Upward]
			straight.up.end xLeftTop top [widths.heading 0 swSideTop Upward]
		include : dispiro
			flat df.rightSB 0 [widths.heading swSideBot 0 Upward]
			curl [mix df.rightSB xRightTop 0.2] (top * 0.2) [heading Upward]
			straight.up.end xRightTop top [widths.heading swSideTop 0 Upward]
		include : dispiro
			flat (xLeftTop + halfSwMiddle) top [widths.heading swSideTop 0 Downward]
			curl df.middle middleY [widths.heading (swSideTop / 2) (swSideTop / 2) Downward]
		include : dispiro
			flat (xRightTop - halfSwMiddle) top [widths.heading 0 swSideTop Downward]
			curl df.middle middleY [widths.heading (swSideTop / 2) (swSideTop / 2) Downward]

		include : match slabType
			[Just SLAB-NONE] : glyph-proc
			[Just SLAB-AUTO] : NeedSlab SLAB : composite-proc
				tagged 'serifLT' : LeftwardTopSerif  xLeftTop top SideJut
				tagged 'serifRT' : RightwardTopSerif xRightTop top SideJut
				tagged 'serifLB' : CenterBottomSerif (df.leftSB + swSideBot / 2 * HVContrast) 0 Jut
				tagged 'serifRB' : CenterBottomSerif (df.rightSB - swSideBot / 2 * HVContrast) 0 Jut
			[Just SLAB-MOTION] : tagged 'serifLT' : LeftwardTopSerif df.leftSB top SideJut
			[Just SLAB-SMALL-CYRILLIC] : NeedSlab SLAB : composite-proc
				tagged 'serifLT' : LeftwardTopSerif xLeftTop top SideJut
				tagged 'serifRT' : NeedNotItalic : RightwardTopSerif xRightTop top SideJut
				tagged 'serifLB' : if para.isItalic
					LeftwardBottomSerif df.leftSB 0 SideJut
					CenterBottomSerif (df.leftSB + swSideBot / 2 * HVContrast) 0 Jut
				tagged 'serifRB' : if para.isItalic
					RightwardBottomSerif df.rightSB 0 SideJut
					CenterBottomSerif (df.rightSB - swSideBot / 2 * HVContrast) 0 Jut

	define MConfig : object
		hanging                             { FORM-HANGING SLAB-AUTO            false }
		flatBottom                          { FORM-FLAT    SLAB-AUTO            false }
		hangingSmallCyrl                    { FORM-HANGING SLAB-SMALL-CYRILLIC  false }
		flatBottomSmallCyrl                 { FORM-FLAT    SLAB-SMALL-CYRILLIC  false }
		hangingMotionSerifed                { FORM-HANGING SLAB-MOTION          false }
		flatBottomMotionSerifed             { FORM-FLAT    SLAB-MOTION          false }
		slantedSidesHanging                 { FORM-HANGING SLAB-AUTO            true  }
		slantedSidesFlatBottom              { FORM-FLAT    SLAB-AUTO            true  }
		slantedSidesHangingSmallCyrl        { FORM-HANGING SLAB-SMALL-CYRILLIC  true  }
		slantedSidesFlatBottomSmallCyrl     { FORM-FLAT    SLAB-SMALL-CYRILLIC  true  }
		slantedSidesHangingMotionSerifed    { FORM-HANGING SLAB-MOTION          true  }
		slantedSidesFlatBottomMotionSerifed { FORM-FLAT    SLAB-MOTION          true  }

	foreach { suffix { form slab slanted } } [Object.entries MConfig] : do
		create-glyph "M.\(suffix)" : glyph-proc
			local df : DivFrame para.diversityM 3
			set-width df.width
			include : df.markSet.capital
			include : MShape CAP df form slab slanted
		create-glyph "smcpM.\(suffix)" : glyph-proc
			local df : DivFrame 1 3
			set-width df.width
			include : df.markSet.e
			include : MShape XH df form slab slanted

	select-variant 'M' 'M'
	select-variant 'smcpM' 0x1D0D (follow -- 'M')
	select-variant 'cyrl/em' 0x43C (shapeFrom -- 'smcpM')
	alias 'grek/Mu' 0x39C 'M'
	alias 'cyrl/Em' 0x41C 'M'

	glyph-block-import Letter-Blackboard : BBS BBD BBBarRight
	create-glyph 'mathbb/M' 0x1D544 : glyph-proc
		local df : DivFrame 1 3
		local bbs : AdviceStroke 8
		local bbd : BBD * [Math.min (3 / 4) (bbs / BBS)]
		local kDiag : DiagCorDs CAP (df.rightSB - df.leftSB) bbd
		local yTurnPos : CAP * 0.25

		include : MarkSet.capital
		include : VBarLeft df.leftSB 0 CAP bbs
		include : BBBarRight df.rightSB  0 CAP (bbd -- bbd) (bbs -- bbs)
		local xLeft : df.leftSB + 0.5 * bbs * HVContrast
		local xRight : df.rightSB - bbd - 0.5 * bbs * HVContrast
		local xMiddle : mix xLeft xRight 0.5
		include : HBarTop xLeft (xLeft + kDiag * bbd) CAP bbs
		include : intersection
			Rect CAP yTurnPos xLeft xRight
			ExtLineCenter 2 bbs xRight CAP xMiddle yTurnPos
		include : difference
			intersection
				Rect CAP yTurnPos xLeft xRight
				union
					ExtLineCenter 2 bbs xLeft CAP xMiddle yTurnPos
					ExtLineCenter 2 bbs (xLeft + kDiag * bbd) CAP (xMiddle + kDiag * bbd) yTurnPos
			spiro-outline
				corner (Width * 4)  [mix CAP yTurnPos (-1)]
				corner [mix xRight xMiddle (-1)] [mix CAP yTurnPos (-1)]
				corner [mix xRight xMiddle (2)]  [mix CAP yTurnPos (2)]
				corner (Width * 4)  [mix CAP yTurnPos (2)]