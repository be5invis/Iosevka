$$include '../../../meta/macros.ptl'

import [mix fallback SuffixCfg] from"../../../support/utils.mjs"
import [Dotless CvDecompose MathSansSerif] from"../../../support/gr.mjs"

glyph-module

glyph-block Letter-Latin-Lower-P : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives
	glyph-block-import Mark-Adjustment : ExtendAboveBaseAnchors LeaningAnchor
	glyph-block-import Letter-Shared : CreateAccentedComposition
	glyph-block-import Letter-Shared-Shapes : OBarLeft DToothlessRise DMBlend
	glyph-block-import Letter-Shared-Shapes : TopHook PalatalHook SerifFrame


	define [EaredBody top] : glyph-proc
		include : tagged 'bowl' : OBarLeft.shape
		include : tagged 'stemLeft' : VBar.l SB Descender top

	define [EarlessCornerBody] : glyph-proc
		include : tagged 'bowl' : OBarLeft.toothlessTop (rise -- DToothlessRise) (mBlend -- DMBlend)
		include : tagged 'stemLeft' : VBar.l SB Descender (XH - DToothlessRise)

	define [EarlessRoundedBody] : glyph-proc
		include : tagged 'bowl' : OBarLeft.roundedTop (yTerminal -- Descender)

	define [SmallPSerifLT top] : return [SerifFrame.fromDf [DivFrame 1] top Descender].lt.outer
	define [SmallPSerifLB top] : return [SerifFrame.fromDf [DivFrame 1] top Descender].lb.fullSide

	define [FullSerifs top] : composite-proc [SmallPSerifLT top] [SmallPSerifLB top]
	define [MotionSerif top] : SmallPSerifLT top
	define [BottomSerif top] : SmallPSerifLB top

	define PConfig : SuffixCfg.weave
		object # body
			eared                EaredBody
			earlessCorner        EarlessCornerBody
			earlessRounded       EarlessRoundedBody
		object # serifs
			serifless           { no-shape     false }
			motionSerifed       { MotionSerif  false }
			bottomSerifed       { BottomSerif  true  }
			serifed             { FullSerifs   true  }

	foreach { suffix { Body {Serifs doBS} }} [Object.entries PConfig] : do
		local yOverlay : mix 0 (Descender + [if doBS Stroke 0]) 0.5
		create-glyph "p.\(suffix)" : glyph-proc
			include : MarkSet.p
			include : Body XH
			include : Serifs XH
			include : LeaningAnchor.Below.VBar.l SB
			set-base-anchor 'overlayOnExtension' (SB + [HSwToV : 0.5 * Stroke]) yOverlay
			set-base-anchor 'strike' Middle (XH / 2)

		create-glyph "thorn.\(suffix)" : glyph-proc
			include : MarkSet.bp
			include : Body Ascender
			include : Serifs Ascender
			set-base-anchor 'overlayOnExtension' (SB + [HSwToV : 0.5 * Stroke]) yOverlay
			set-base-anchor 'strike' Middle (XH / 2)

		create-glyph "pPalatalHook.\(suffix)" : glyph-proc
			include [refer-glyph "p.\(suffix)"] AS_BASE ALSO_METRICS

			local tinyGap : Math.max (Width / 32) [AdviceStroke 24]
			local xMask : Math.max
				SB + [HSwToV Stroke] + 2 * tinyGap
				SB + [HSwToV : 0.5 * Stroke] + MidJutSide + tinyGap

			include : PalatalHook.r
				x -- [mix (Middle + CorrectionOMidS) RightSB 0.75]
				y -- 0
				yAttach -- [mix O SmallArchDepthA : archv.yFromX 0.75]
				maskOut -- [intersection [MaskBelow 0] [MaskLeft xMask]]

	select-variant 'p' 'p'
	link-reduced-variant 'p/sansSerif' 'p' MathSansSerif
	link-reduced-variant 'p/hookTopBase' 'p'
	select-variant 'cyrl/er' 0x440 (shapeFrom -- 'p') (follow -- 'p')
	select-variant "pPalatalHook" 0x1D88 (follow -- 'p')

	select-variant 'thorn' 0xFE
	alias 'grek/sho' 0x3F8 'thorn.earedSerifless'

	CreateAccentedComposition 'pTildeOver' 0x1D71 'p' 'tildeOverOnExension'

	derive-glyphs 'cyrl/rrTick' 0x48F 'cyrl/er' : lambda [src gr] : glyph-proc
		include [refer-glyph src] AS_BASE ALSO_METRICS
		include : dispiro
			widths.center [AdviceStroke 4]
			flat [mix SB RightSB 0.5]   (HalfStroke + XH * 0.25)
			curl [mix SB RightSB 0.875] (HalfStroke - XH * 0.25)

	derive-glyphs 'pHookTop' 0x1A5 "p/hookTopBase" : function [src gr] : glyph-proc
		include [refer-glyph src] AS_BASE ALSO_METRICS
		eject-contour 'serifLT'
		eject-contour 'stemLeft'
		include : TopHook.lBarOuter SB Descender XH

	glyph-block-import Letter-Blackboard : BBS BBD BBBarLeft
	create-glyph 'mathbb/p' 0x1D561 : glyph-proc
		include : MarkSet.p
		include : BBBarLeft SB Descender XH
		include : intersection
			OShapeOutline.NoOvershoot XH 0 (SB + BBD + [HSwToV BBS]) RightSB
			VBar.r (RightSB - BBD - OX) 0 XH BBS
		include : OBarLeft.shape
			left -- SB + BBD
			sw -- BBS
