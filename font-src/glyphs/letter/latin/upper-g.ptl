$$include '../../../meta/macros.ptl'

import [mix linreg clamp fallback] from '../../../support/utils'
import [DesignParameters] from '../../../meta/aesthetics'
import [Dotless CvDecompose] from "../../../support/gr"

glyph-module

glyph-block Letter-Latin-Upper-G : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives
	glyph-block-import Letter-Shared-Shapes : DToothlessRise ShoulderMidKnotLhs
	glyph-block-import Letter-Shared-Shapes : SerifedArcStart_RtlLhs ArcStartHookTop
	glyph-block-import Letter-Shared-Shapes : ArcStartSerifR ArcStartInwardSerifR

	define TOOTHED            0
	define TOOTHLESS-CORNER   1
	define TOOTHLESS-ROUNDED  2

	define CROSSBAR-NONE      0
	define CROSSBAR-HOOKED    1
	define CROSSBAR-CAPPED    2

	define SLAB-NONE          0
	define SLAB-LETTER        1
	define SLAB-INWARD        2
	define SLAB-HOOK-TOP     10

	define [GShape toothShape slabShape crossBarShape top sma smb] : glyph-proc
		local yBar : top * 0.52 + Stroke * 0.25
		local fine ShoulderFine

		local knots : match slabType
			([Just SLAB-LETTER] || [Just SLAB-HOOK-TOP]) : SerifedArcStart_RtlLhs RightSB Middle top Stroke Hook (slabShape === SLAB-HOOK-TOP)
			[Just SLAB-INWARD] : list [g4 RightSB (top - DToothlessRise) [widths.lhs]] [g4 (Middle - CorrectionOMidS) (top - O)] [archv]
			__ : list [widths.lhs] [g4 RightSB (top - Hook)] [hookstart (top - O)]

		knots.push
			flat (SB + OX) (top - sma)
			curl (SB + OX) smb
			arcvh

		match toothShape
			[Just TOOTHED] : knots.push
				ShoulderMidKnotLhs Middle O fine Stroke 1 1
				archv
				straight.up.end (RightSB - (Stroke - fine) * HVContrast) sma [widths.heading fine 0 Upward]
			[Just TOOTHLESS-CORNER] : knots.push
				g4   (Middle + CorrectionOMidS) O
				g4   RightSB  DToothlessRise
			[Just TOOTHLESS-ROUNDED] : knots.push
				g4   (Middle + CorrectionOMidS) O
				flat RightSB sma
				curl RightSB yBar [heading Upward]

		include : union
			dispiro.apply null knots
			if (slabShape === SLAB-LETTER) [ArcStartSerifR RightSB top Stroke Hook] [glyph-proc]
			if (slabShape === SLAB-INWARD) [ArcStartInwardSerifR RightSB top Stroke Hook] [glyph-proc]
			match crossBarShape
				[Just CROSSBAR-NONE]   : glyph-proc
				[Just CROSSBAR-HOOKED] : HBarTop Middle RightSB yBar
				[Just CROSSBAR-CAPPED] : HBarTop Middle [mix RightSB Width 0.625] yBar
			match toothShape
				[Just TOOTHED] : union
					VBarRight RightSB (sma + O) yBar
					VBarRight RightSB sma 0 [Math.max [AdviceStroke 5] (Stroke - fine / 2)]
				[Just TOOTHLESS-CORNER]  : VBarRight RightSB DToothlessRise yBar
				[Just TOOTHLESS-ROUNDED] : glyph-proc

	define [OverlayBarOfG top] : begin
		return : HOverlayBar [mix SB RightSB 0.5] [mix RightSB Width 0.7] [mix 0 (top / 2) 0.55]

	define GConfig : object
		toothedSeriflessHookless              { TOOTHED           CROSSBAR-NONE   SLAB-NONE   }
		toothlessCornerSeriflessHookless      { TOOTHLESS-CORNER  CROSSBAR-NONE   SLAB-NONE   }
		toothlessRoundedSeriflessHookless     { TOOTHLESS-ROUNDED CROSSBAR-NONE   SLAB-NONE   }
		toothedSeriflessHooked                { TOOTHED           CROSSBAR-HOOKED SLAB-NONE   }
		toothlessCornerSeriflessHooked        { TOOTHLESS-CORNER  CROSSBAR-HOOKED SLAB-NONE   }
		toothlessRoundedSeriflessHooked       { TOOTHLESS-ROUNDED CROSSBAR-HOOKED SLAB-NONE   }
		toothedSeriflessCapped                { TOOTHED           CROSSBAR-CAPPED SLAB-NONE   }
		toothlessCornerSeriflessCapped        { TOOTHLESS-CORNER  CROSSBAR-CAPPED SLAB-NONE   }
		toothlessRoundedSeriflessCapped       { TOOTHLESS-ROUNDED CROSSBAR-CAPPED SLAB-NONE   }
		toothedSerifedHookless                { TOOTHED           CROSSBAR-NONE   SLAB-LETTER }
		toothlessCornerSerifedHookless        { TOOTHLESS-CORNER  CROSSBAR-NONE   SLAB-LETTER }
		toothlessRoundedSerifedHookless       { TOOTHLESS-ROUNDED CROSSBAR-NONE   SLAB-LETTER }
		toothedSerifedHooked                  { TOOTHED           CROSSBAR-HOOKED SLAB-LETTER }
		toothlessCornerSerifedHooked          { TOOTHLESS-CORNER  CROSSBAR-HOOKED SLAB-LETTER }
		toothlessRoundedSerifedHooked         { TOOTHLESS-ROUNDED CROSSBAR-HOOKED SLAB-LETTER }
		toothedSerifedCapped                  { TOOTHED           CROSSBAR-CAPPED SLAB-LETTER }
		toothlessCornerSerifedCapped          { TOOTHLESS-CORNER  CROSSBAR-CAPPED SLAB-LETTER }
		toothlessRoundedSerifedCapped         { TOOTHLESS-ROUNDED CROSSBAR-CAPPED SLAB-LETTER }
		toothedInwardSerifedHookless          { TOOTHED           CROSSBAR-NONE   SLAB-INWARD }
		toothlessCornerInwardSerifedHookless  { TOOTHLESS-CORNER  CROSSBAR-NONE   SLAB-INWARD }
		toothlessRoundedInwardSerifedHookless { TOOTHLESS-ROUNDED CROSSBAR-NONE   SLAB-INWARD }
		toothedInwardSerifedHooked            { TOOTHED           CROSSBAR-HOOKED SLAB-INWARD }
		toothlessCornerInwardSerifedHooked    { TOOTHLESS-CORNER  CROSSBAR-HOOKED SLAB-INWARD }
		toothlessRoundedInwardSerifedHooked   { TOOTHLESS-ROUNDED CROSSBAR-HOOKED SLAB-INWARD }
		toothedInwardSerifedCapped            { TOOTHED           CROSSBAR-CAPPED SLAB-INWARD }
		toothlessCornerInwardSerifedCapped    { TOOTHLESS-CORNER  CROSSBAR-CAPPED SLAB-INWARD }
		toothlessRoundedInwardSerifedCapped   { TOOTHLESS-ROUNDED CROSSBAR-CAPPED SLAB-INWARD }


	foreach { suffix { shape crossBarShape slabType } } [Object.entries GConfig] : begin
		create-glyph "G.\(suffix)" : glyph-proc
			include : MarkSet.capital
			include : GShape shape slabType crossBarShape CAP SmoothA SmoothB
			create-derived "Gbar.\(suffix)" : OverlayBarOfG CAP
		create-glyph "GHookTop.\(suffix)" : glyph-proc
			include : MarkSet.capital
			include : GShape shape SLAB-HOOK-TOP crossBarShape CAP SmoothA SmoothB
			include : ArcStartHookTop RightSB CAP Stroke HookX Hook
		create-glyph "smcpG.\(suffix)" : glyph-proc
			include : MarkSet.e
			include : GShape shape slabType crossBarShape XH SmoothA SmoothB
		create-glyph "smcpGHookTop.\(suffix)" : glyph-proc
			include : MarkSet.e
			include : GShape shape SLAB-HOOK-TOP crossBarShape XH SmoothA SmoothB
			include : ArcStartHookTop RightSB XH Stroke HookX Hook

	select-variant 'G' 'G'
	select-variant 'smcpG' 0x262 (follow -- 'G')
	select-variant 'GHookTop' 0x193
	select-variant 'smcpGHookTop' 0x29B (follow -- 'GHookTop')
	select-variant 'Gbar' 0x1E4 (follow -- 'G')
	turned 'turnG' 0x2141 'G' Middle (CAP / 2)

	glyph-block-import Letter-Blackboard : BBS BBD BBBarRight
	define [BBGArcT sink offset top sma smb xTerm yTerm] : sink
		widths.lhs BBS
		g4 (RightSB - offset) (top - Hook)
		hookstart (top - O - offset)
		flat (SB + OX + offset) (top - sma)
		curl (SB + OX + offset) smb
		arcvh
		g4   ([mix SB xTerm 0.5] + CorrectionOMidS) (O + offset)
		archv
		flat (xTerm - offset) sma
		curl (xTerm - offset) yTerm [heading Upward]

	create-glyph 'mathbb/G' 0x1D53E : glyph-proc
		local yBar : CAP * 0.52 + BBS * 0.25
		include : MarkSet.capital
		include : BBGArcT dispiro 0 CAP SmoothA SmoothB (RightSB - BBD) yBar
		include : intersection
			BBGArcT spiro-outline 1 CAP SmoothA SmoothB (RightSB - BBD) yBar
			VBarLeft (SB + OX + BBD) 0 CAP BBS
		include : BBBarRight RightSB 0 yBar
