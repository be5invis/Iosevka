$$include '../../../meta/macros.ptl'

import [mix linreg clamp fallback] from '../../../support/utils'
import [Dotless CvDecompose] from "../../../support/gr"

glyph-module

glyph-block Letter-Latin-Lower-E : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives
	glyph-block-import Letter-Shared-Shapes : FlatHookDepth

	define [SmallEShape] : params [df top stroke barpos curly [bbd 0]] : glyph-proc
		local barbottom : top * [fallback barpos DesignParameters.eBarPos] - (stroke / 2)
		local hook : Math.min AHook : if para.isItalic top (stroke / 2 + (top - stroke * 3) / 4)

		include : HBarBottom (df.leftSB + (stroke / 2) + OX + bbd) (df.rightSB - (stroke / 2) - OX) barbottom stroke
		include : dispiro
			widths.lhs stroke
			flat (df.rightSB - OX) barbottom [heading Upward]
			curl (df.rightSB - OX) (top - SmallSmoothB)
			arcvh
			g4 (df.middle - CorrectionOMidS) (top - O)
			archv
			flat (df.leftSB + OX) (top - SmallSmoothA)
			curl (df.leftSB + OX) (0 + SmallSmoothB)
			hookend O (sw -- stroke)
			g4 (df.rightSB - OX * [if para.isItalic 0 0.5]) hook

	define [RevSmallEShape] : params [df top stroke barpos] : glyph-proc
		local barbottom (top * [fallback barpos DesignParameters.eBarPos] - HalfStroke)
		local hookx df.leftSB
		local hookmiddle : [mix (df.rightSB - O) hookx 0.55] + CorrectionOMidS

		include : HBarBottom (df.leftSB + (stroke / 2)) (df.rightSB - (stroke / 2)) barbottom stroke
		include : dispiro
			widths.rhs stroke
			flat (df.leftSB + OX) barbottom [heading Upward]
			curl (df.leftSB + OX) (top - SmallSmoothA)
			arcvh
			g4   df.middle (top - O)
			archv
			flat (df.rightSB - OX) (top - SmallSmoothB)
			curl (df.rightSB - OX) SmallSmoothA
			hookend O (sw -- stroke)
			g4 (df.leftSB + O) AHook

	define [SmallERoundedShape] : params [df top stroke barpos] : glyph-proc
		local barbottom : top * [fallback barpos : if para.isItalic 0.500 0.475] - (stroke / 2)
		local hook : Math.min AHook : if para.isItalic top (stroke / 2 + (top - stroke * 3) / 4)
		local pBarRight : 0.475 - TanSlope * 0.5
		local pArcRight : if para.isItalic (0.425 - TanSlope * 0.25) (SmoothA / (SmoothA + SmoothB))

		local xStart : df.leftSB + (0.125 * stroke * HVContrast)
		local pfIt : if para.isItalic 1 0
		include : dispiro
			widths.lhs stroke
			[if para.isItalic g2 flat] xStart (barbottom - pfIt * [StrokeWidthBlend 2 3] * O)
			if para.isItalic [alsoThru.g2 0.5 0.8] [list]
			[if para.isItalic g2 curl] [mix (xStart + [if para.isItalic 0.25 0.0] * stroke * HVContrast) df.rightSB pBarRight] (barbottom + pfIt * [StrokeWidthBlend 0.25 1] * O)
			if para.isItalic {} [archv]
			g4 (df.rightSB - OX) [mix barbottom top pArcRight]
			arcvh
			g4 (df.middle - CorrectionOMidS) (top - O)
			archv
			flat (df.leftSB + OX) (top - SmallSmoothA)
			curl (df.leftSB + OX) (0 + SmallSmoothB)
			hookend O (sw -- stroke)
			g4 (df.rightSB - OX * [if para.isItalic 0 0.5]) hook

	define [RevSmallERoundedShape] : params [df top stroke barpos] : glyph-proc
		local barbottom : top * [fallback barpos : if para.isItalic 0.500 0.475] - (stroke / 2)
		local hook : Math.min AHook : if para.isItalic top (stroke / 2 + (top - stroke * 3) / 4)
		local pBarRight : 0.475 - TanSlope * 0.5
		local pArcRight : if para.isItalic (0.425 + TanSlope * 0.25) (SmoothB / (SmoothA + SmoothB))

		local xStart : df.rightSB - (0.125 * stroke * HVContrast)
		local pfIt : if para.isItalic 1 0
		include : dispiro
			widths.rhs stroke
			[if para.isItalic g2 flat] xStart (barbottom - pfIt * [StrokeWidthBlend 2 3] * O)
			if para.isItalic [alsoThru.g2 0.5 0.8] [list]
			[if para.isItalic g2 curl] [mix (xStart - [if para.isItalic 0.25 0.0] * stroke * HVContrast) df.leftSB pBarRight] (barbottom + pfIt * [StrokeWidthBlend 0.25 1] * O)
			if para.isItalic {} [archv]
			g4 (df.leftSB + OX) [mix barbottom top pArcRight]
			arcvh
			g4 (df.middle - CorrectionOMidS) (top - O)
			archv
			flat (df.rightSB - OX) (top - SmallSmoothB)
			curl (df.rightSB - OX) (0 + SmallSmoothA)
			hookend O (sw -- stroke)
			g4 (df.leftSB + OX * [if para.isItalic 0 0.5]) hook

	define SmallEConfig : object
		flatCrossbar { SmallEShape        RevSmallEShape        }
		rounded      { SmallERoundedShape RevSmallERoundedShape }

	define [AbkCheShape Body df top fDesc] : glyph-proc
		local gap : (df.width - 2 * df.leftSB - 2.5 * df.mvs) * 0.375 - 0.25 * df.mvs * HVContrast
		define divSub : (df.width - gap - df.mvs) / Width
		define dfSub : DivFrame divSub 2
		include : Body dfSub top df.mvs
		if fDesc : include : difference
			VBar dfSub.middle (- Accent) 0 [AdviceStroke 3.5 df.div]
			OShapeOutline.NoOvershoot top 0 dfSub.leftSB dfSub.rightSB df.mvs
		include : Translate (Width * (df.div - divSub)) 0

		local hd : FlatHookDepth df
		local yBar : top * DesignParameters.eBarPos - 0.5 * df.mvs
		include : intersection [MaskLeft (dfSub.leftSB + Width * (df.div - divSub))] : dispiro
			flat (df.leftSB - 0.25 * df.mvs * HVContrast) (yBar + Hook) [widths.lhs.heading df.mvs Downward]
			curl (df.leftSB - 0.25 * df.mvs * HVContrast) (yBar + [Math.min Hook hd.y] - df.mvs * 0.25) [heading Downward]
			arcvh
			flat [Math.min (df.leftSB + hd.x - 0.5 * df.mvs * HVContrast) (dfSub.leftSB + Width * (df.div - divSub))] yBar
			curl (dfSub.middle + Width * (df.div - divSub)) yBar

	foreach { suffix { Body RevBody } } [Object.entries SmallEConfig] : do
		create-glyph "e.\(suffix)" : glyph-proc
			include : MarkSet.e
			include : Body [DivFrame 1] XH [AdviceStroke2 2 3 XH]

		create-glyph "Schwa.\(suffix)" : glyph-proc
			include : MarkSet.capital
			include : Body [DivFrame 1] CAP [AdviceStroke2 2 3 CAP]
			include : FlipAround Middle (CAP / 2)

		create-glyph "schwa.\(suffix)" : glyph-proc
			include [refer-glyph "e.\(suffix)"] AS_BASE ALSO_METRICS
			include : FlipAround Middle (XH / 2)

		create-glyph "eRev.\(suffix)" : glyph-proc
			include : MarkSet.e
			include : RevBody [DivFrame 1] XH [AdviceStroke2 2 3 XH]

		create-glyph "cyrl/abk/Che.\(suffix)" : glyph-proc
			local df : DivFrame para.diversityM 2.5
			set-width df.width
			include : df.markSet.capital
			include : AbkCheShape Body df CAP 0

		create-glyph "cyrl/abk/che.\(suffix)" : glyph-proc
			local df : DivFrame para.diversityM 2.5
			set-width df.width
			include : df.markSet.e
			include : AbkCheShape Body df XH 0

		create-glyph "cyrl/abk/CheDescender.\(suffix)" : glyph-proc
			local df : DivFrame para.diversityM 2.5
			set-width df.width
			include : df.markSet.if
			include : AbkCheShape Body df CAP 1

		create-glyph "cyrl/abk/cheDescender.\(suffix)" : glyph-proc
			local df : DivFrame para.diversityM 2.5
			set-width df.width
			include : df.markSet.p
			include : AbkCheShape Body df XH 1

	select-variant 'e' 'e'
	alias 'cyrl/ie' 0x435 'e'
	turned 'turne' 0x1DD 'e' Middle (XH / 2)

	select-variant 'Schwa' 0x18F
	alias 'cyrl/Schwa' 0x4D8 'Schwa'

	select-variant 'schwa' 0x259
	alias 'cyrl/schwa' 0x4D9 'turne'

	select-variant 'eRev' 0x258 (follow -- 'e')

	select-variant 'cyrl/abk/Che' 0x4BC (follow -- 'e')
	select-variant 'cyrl/abk/che' 0x4BD (follow -- 'e')
	select-variant 'cyrl/abk/CheDescender' 0x4BE (follow -- 'e')
	select-variant 'cyrl/abk/cheDescender' 0x4BF (follow -- 'e')

	glyph-block-import Letter-Blackboard : BBS BBD
	create-glyph 'mathbb/e' 0x1D556 : glyph-proc
		include : MarkSet.e
		include : SmallEShape [DivFrame 1] XH BBS (bbd -- BBD)
		include : intersection
			OShapeOutline.NoOvershoot XH 0 SB RightSB BBS
			union
				VBarLeft (SB + BBD + OX) 0 XH BBS
				VBarRight (RightSB - BBD - OX) (XH * DesignParameters.eBarPos) XH BBS

