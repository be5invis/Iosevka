$$include '../../../meta/macros.ptl'

import [mix linreg clamp fallback] from '../../../support/utils'
import [DesignParameters] from '../../../meta/aesthetics'
import [Dotless CvDecompose MathSansSerif] from "../../../support/gr"

glyph-module

glyph-block Letter-Latin-C : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives
	glyph-block-import Letter-Shared : CreateAccentedComposition
	glyph-block-import Letter-Shared-Shapes : SerifFrame CurlyTail DToothlessRise ArcStartHookTop
	glyph-block-import Letter-Shared-Shapes : SerifedArcStart_RtlLhs SerifedArcStart_LtrRhs
	glyph-block-import Letter-Shared-Shapes : SerifedArcEnd_LtrLhs SerifedArcEnd_RtlRhs
	glyph-block-import Letter-Shared-Shapes : ArcStartSerifL ArcStartSerifR
	glyph-block-import Letter-Shared-Shapes : ArcEndSerifL ArcEndSerifR
	glyph-block-import Letter-Shared-Shapes : ArcStartInwardSerifL ArcStartInwardSerifR
	glyph-block-import Letter-Shared-Shapes : ArcEndInwardSerifL ArcEndInwardSerifR
	glyph-block-import Letter-Shared-Shapes : InwardSlabArcStart_RtlLhs
	glyph-block-import Letter-Blackboard : BBS BBD

	define SLAB-NONE       0
	define SLAB-CLASSICAL  1
	define SLAB-INWARD     2

	define [CShapeT sink offset df top bot sma smb sw] : sink
		widths.lhs sw
		g4 df.rightSB (top - Hook)
		hookstart (top - O - offset)
		flat (df.leftSB + OX + offset) (top - sma)
		curl (df.leftSB + OX + offset) (bot + smb)
		hookend (bot + O + offset)
		g4 df.rightSB (bot + Hook)

	define [SlabCShapeT sink offset df top bot sma smb sw origBar] : sink
		SerifedArcStart_RtlLhs (df.rightSB - offset) df.middle top sw Hook origBar
		flat (df.leftSB + OX + offset) (top - sma)
		curl (df.leftSB + OX + offset) (bot + smb)
		hookend (bot + O + offset)
		g4 df.rightSB (bot + Hook)

	define [BiSlabCShapeT sink offset df top bot sma smb sw origBar] : sink
		SerifedArcStart_RtlLhs (df.rightSB - offset) df.middle top sw Hook origBar
		flat (df.leftSB + OX + offset) (top - sma)
		curl (df.leftSB + OX + offset) (bot + smb)
		SerifedArcEnd_LtrLhs (df.rightSB - offset) df.middle bot sw Hook origBar

	define [UniInwardSlabCShapeT sink offset df top bot sma smb sw origBar] : sink
		widths.lhs sw
		g4 df.rightSB (top - DToothlessRise)
		g4 (df.middle - CorrectionOMidX * sw) (top - O - offset)
		archv
		flat (df.leftSB + OX + offset) (top - sma)
		curl (df.leftSB + OX + offset) (bot + smb)
		hookend (bot + O + offset)
		g4 df.rightSB (bot + Hook)

	define [BuInwardSlabCShapeT sink offset df top bot sma smb sw origBar] : sink
		widths.lhs sw
		g4 df.rightSB (top - DToothlessRise)
		g4 (df.middle - CorrectionOMidX * sw) (top - O - offset)
		archv
		flat (df.leftSB + OX + offset) (top - sma)
		curl (df.leftSB + OX + offset) (bot + smb)
		arcvh
		g4 (df.middle + CorrectionOMidX * sw) (bot + O + offset)
		g4 df.rightSB (bot + DToothlessRise)

	define [RevCShape df top bot sma smb sw] : dispiro
		widths.rhs sw
		g4 df.leftSB (top - Hook)
		hookstart (top - O)
		flat (df.rightSB - OX) (top - sma)
		curl (df.rightSB - OX) (bot + smb)
		hookend (bot + O)
		g4 df.leftSB (bot + Hook)

	define [SlabRevCShape df top bot sma smb sw] : dispiro
		widths.rhs sw
		SerifedArcStart_LtrRhs df.leftSB df.middle top sw Hook
		flat (df.rightSB - OX) (top - SmoothA)
		curl (df.rightSB - OX) (bot + SmoothB)
		hookend (bot + O)
		g4 df.leftSB (bot + Hook)

	define [BiSlabRevCShape df top bot sma smb sw] : dispiro
		widths.rhs sw
		SerifedArcStart_LtrRhs df.leftSB df.middle top sw Hook
		flat (df.rightSB - OX) (top - SmoothA)
		curl (df.rightSB - OX) (bot + SmoothB)
		SerifedArcEnd_RtlRhs df.leftSB df.middle bot sw Hook

	define [UniInwardSlabRevCShape df top bot sma smb sw] : dispiro
		widths.rhs sw
		g4 df.leftSB (top - DToothlessRise)
		g4 (df.middle - CorrectionOMidX * sw) (top - O)
		archv
		flat (df.rightSB - OX) (top - SmoothA)
		curl (df.rightSB - OX) (bot + SmoothB)
		hookend (bot + O)
		g4 df.leftSB (bot + Hook)

	define [BiInwardSlabRevCShape df top bot sma smb sw] : dispiro
		widths.rhs sw
		g4 df.leftSB (top - DToothlessRise)
		g4 (df.middle - CorrectionOMidX * sw) (top - O)
		archv
		flat (df.rightSB - OX) (top - SmoothA)
		curl (df.rightSB - OX) (bot + SmoothB)
		arcvh
		g4 (df.middle + CorrectionOMidX * sw) (bot + O)
		g4 df.leftSB (bot + DToothlessRise)

	glyph-block-export SansCShape
	define [SansCShape df top bot sma smb sw] : CShapeT dispiro 0 df top bot sma smb sw
	define [CShapeOutline df top bot sma smb sw] : CShapeT spiro-outline 1 df top bot sma smb sw
	define [SlabCShape df top bot sma smb sw] : SlabCShapeT dispiro 0 df top bot sma smb sw
	define [BiSlabCShape df top bot sma smb sw] : BiSlabCShapeT dispiro 0 df top bot sma smb sw
	define [UniInwardSlabCShape df top bot sma smb sw] : UniInwardSlabCShapeT dispiro 0 df top bot sma smb sw
	define [BiInwardSlabCShape df top bot sma smb sw] : BuInwardSlabCShapeT dispiro 0 df top bot sma smb sw

	define [AutoStartSerifR df sty top sw] : match sty
		[Just SLAB-CLASSICAL] : ArcStartSerifR df.rightSB top [fallback sw Stroke] Hook
		[Just SLAB-INWARD]    : ArcStartInwardSerifR df.rightSB top [fallback sw Stroke] Hook
		__ : glyph-proc

	define [AutoStartSerifL df sty top sw] : match sty
		[Just SLAB-CLASSICAL] : ArcStartSerifL df.leftSB top [fallback sw Stroke] Hook
		[Just SLAB-INWARD]    : ArcStartInwardSerifL df.leftSB top [fallback sw Stroke] Hook
		__ : glyph-proc

	define [AutoStartSerifRB df sty bot sw] : match sty
		[Just SLAB-CLASSICAL] : ArcEndSerifR df.rightSB bot [fallback sw Stroke] Hook
		[Just SLAB-INWARD]    : ArcEndInwardSerifR df.rightSB bot [fallback sw Stroke] Hook
		__ : glyph-proc

	define [AutoStartSerifLB df sty bot sw] : match sty
		[Just SLAB-CLASSICAL] : ArcEndSerifL df.leftSB bot [fallback sw Stroke] Hook
		[Just SLAB-INWARD]    : ArcEndInwardSerifL df.leftSB bot [fallback sw Stroke] Hook
		__ : glyph-proc

	define [CCurlyTailShape slabType] : glyph-proc
		local sw : AdviceStroke2 2 3 XH
		local fine : AdviceStroke2 3 3 XH
		local rinner : ((XH * 0.45) - fine * 1.5) / 2
		local m1 : SB + O + sw * HVContrast
		local x2 : SB + sw * 0.3
		local y2 : -XH * 0.05

		include : dispiro
			match slabType
				[Just SLAB-CLASSICAL] : SerifedArcStart_RtlLhs RightSB Middle XH sw Hook
				[Just SLAB-INWARD] : InwardSlabArcStart_RtlLhs RightSB Middle XH sw Hook
				__ : list [g4 RightSB (XH - Hook) [widths.lhs sw]] [hookstart XH]
			flat (SB + OX) (XH - SmallSmoothA)
			curl (SB + OX) SmallSmoothB
			arcvh
			CurlyTail fine rinner m1 0 RightSB x2 y2

	local CConfig : object
		serifless               { SLAB-NONE      SLAB-NONE      SansCShape          RevCShape              }
		unilateralSerifed       { SLAB-CLASSICAL SLAB-NONE      SlabCShape          SlabRevCShape          }
		bilateralSerifed        { SLAB-CLASSICAL SLAB-CLASSICAL BiSlabCShape        BiSlabRevCShape        }
		unilateralInwardSerifed { SLAB-INWARD    SLAB-NONE      UniInwardSlabCShape UniInwardSlabRevCShape }
		bilateralInwardSerifed  { SLAB-INWARD    SLAB-INWARD    BiInwardSlabCShape  BiInwardSlabRevCShape  }

	foreach { suffix { sty styBot cShape revCShape } } [Object.entries CConfig] : do
		create-glyph "C.\(suffix)" : glyph-proc
			include : MarkSet.capital
			include : cShape [DivFrame 1] CAP 0 SmoothA SmoothB Stroke
			include : AutoStartSerifR [DivFrame 1] sty CAP
			include : AutoStartSerifRB [DivFrame 1] styBot 0

		create-glyph "smcpC.\(suffix)" : glyph-proc
			include : MarkSet.e
			include : cShape [DivFrame 1] XH 0 SmoothA SmoothB Stroke
			include : AutoStartSerifR [DivFrame 1] sty XH
			include : AutoStartSerifRB [DivFrame 1] styBot 0

		create-glyph "revC.\(suffix)" : glyph-proc
			include : MarkSet.capital
			include : revCShape [DivFrame 1] CAP 0 SmoothA SmoothB Stroke
			include : AutoStartSerifL [DivFrame 1] sty CAP
			include : AutoStartSerifLB [DivFrame 1] styBot 0

		create-glyph "CHookTop.\(suffix)" : glyph-proc
			include : MarkSet.capital
			include : cShape [DivFrame 1] CAP 0 SmoothA SmoothB Stroke
			include : ArcStartHookTop RightSB CAP Stroke HookX Hook
			include : AutoStartSerifRB [DivFrame 1] styBot 0

		create-glyph "c.\(suffix)" : glyph-proc
			include : MarkSet.e
			include : cShape [DivFrame 1] XH 0 SmallSmoothA SmallSmoothB Stroke
			include : AutoStartSerifR [DivFrame 1] sty XH
			include : AutoStartSerifRB [DivFrame 1] styBot 0

		create-glyph "revSmallC.\(suffix)" : glyph-proc
			include : MarkSet.e
			include : revCShape [DivFrame 1] XH 0 SmallSmoothA SmallSmoothB Stroke
			include : AutoStartSerifL [DivFrame 1] sty XH
			include : AutoStartSerifLB [DivFrame 1] styBot 0

		create-glyph "cHookTop.\(suffix)" : glyph-proc
			include : MarkSet.e
			include : cShape [DivFrame 1] XH 0 SmallSmoothA SmallSmoothB Stroke
			include : ArcStartHookTop RightSB XH Stroke HookX Hook
			include : AutoStartSerifRB [DivFrame 1] styBot 0

		create-glyph "cCurlyTail.\(suffix)" : glyph-proc
			include : MarkSet.e
			include : CCurlyTailShape sty
			include : AutoStartSerifR [DivFrame 1] sty XH

		create-glyph "stretchedC.\(suffix)" : glyph-proc
			include : MarkSet.p
			include : cShape [DivFrame 1] XH Descender SmoothA SmoothB Stroke
			include : AutoStartSerifR [DivFrame 1] sty XH
			include : AutoStartSerifRB [DivFrame 1] styBot Descender

		define [IotifiedEsShape df top sma smb] : glyph-proc
			local gap : (df.width - 2 * df.leftSB - 2.5 * df.mvs) * 0.375
			define divSub : (df.width - gap - df.mvs) / Width
			define dfSub : DivFrame divSub 2
			include : cShape dfSub top 0 sma smb df.mvs
			include : AutoStartSerifR dfSub sty top df.mvs
			include : AutoStartSerifRB dfSub styBot 0 df.mvs

			include : Translate (Width * (df.div - divSub)) 0
			include : VBarLeft df.leftSB 0 top df.mvs
			include : HBar (df.leftSB - O) [mix df.leftSB df.rightSB (3 / 4)] (top / 2) df.mvs
			if SLAB : begin
				local sf : SerifFrame top 0 df.leftSB df.rightSB df.mvs
				if (df.width > para.refJut * 7) : begin
					include : tagged 'serifLT' sf.lt.full
					if [not para.isItalic] : include : tagged 'serifLB' sf.lb.full
				: else : begin
					include : tagged 'serifLT' sf.lt.outer
					if [not para.isItalic] : include : tagged 'serifLB' sf.lb.outer

		create-glyph "cyrl/iotified{E}.\(suffix)" : glyph-proc
			local df : DivFrame para.diversityM 2.5
			set-width df.width
			include : df.markSet.capital
			include : IotifiedEsShape df CAP SmoothA SmoothB

		create-glyph "cyrl/iotified{e}.\(suffix)" : glyph-proc
			local df : DivFrame para.diversityM 2.5
			set-width df.width
			include : df.markSet.e
			include : IotifiedEsShape df XH SmallSmoothA SmallSmoothB

	select-variant 'C' 'C'
	link-reduced-variant 'C/sansSerif' 'C' MathSansSerif
	select-variant 'revC' 0x2183 (follow -- 'C')
	alias 'cyrl/Es' 0x421 'C'
	alias 'grek/lunateSigma' 0x3F9 'C'

	select-variant 'CTopSerifOnly' (shapeFrom -- 'C')

	select-variant 'CHookTop' 0x187
	select-variant 'smcpC' 0x1D04 (follow -- 'C')

	select-variant 'c' 'c'
	link-reduced-variant 'c/sansSerif' 'c' MathSansSerif
	select-variant 'revSmallC' 0x2184 (follow -- 'c')
	alias 'cyrl/es' 0x441 'c'
	alias 'grek/lunateSmallSigma' 0x3F2 'c.serifless'

	select-variant 'cTopSerifOnly' (shapeFrom -- 'c')

	select-variant 'cCurlyTail' 0x255
	select-variant 'cHookTop' 0x188
	select-variant 'stretchedC' 0x297 (follow -- 'C')

	select-variant "cyrl/iotified{E}" 0x464 (follow -- 'C')
	select-variant "cyrl/iotified{e}" 0x465 (follow -- 'c')

	create-glyph 'mathbb/C' 0x2102 : glyph-proc
		include : MarkSet.capital
		include : SansCShape [DivFrame 1] CAP 0 SmoothA SmoothB BBS
		include : intersection
			CShapeOutline [DivFrame 1] CAP 0 SmoothA SmoothB BBS
			VBarLeft (SB + BBD) 0 CAP BBS

	create-glyph 'mathbb/c' 0x1D554 : glyph-proc
		include : MarkSet.capital
		include : SansCShape [DivFrame 1] XH 0 SmallSmoothA SmallSmoothB BBS
		include : intersection
			CShapeOutline [DivFrame 1] XH 0 SmallSmoothA SmallSmoothB BBS
			VBarLeft (SB + BBD + OX) 0 CAP BBS

	derive-glyphs 'currency/euroSign' 0x20AC 'C' : lambda [src sel] : glyph-proc
		include [refer-glyph src] AS_BASE ALSO_METRICS
		include : HOverlayBar [mix SB 0 0.7] [mix SB RightSB 0.7] (CAP * 0.4)
		include : HOverlayBar [mix SB 0 0.7] [mix SB RightSB 0.7] (CAP * 0.6)

	create-glyph 'currency/centSignBar.open' : glyph-proc
		set-width 0
		include : dispiro
			widths.center
			flat (Middle - Width) (XH - HalfStroke)
			curl (Middle - Width) (XH - Descender / 2) [heading Upward]
		include : dispiro
			widths.center
			flat (Middle - Width) (Descender / 2) [heading Upward]
			curl (Middle - Width) HalfStroke

	create-glyph 'currency/centSignBar.through' : glyph-proc
		define fine : AdviceStroke 4
		include [refer-glyph 'currency/centSignBar.open'] AS_BASE ALSO_METRICS
		include : VBar (Middle - Width) (XH - HalfStroke) (0 + HalfStroke) fine

	create-glyph 'currency/centSignBar.barInterrupted' : glyph-proc
		set-width 0
		define fine : AdviceStroke 4
		define gap : Math.max (XH / 8) [AdviceStroke2 6 6 XH]
		include : dispiro
			flat (Middle - Width) (XH - Descender / 2) [widths.center.heading Stroke Downward]
			curl (Middle - Width) (XH / 2 + gap / 2) [widths.center.heading fine Downward]
		include : dispiro
			flat (Middle - Width) (Descender / 2) [widths.center.heading Stroke Upward]
			curl (Middle - Width) (XH / 2 - gap / 2) [widths.center.heading fine Upward]

	select-variant 'currency/centSignBar' (follow -- 'cent')

	create-glyph 'currency/centSign' 0xA2 : glyph-proc
		include : union
			refer-glyph 'c'
			WithTransform [Translate Width 0] [refer-glyph 'currency/centSignBar']
		CvDecompose.set currentGlyph { 'c' 'currency/centSignBar' }

	turned 'turnC' 0x186 'C' Middle (CAP / 2)
	turned 'turnc' 0x254 'c' Middle (XH / 2)
	turned 'turnSmcpC' 0x1D10 'smcpC' Middle (XH / 2)
	alias 'grek/revLunateSigma' 0x3FD 'revC'
	alias 'grek/revLunateSmallSigma' 0x37B 'revSmallC.serifless'

	define [LunateSigmaShape k] : begin
		local halfDotWidth  : Math.min DotRadius ((RightSB - SB - HVContrast * Stroke * 2) / 4)
		local halfDotHeight : Math.min DotRadius (halfDotWidth * k)
		return : OShapeOutline.NoOvershoot
			CAP / 2 + halfDotHeight
			CAP / 2 - halfDotHeight
			0 + halfDotWidth
			0 - halfDotWidth
			Stroke * halfDotWidth * 2 / Width
			SmoothAOf halfDotWidth (halfDotWidth * 2)
			SmoothBOf halfDotWidth (halfDotWidth * 2)

	create-glyph 'grek/lunateSigmaDot' : glyph-proc
		set-width 0
		set-mark-anchor 'slash' 0 (CAP / 2) 0 (CAP / 2)
		include : LunateSigmaShape 2
	create-glyph 'grek/lunateSigmaDotSmall' : glyph-proc
		set-width 0
		set-mark-anchor 'slash' 0 (CAP / 2) 0 (CAP / 2)
		include : LunateSigmaShape 1.5

	derive-composites 'grek/dotLunateSigma' 0x3FE 'grek/lunateSigma' 'grek/lunateSigmaDot'
	derive-composites 'grek/dotRevLunateSigma' 0x3FF 'grek/revLunateSigma' 'grek/lunateSigmaDot'
	derive-composites 'grek/dotLunateSmallSigma' 0x37C 'grek/lunateSmallSigma' 'grek/lunateSigmaDotSmall'
	derive-composites 'grek/dotRevLunateSmallSigma' 0x37D 'grek/revLunateSmallSigma' 'grek/lunateSigmaDotSmall'

	derive-glyphs 'Cbar' 0xA792 'C' : lambda [src sel] : glyph-proc
		include [refer-glyph src] AS_BASE ALSO_METRICS
		include : HOverlayBar (SB * 0.3) [mix (SB + Stroke) (RightSB - Stroke) 0.55] (CAP * OverlayPos)

	derive-glyphs 'cbar' 0xA793 'c' : lambda [src sel] : glyph-proc
		include [refer-glyph src] AS_BASE ALSO_METRICS
		include : HOverlayBar (SB * 0.3) [mix (SB + Stroke) (RightSB - Stroke) 0.55] (XH * OverlayPos)

	derive-glyphs 'cyrl/E' 0x42D 'revC' : lambda [src sel] : glyph-proc
		include [refer-glyph src] AS_BASE ALSO_METRICS
		include : HBar [mix SB RightSB 0.25] RightSB (CAP / 2)

	derive-glyphs 'cyrl/e' 0x44D 'revSmallC' : lambda [src sel] : glyph-proc
		include [refer-glyph src] AS_BASE ALSO_METRICS
		include : HBar [mix SB RightSB 0.25] RightSB (XH / 2)

	derive-glyphs 'cyrl/Ye' 0x404 'C' : lambda [src sel] : glyph-proc
		include [refer-glyph src] AS_BASE ALSO_METRICS
		include : HBar SB [mix RightSB SB 0.25] (CAP / 2) [AdviceStroke2 2 4 CAP]

	derive-glyphs 'cyrl/ye' 0x454 'c' : lambda [src sel] : glyph-proc
		include [refer-glyph src] AS_BASE ALSO_METRICS
		include : HBar SB [mix RightSB SB 0.25] (XH / 2) [AdviceStroke2 2 4 XH]

	derive-glyphs 'cyrl/Koppa' 0x480 'CTopSerifOnly' : lambda [src sel] : glyph-proc
		include : MarkSet.if
		include : union [VBarRight (Middle + Stroke * HVContrast) Descender (Stroke / 2)] : difference
			refer-glyph src
			Rect (CAP / 2) Descender (Middle + Stroke * HVContrast) (Width * 4)
			Rect (XH / 2) [mix Stroke Hook 0.5] Middle (Width * 4)

	derive-glyphs 'cyrl/koppa' 0x481 'cTopSerifOnly' : lambda [src sel] : glyph-proc
		include : MarkSet.p
		include : union [VBarRight (Middle + Stroke * HVContrast) Descender (Stroke / 2)] : difference
			refer-glyph src
			Rect (XH / 2) Descender (Middle + Stroke * HVContrast) (Width * 4)
			Rect (XH / 2) [mix Stroke Hook 0.5] Middle (Width * 4)

	CreateAccentedComposition 'CCedilla' 0xC7 'C' 'cedillaBelow'
	CreateAccentedComposition 'cCedilla' 0xE7 'c' 'cedillaBelow'
