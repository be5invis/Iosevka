$$include '../../../meta/macros.ptl'

import [mix linreg clamp fallback] from"../../../support/utils.mjs"
import [Dotless CvDecompose MathSansSerif] from"../../../support/gr.mjs"

glyph-module

glyph-block Letter-Latin-X : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives
	glyph-block-import Letter-Shared-Shapes : ShoulderMidKnotRhs SerifFrame WithSerifOverflowMask
	glyph-block-import Letter-Shared-Shapes : DiagonalTailInnerRadius DiagonalTailF
	glyph-block-import Letter-Shared-Shapes : CyrDescender PalatalHook

	glyph-block-export HalfXStrand
	define [HalfXStrand stb fSlab _leftx lefty rightx righty turn pStraight tension _fine] : glyph-proc
		local sbCor : if stb ([StrokeWidthBlend 1 6] * OX * ([Math.abs (lefty - righty)] / CAP)) 0
		local leftx : _leftx + ([HSwToV HalfStroke] + [Math.max (-SideJut) sbCor]) * [if (rightx > _leftx) 1 (-1)]
		local fine  : (_fine || Stroke) * 0.5

		if stb : begin
			local hst : HalfStroke * [DiagCor (righty - lefty) (rightx - leftx) 0 0]
			local hse : HalfStroke * [Math.min 0.97 ([AdviceStroke 3] / Stroke)]
			local leftx2 : _leftx + ([HSwToV hst] + [Math.max (-SideJut) sbCor]) * [if (rightx > _leftx) 1 (-1)]
			include : dispiro
				corner leftx2 lefty [widths.heading hst hst [if (lefty < righty) Upward Downward]]
				corner rightx righty [widths hse hse]

		: else : begin
			local height : Math.abs (lefty - righty)
			local slabClearance : if fSlab [AdviceStroke2 2 3 height] 0
			local turnyleft : mix lefty righty [if fSlab [Math.max turn (slabClearance / height)] turn]
			local cyleft : mix turnyleft righty tension
			local straightxleft : mix leftx rightx pStraight
			local straightyleft : mix cyleft righty pStraight
			include : dispiro
				widths.center
				flat leftx lefty [heading [if (lefty < righty) Upward Downward]]
				curl leftx turnyleft [heading [if (lefty < righty) Upward Downward]]
				quadControls 0 ((cyleft - turnyleft) / (straightyleft - turnyleft)) 24
				flat straightxleft straightyleft
				curl rightx righty

	glyph-block-export XStrand
	define [XStrand stb slab _leftx lefty _rightx righty turn pStraight tension] : glyph-proc
		local middlex : mix _leftx _rightx 0.5
		local middley : mix lefty righty 0.5

		include : HalfXStrand stb slab _leftx lefty middlex middley turn pStraight tension
		include : HalfXStrand stb slab _rightx righty middlex middley turn pStraight tension

	define Shape : namespace
		define [XBase fStraight fSlab top bottom turn tension] : composite-proc
			XStrand fStraight fSlab SB bottom RightSB top turn 0.4 tension
			XStrand fStraight fSlab SB top RightSB bottom turn 0.4 tension

		export : define [StraightBase fSlab top bottom turn tension]
			XBase true  fSlab top bottom turn tension
		export : define [CurlyBase    fSlab top bottom turn tension]
			XBase false fSlab top bottom turn tension

		define [XCursiveHalfShape top bottom] : glyph-proc
			local ada : ArchDepthAOf (ArchDepth * 0.8) (Width / 2)
			local adb : ArchDepthBOf (ArchDepth * 0.8) (Width / 2)

			define kThin 0.550
			define swEnd : AdviceStroke 2.75
			define swMid : AdviceStroke 3
			define xCenterRight : Middle + [HSwToV swMid] / 2
			define xCenterLeft  : Middle - [HSwToV swMid] / 2
			define xTurn : mix (RightSB - [HSwToV swEnd]) xCenterRight 0.5
			define hook1Depth : Hook + Stroke * 0.125
			define hook1StraightDepth : Math.min
				hook1Depth - swEnd * 1.125
				Math.max 1 : hook1Depth / 5 - swEnd / 4

			define fineMid : swMid * kThin
			define rIn : Math.max (0.2 * (RightSB - SB) - [HSwToV : 0.7 * Stroke]) [AdviceStroke 16]
			define flatHookDepth : 0.7 * (RightSB - SB) - swEnd - 1.5 * rIn

			define upperHalf : include : dispiro
				flat (xCenterRight - [HSwToV fineMid]) ([mix bottom top 0.5] + O) [widths.rhs.heading fineMid Upward]
				curl (xCenterRight - [HSwToV fineMid]) (top - ada)
				arcvh
				ShoulderMidKnotRhs xTurn (top - O) fineMid swEnd (+1) (-1)
				archv
				flat (RightSB - OX) (top - hook1Depth + hook1StraightDepth) [heading Downward]
				curl (RightSB - OX) (top - hook1Depth) [heading Downward]

			# define upperHalf : include : dispiro
			# 	curl (RightSB - OX) (top - hook1Depth) [widths.lhs.heading swEnd Upward]
			# 	flat (RightSB - OX) (top - hook1Depth + hook1StraightDepth) [heading Upward]
			# 	arcvh 16
			# 	ShoulderMidKnotLhsRev (xTurn) (top - O) fineMid swEnd (-1) (-1)
			# 	archv 16
			# 	flat (xCenterRight - [HSwToV fineMid]) (top - ada) [widths.lhs fineMid]
			# 	curl (xCenterRight - [HSwToV fineMid]) ([mix bottom top 0.5] + O)

			define lowerHalf : include : dispiro
				flat (xCenterRight - [HSwToV : 0.5 * fineMid]) ([mix bottom top 0.5] - O) [widths.center fineMid]
				curl (xCenterRight - [HSwToV : 0.5 * fineMid]) (bottom + [Math.min adb (swMid + rIn)])
				arcvh 16
				DiagonalTailF 1 [DivFrame 1] (xCenterRight - [HSwToV : 0.5 * Stroke] + TanSlope * rIn) bottom rIn 50 flatHookDepth Stroke

			define lowerHalfLastKnot lowerHalf.rhsKnots.(lowerHalf.rhsKnots.length - 1)
			set-base-anchor 'cyrlDescenderAttach' lowerHalfLastKnot.x lowerHalfLastKnot.y

		export : define [CursiveBase fSerifs top bottom turn tension] : glyph-proc
			include : XCursiveHalfShape top bottom
			include : FlipAround Middle [mix bottom top 0.5]
			include : XCursiveHalfShape top bottom

		# Serifs
		export : define [FullSerifs top bot] : let [sf : SerifFrame.fromDf [DivFrame 1] top bot]
			composite-proc sf.lt.full sf.rt.full sf.lb.full sf.rb.full
		export : define [MotionSerifs top bot] : glyph-proc
			include : tagged 'serifLT' : HSerif.lt SB top SideJut
		export : define [MotionSerifsBilateral top bot] : glyph-proc
			include : tagged 'serifLT' : HSerif.lt SB top SideJut
			include : tagged 'serifRB' : HSerif.rb RightSB bot SideJut

	define Config : object
		straightSerifless              { Shape.StraightBase null                        false }
		curlySerifless                 { Shape.CurlyBase    null                        false }
		cursive                        { Shape.CursiveBase  null                        false }
		straightSerifed                { Shape.StraightBase Shape.FullSerifs            true  }
		curlySerifed                   { Shape.CurlyBase    Shape.FullSerifs            true  }
		straightMotionSerifed          { Shape.StraightBase Shape.MotionSerifs          false }
		curlyMotionSerifed             { Shape.CurlyBase    Shape.MotionSerifs          false }
		straightBilateralMotionSerifed { Shape.StraightBase Shape.MotionSerifsBilateral false }
		curlyBilateralMotionSerifed    { Shape.CurlyBase    Shape.MotionSerifsBilateral false }

	foreach { suffix { baseShape serifShape fMaskBase} } [Object.entries Config] : do
		define [letterShape top bottom turn tension] : glyph-proc
			local base : baseShape fMaskBase top bottom turn tension
			include : WithSerifOverflowMask fMaskBase top bottom SB RightSB base
			if serifShape : include : serifShape top bottom

		create-glyph "X.\(suffix)" : glyph-proc
			include : MarkSet.capital
			include : letterShape CAP 0 0.1 0.28

		create-glyph "x.\(suffix)" : glyph-proc
			include : MarkSet.e
			include : letterShape XH 0 0.1 0.24

		create-glyph "latn/chi.\(suffix)" : glyph-proc
			include : MarkSet.p
			include : letterShape XH Descender 0.05 0.11

		create-glyph "latn/Chi.\(suffix)" : glyph-proc
			include : MarkSet.capDesc
			include : letterShape CAP Descender 0.05 0.11

	select-variant 'X' 'X'
	link-reduced-variant 'X/sansSerif' 'X' MathSansSerif
	alias 'grek/Chi' 0x3A7 'X'
	alias-reduced-variant 'grek/Chi/sansSerif' 'grek/Chi' 'X/sansSerif' MathSansSerif
	alias 'cyrl/Ha' 0x425 'X'

	select-variant 'x' 'x'
	link-reduced-variant 'x/sansSerif' 'x' MathSansSerif
	select-variant 'cyrl/ha' 0x445 (shapeFrom -- 'x')

	select-variant 'grek/chi' 0x3C7 (shapeFrom -- 'latn/chi')
	link-reduced-variant 'grek/chi/sansSerif' 'grek/chi' MathSansSerif (shapeFrom -- 'latn/chi')
	select-variant 'latn/chi' 0xAB53 (follow -- 'x')
	select-variant 'latn/Chi' 0xA7B3 (follow -- 'X')


	define [AddDescender Ctor] : function [src sel] : glyph-proc
		include [refer-glyph src] AS_BASE ALSO_METRICS
		local attachAnchor currentGlyph.baseAnchors.cyrlDescenderAttach
		if attachAnchor
		: then : begin
			local attach : currentGlyph.gizmo.unapply attachAnchor
			include : Ctor.r attach.x 0 (yAttach -- attach.y)
		: else : begin
			local sw : Ctor.Sw Stroke
			include : Ctor.r
				x -- RightSB + SideJut + sw * TanSlope
				y -- 0
				xLink -- RightSB - [HSwToV HalfStroke]

	derive-glyphs 'cyrl/HaDescender' 0x4B2 'cyrl/Ha' [AddDescender CyrDescender]
	derive-glyphs 'cyrl/haDescender' 0x4B3 'cyrl/ha' [AddDescender CyrDescender]
	derive-glyphs 'cyrl/HaHook'      0x4FC 'cyrl/Ha' [AddDescender PalatalHook]
	derive-glyphs 'cyrl/haHook'      0x4FD 'cyrl/ha' [AddDescender PalatalHook]
	alias 'xPalatalHook' 0x1D8D 'cyrl/haHook'

	define [CyrlHaBarShape top] : HOverlayBar [mix SB RightSB 0.08] [mix SB RightSB 0.92] (top / 2)
	derive-composites 'cyrl/HaBar' 0x4FE 'cyrl/Ha' [CyrlHaBarShape CAP]
	derive-composites 'cyrl/haBar' 0x4FF 'cyrl/ha' [CyrlHaBarShape XH]

	glyph-block-import Letter-Blackboard : BBS BBD
	define [BBXShape l r top] : let [kDiag : DiagCorDs top (r - l) BBD] : union
		intersection
			Rect top 0 (-Width) (Width * 2)
			union
				ExtLineCenter 1 BBS l top (r - kDiag * BBD) 0
				ExtLineCenter 1 BBS (l + kDiag * BBD) top r 0
		HBar.t    l (l + kDiag * BBD) top BBS
		HBar.b r (r - kDiag * BBD) 0 BBS
		intersection
			Rect top 0 (-Width) (Width * 2)
			difference
				ExtLineCenter 1 BBS l 0 r top
				spiro-outline
					corner l top
					corner (l + kDiag * BBD) top
					corner r 0
					corner (r - kDiag * BBD) 0

	create-glyph 'mathbb/X' 0x1D54F : composite-proc [MarkSet.capital] [BBXShape SB RightSB CAP]
	create-glyph 'mathbb/x' 0x1D569 : composite-proc [MarkSet.e]       [BBXShape SB RightSB XH]
