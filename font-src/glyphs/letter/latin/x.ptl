$$include '../../../meta/macros.ptl'

import [mix linreg clamp fallback] from '../../../support/utils'
import [DesignParameters] from '../../../meta/aesthetics'
import [Dotless CvDecompose] from "../../../support/gr"

glyph-module

glyph-block Letter-Latin-X : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives
	glyph-block-import Letter-Shared-Shapes : ShoulderMidKnotLhsRev SerifFrame WithAIHSerifsMask
	glyph-block-import Letter-Shared-Shapes : DiagonalTailInnerRadius DiagonalTailF
	glyph-block-export HalfXStrand XStrand

	define [HalfXStrand stb slab _leftx lefty rightx righty turn pStraight tension _fine] : glyph-proc
		local sbCor : if stb ([StrokeWidthBlend 1 6] * OX * ([Math.abs (lefty - righty)] / CAP)) 0
		local leftx : _leftx + ((HalfStroke * HVContrast + sbCor) * [if (rightx > _leftx) 1 (-1)])
		local fine  : (_fine || Stroke) * 0.5

		if stb : begin
			local hst : HalfStroke * [DiagCor (righty - lefty) (rightx - leftx) 0 0]
			local hse : HalfStroke * [Math.min 0.97 ([AdviceStroke 3] / Stroke)]
			include : dispiro
				corner leftx lefty [widths.heading hst hst [if (lefty < righty) Upward Downward]]
				corner rightx righty [widths hse hse]

		: else : begin
			local height : Math.abs (lefty - righty)
			local slabClearance : if slab [AdviceStroke2 2 3 height] 0
			local turnyleft : mix lefty righty [if slab [Math.max turn (slabClearance / height)] turn]
			local cyleft : mix turnyleft righty tension
			local straightxleft : mix leftx rightx pStraight
			local straightyleft : mix cyleft righty pStraight
			include : dispiro
				widths.center
				flat leftx lefty [heading [if (lefty < righty) Upward Downward]]
				curl leftx turnyleft [heading [if (lefty < righty) Upward Downward]]
				quadcontrols 0 ((cyleft - turnyleft) / (straightyleft - turnyleft)) 24
				flat straightxleft straightyleft
				curl rightx righty

	define [XStrand stb slab _leftx lefty _rightx righty turn pStraight tension] : glyph-proc
		local middlex : mix _leftx _rightx 0.5
		local middley : mix lefty righty 0.5

		include : HalfXStrand stb slab _leftx lefty middlex middley turn pStraight tension
		include : HalfXStrand stb slab _rightx righty middlex middley turn pStraight tension

	define [UpperXBase fStraight] : union
		XStrand fStraight SLAB SB 0 RightSB CAP 0.1 0.4 0.28
		XStrand fStraight SLAB SB CAP RightSB 0 0.1 0.4 0.28

	define [XSerifs top] : let [sf : SerifFrame top 0 SB RightSB]
		composite-proc sf.lt.full sf.rt.full sf.lb.full sf.rb.full
	define [XMotionSerifs top] : glyph-proc
		include : tagged 'serifLT' : LeftwardTopSerif SB top SideJut

	create-glyph 'X.straightSerifless' : glyph-proc
		include : MarkSet.capital
		include : WithAIHSerifsMask CAP 0 SB RightSB : UpperXBase true
	create-glyph 'X.curlySerifless' : glyph-proc
		include : MarkSet.capital
		include : WithAIHSerifsMask CAP 0 SB RightSB : UpperXBase false
	create-glyph 'X.straightSerifed' : glyph-proc
		include : MarkSet.capital
		include : WithAIHSerifsMask CAP 0 SB RightSB : UpperXBase true
		include : XSerifs CAP
	create-glyph 'X.curlySerifed' : glyph-proc
		include : MarkSet.capital
		include : WithAIHSerifsMask CAP 0 SB RightSB : UpperXBase false
		include : XSerifs CAP
	create-glyph 'X.straightMotionSerifed' : glyph-proc
		include : MarkSet.capital
		include : UpperXBase true
		include : XMotionSerifs CAP
	create-glyph 'X.curlyMotionSerifed' : glyph-proc
		include : MarkSet.capital
		include : UpperXBase false
		include : XMotionSerifs CAP

	define [LowerXBase fStraight] : begin
		local turn    : if SLAB 0.1  0.1
		local tension : if SLAB 0.24 0.24
		union
			XStrand fStraight SLAB SB 0 RightSB XH turn 0.4 tension
			XStrand fStraight SLAB SB XH RightSB 0 turn 0.4 tension

	create-glyph 'x.straightSerifless' : glyph-proc
		include : MarkSet.e
		include : WithAIHSerifsMask XH 0 SB RightSB : LowerXBase true
	create-glyph 'x.curlySerifless' : glyph-proc
		include : MarkSet.e
		include : WithAIHSerifsMask XH 0 SB RightSB : LowerXBase false
	create-glyph 'x.straightSerifed' : glyph-proc
		include : MarkSet.e
		include : WithAIHSerifsMask XH 0 SB RightSB : LowerXBase true
		include : XSerifs XH
	create-glyph 'x.curlySerifed' : glyph-proc
		include : MarkSet.e
		include : WithAIHSerifsMask XH 0 SB RightSB : LowerXBase false
		include : XSerifs XH
	create-glyph 'x.straightMotionSerifed' : glyph-proc
		include : MarkSet.e
		include : LowerXBase true
		include : XMotionSerifs XH
	create-glyph 'x.curlyMotionSerifed' : glyph-proc
		include : MarkSet.e
		include : LowerXBase false
		include : XMotionSerifs XH

	define [XCursiveHalfShape fFlatEnd] : begin
		local sma : SmoothAOf (Smooth * 0.8) (Width / 2)
		local smb : SmoothBOf (Smooth * 0.8) (Width / 2)

		define kThin 0.550
		define swEnd : AdviceStroke 3
		define swMid : AdviceStroke 3.125
		define xCenterRight : Middle + HVContrast * swMid / 2
		define xCenterLeft  : Middle - HVContrast * swMid / 2
		define xTurn : mix (RightSB - swEnd * HVContrast) xCenterRight 0.5
		define hook1Depth : Hook + Stroke * 0.125
		define hook1StraightDepth : Math.min
			hook1Depth - swEnd * 1.125
			Math.max 1 : hook1Depth / 5 - swEnd / 4

		define fineMid : swMid * kThin
		define rIn : Math.max (0.2 * (RightSB - SB) - 0.7 * Stroke * HVContrast) [AdviceStroke 16]
		define flatHookDepth : 0.7 * (RightSB - SB) - swEnd - 1.5 * rIn

		return : union
			dispiro
				curl (RightSB - OX) (XH - hook1Depth) [widths.lhs.heading swEnd Upward]
				flat (RightSB - OX) (XH - hook1Depth + hook1StraightDepth) [heading Upward]
				arcvh 16
				ShoulderMidKnotLhsRev (xTurn) (XH - O) fineMid swEnd (-1) (-1)
				archv 16
				flat (xCenterRight - HVContrast * fineMid) (XH - sma) [widths.lhs fineMid]
				curl (xCenterRight - HVContrast * fineMid) (XH / 2 + O)
			if fFlatEnd
			: then : dispiro
				flat (xCenterRight - 0.5 * HVContrast * fineMid) (XH / 2 - O) [widths.center fineMid]
				curl (xCenterRight - 0.5 * HVContrast * fineMid) smb
				arcvh 16
				flat [mix xCenterRight RightSB 0.5] HalfStroke [widths.center.heading Stroke Rightward]
				curl RightSB HalfStroke [heading Rightward]
			: else : dispiro
				flat (xCenterRight - 0.5 * HVContrast * fineMid) (XH / 2 - O) [widths.center fineMid]
				curl (xCenterRight - 0.5 * HVContrast * fineMid) [Math.min smb (swMid + rIn)]
				arcvh 16
				DiagonalTailF 1 [DivFrame 1] (xCenterRight - 0.5 * HVContrast * Stroke + TanSlope * rIn) 0 rIn 50 flatHookDepth Stroke

	create-glyph 'x.cursive' : glyph-proc
		include : MarkSet.e
		include : XCursiveHalfShape
		include : FlipAround Middle (XH / 2)
		include : XCursiveHalfShape

	create-glyph 'x.cursiveCyrlHaDescenderBase' : glyph-proc
		include : MarkSet.e
		include : XCursiveHalfShape
		include : FlipAround Middle (XH / 2)
		include : XCursiveHalfShape true

	define [ChiBase fStraight] : union
		XStrand fStraight false SB Descender RightSB XH 0.05 0.4 0.11
		XStrand fStraight false SB XH RightSB Descender 0.05 0.4 0.11

	create-glyph 'grek/chi.straight' : composite-proc [MarkSet.p] [ChiBase true]
	create-glyph 'grek/chi.curly' : composite-proc [MarkSet.p] [ChiBase false]

	select-variant 'X' 'X'
	alias 'grek/Chi' 0x3A7 'X'
	alias 'cyrl/Ha' 0x425 'X'

	select-variant 'x' 'x'
	select-variant 'xCyrlHaDescenderBase' nothing (shapeFrom -- 'x')
	select-variant 'cyrl/ha' 0x445 (shapeFrom -- 'x')

	select-variant 'grek/chi' 0x3C7
	alias 'latinchi' 0xAB53 'grek/chi'

	with-related-glyphs 'cyrl/Hadescender' 0x4B2 'cyrl/Ha' : lambda [src sel] : glyph-proc
		include [refer-glyph src] AS_BASE ALSO_METRICS
		include : CyrDescender RightSB (connex -- 0.5)

	with-related-glyphs 'cyrl/hadescender' 0x4B3 'xCyrlHaDescenderBase' : lambda [src sel] : glyph-proc
		include [refer-glyph src] AS_BASE ALSO_METRICS
		include : CyrDescender RightSB (connex -- 0.5)

	glyph-block-import Letter-Blackboard : BBS BBD
	define [BBXShape l r top] : let [kDiag : DiagCorDs top (r - l) BBD] : union
		intersection
			Rect top 0 (-Width) (Width * 2)
			union
				ExtLineCenter 1 BBS l top (r - kDiag * BBD) 0
				ExtLineCenter 1 BBS (l + kDiag * BBD) top r 0
		HBarTop    l (l + kDiag * BBD) top BBS
		HBarBottom r (r - kDiag * BBD) 0 BBS
		intersection
			Rect top 0 (-Width) (Width * 2)
			difference
				ExtLineCenter 1 BBS l 0 r top
				spiro-outline
					corner l top
					corner (l + kDiag * BBD) top
					corner r 0
					corner (r - kDiag * BBD) 0

	create-glyph 'mathbb/X' 0x1D54F : composite-proc [MarkSet.capital] [BBXShape SB RightSB CAP]
	create-glyph 'mathbb/x' 0x1D569 : composite-proc [MarkSet.e]       [BBXShape SB RightSB XH]
