$$include '../../../meta/macros.ptl'

import [mix linreg clamp fallback] from '../../../support/utils'
import [DesignParameters] from '../../../meta/aesthetics'
import [Dotless CvDecompose] from "../../../support/gr"

glyph-module

glyph-block Letter-Latin-Upper-R : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives
	glyph-block-import Letter-Latin-Upper-P : PShape RevPShape PBarPosY PRotundaShape BBPShape PShapeOutline
	glyph-block-import Letter-Blackboard : BBS BBD

	define LEG-SHAPE-CURLY    0
	define LEG-SHAPE-STRAIGHT 1
	define LEG-SHAPE-STANDING 2

	define [RLegTerminalX legShape right sw] : right - [if legShape (OX * 2) 0] - sw / 2 * HVContrast
	define [RSlabExtraShift slab sw] : if slab (-Jut / 2 + 0.25 * sw * HVContrast) 0
	define [RLegDiagCor top bottom left right extraShift sw] : DiagCor (top - bottom) (right - left - extraShift) 0 sw

	define [RStandingLegFine sw] : sw * CThinB
	define [RStandingLegBend left right top bottom sw] : Math.min ((right - left) * 0.99) : Math.max (sw * 1.5) ((right - left) * 0.875)
	define [RStandingBottomOverflow slab] : if slab 0 (2 * OX)

	define [RLegShape-Straight] : params [top bottom left right charTop slab sw extraShift] : glyph-proc
		local xRightBottom : RLegTerminalX LEG-SHAPE-STRAIGHT right sw
		local shift : RSlabExtraShift slab sw
		include : dispiro
			widths.center (sw * [RLegDiagCor top bottom left right extraShift sw])
			corner (left + extraShift + shift / 2) top [heading Downward]
			corner (xRightBottom + shift) bottom [heading Downward]
		if slab : begin
			include : RightwardBottomSerif (xRightBottom + shift + 0.25 * sw * HVContrast) bottom (SideJut + Jut / 8)

	define [RLegShape-Curly] : params [top bottom left right charTop slab sw extraShift] : glyph-proc
		local xRightBottom : RLegTerminalX LEG-SHAPE-CURLY right sw
		include : intersection
			Rect top bottom (-Width) (2 * Width)
			dispiro
				widths.center sw
				g4.up.start xRightBottom bottom [heading Upward]
				quadcontrols 0 0.4 8
				g4 left top
		if slab : begin
			include : RightwardBottomSerif right bottom (SideJut + Jut / 8)

	define [RLegShape-Standing] : params [top bottom left right charTop slab sw extraShift] : glyph-proc
		local fine : RStandingLegFine sw
		local bend : RStandingLegBend left right top bottom sw
		local ytopRef : begin top
		local yTopStroke : top - sw / 2 + fine
		local yOffset : mix bottom top 0.2
		local xOverflow : RStandingBottomOverflow slab
		include : dispiro
			widths.rhs fine
			flat left yTopStroke [heading Rightward]
			curl (right - bend) yTopStroke [heading Rightward]
			archv
			flat (right + O) [Math.max (yOffset + 1) (ytopRef - bend * charTop / CAP)] [widths.rhs.heading sw Downward]
			curl (right + O) yOffset [heading Downward]
			curl (right + O - xOverflow) bottom  [heading Downward]
		if slab : include : RightwardBottomSerif right bottom SideJut

	define RLegShapes { RLegShape-Curly RLegShape-Straight RLegShape-Standing }

	define [RevRLegTerminalX legShape left sw] : left + [if legShape (OX * 2) 0] + sw / 2 * HVContrast

	define [RevRLegShape-Straight] : params [top bottom left right charTop slab sw extraShift] : glyph-proc
		local xLeftBottom : RevRLegTerminalX LEG-SHAPE-STRAIGHT left sw
		local shift : RSlabExtraShift slab sw
		include : dispiro
			widths.center (sw * [RLegDiagCor top bottom left right extraShift sw])
			corner (right - extraShift - shift / 2) top [heading Downward]
			corner (xLeftBottom - shift) bottom [heading Downward]
		if slab : begin
			include : LeftwardBottomSerif (xLeftBottom - shift - 0.25 * sw * HVContrast) bottom (SideJut + Jut / 8)

	define [RevRLegShape-Curly] : params [top bottom left right charTop slab sw extraShift] : glyph-proc
		local xLeftBottom : RevRLegTerminalX LEG-SHAPE-CURLY left sw
		include : intersection
			Rect top 0 (-Width) (2 * Width)
			dispiro
				widths.center sw
				g4.up.start xLeftBottom bottom [heading Upward]
				quadcontrols 0 0.4 8
				g4 right top
		if slab : begin
			include : LeftwardBottomSerif left bottom (SideJut + Jut / 8)

	define [RevRLegShape-Standing] : params [top bottom left right charTop slab sw extraShift] : glyph-proc
		local fine : RStandingLegFine sw
		local bend : RStandingLegBend left right top bottom sw
		local ytopRef : begin top
		local yTopStroke : top - sw / 2 + fine
		local yOffset : mix bottom top 0.2
		local xOverflow : RStandingBottomOverflow slab
		include : dispiro
			widths.lhs fine
			flat right yTopStroke [heading Leftward]
			curl (left + bend) yTopStroke [heading Leftward]
			archv
			flat (left - O) [Math.max (yOffset + 1) (ytopRef - bend * charTop / CAP)] [widths.lhs.heading sw Downward]
			curl (left - O) yOffset [heading Downward]
			curl (left - O + xOverflow) bottom  [heading Downward]
		if slab : include : LeftwardBottomSerif left bottom SideJut

	define RevRLegShapes { RevRLegShape-Curly RevRLegShape-Straight RevRLegShape-Standing }


	define [RBarPos charTop slab] : begin PShape.BarPos
	define [RLegTop charTop sw bp] : (sw / 2) + [PBarPosY charTop sw bp]

	define [RShape] : params [legShape top bp [mul 1] [slab null]] : glyph-proc
		include : PShape top (mul -- mul) (overshoot -- O) (slab -- slab) (bp -- bp)
		local right : RightSB - O - [if SLAB (Jut / 8) 0]
		include : RLegShapes.(legShape) [RLegTop top Stroke bp] 0 Middle right top (slab === PShape.SlabSymmetric) Stroke 0

	define [RRotundaShape] : params [legShape top [mul 1] [pmRotunda 0] [endX Middle] [hook Hook] [pBar 1]] : glyph-proc
		local bp : pBar * [RBarPos top false]
		local legTop : RLegTop top Stroke (pBar * bp)
		local right (RightSB - O - [if SLAB (Jut / 8) 0])
		local cor : RLegDiagCor legTop 0 endX right 0 Stroke
		local endX1 : endX - [if legShape (HalfStroke * cor * HVContrast) HalfStroke] + [if legShape [RSlabExtraShift SLAB Stroke] 0]
		include : PRotundaShape top (mul -- mul) (bp -- bp) (overshoot -- O) (slab -- false) (endX -- endX1) (hook -- hook)
		include : RLegShapes.(legShape) legTop 0 endX right top SLAB Stroke 0

	define [RevRShape] : params [legShape top bp [slab null] [mul 1] [italicShape false]] : glyph-proc
		include : RevPShape top (mul -- mul) (overshoot -- O) (slab -- slab) (bp -- bp)
		local left : SB + O + [if SLAB (Jut / 8) 0]
		include : RevRLegShapes.(legShape) [RLegTop top Stroke bp] 0 left Middle top SLAB Stroke 0
		if (SLAB && italicShape) : begin
			eject-contour 'serifRT'
			eject-contour 'serifRB'
			include : tagged 'serifRB' : RightwardBottomSerif (Width - SB * mul) 0 SideJut

	define [ROverlayAnchor] : glyph-proc
		set-base-anchor 'overlay'
			SB + Stroke * HVContrast
			PBarPosY CAP Stroke : RBarPos CAP SLAB

	define RConfig : object
		standing                   { LEG-SHAPE-STANDING   [if SLAB PShape.SlabSymmetric]  [if SLAB RevPShape.SlabSymmetric]  false }
		straight                   { LEG-SHAPE-STRAIGHT   [if SLAB PShape.SlabSymmetric]  [if SLAB RevPShape.SlabSymmetric]  false }
		curly                      { LEG-SHAPE-CURLY      [if SLAB PShape.SlabSymmetric]  [if SLAB RevPShape.SlabSymmetric]  false }
		standingOpen               { LEG-SHAPE-STANDING   [if SLAB PShape.SlabSymmetric]  [if SLAB RevPShape.SlabSymmetric]  true  }
		straightOpen               { LEG-SHAPE-STRAIGHT   [if SLAB PShape.SlabSymmetric]  [if SLAB RevPShape.SlabSymmetric]  true  }
		curlyOpen                  { LEG-SHAPE-CURLY      [if SLAB PShape.SlabSymmetric]  [if SLAB RevPShape.SlabSymmetric]  true  }
		standingMotionSerifed      { LEG-SHAPE-STANDING   PShape.SlabMotion               RevPShape.SlabMotion               false }
		straightMotionSerifed      { LEG-SHAPE-STRAIGHT   PShape.SlabMotion               RevPShape.SlabMotion               false }
		curlyMotionSerifed         { LEG-SHAPE-CURLY      PShape.SlabMotion               RevPShape.SlabMotion               false }
		standingOpenMotionSerifed  { LEG-SHAPE-STANDING   PShape.SlabMotion               RevPShape.SlabMotion               true  }
		straightOpenMotionSerifed  { LEG-SHAPE-STRAIGHT   PShape.SlabMotion               RevPShape.SlabMotion               true  }
		curlyOpenMotionSerifed     { LEG-SHAPE-CURLY      PShape.SlabMotion               RevPShape.SlabMotion               true  }

	foreach { suffix { fStraight slabs revSlabs fOpen } } [Object.entries RConfig] : begin
		local fMotion : slabs === PShape.SlabMotion
		local bpCap : RBarPos CAP (slabs === PShape.SlabSymmetric)
		local bpXH  : RBarPos XH  (slabs === PShape.SlabSymmetric)

		create-glyph "R.\(suffix)" : glyph-proc
			include : MarkSet.capital
			include : ROverlayAnchor
			if fOpen
			: then : begin
				include : difference
					RShape fStraight CAP (slab -- slabs) (bp -- bpCap)
					PShape.OpenGap CAP (mul -- 1) (bp -- bpCap)
			: else : begin
				include : RShape fStraight CAP (slab -- slabs) (bp -- bpCap)

		create-glyph "smcpR.\(suffix)" : glyph-proc
			include : MarkSet.capital
			include : ROverlayAnchor
			if fOpen
			: then : begin
				include : difference
					RShape fStraight XH (slab -- slabs) (bp -- bpXH)
					PShape.OpenGap XH (mul -- 1) (bp -- bpXH)
			: else : begin
				include : RShape fStraight XH (slab -- slabs) (bp -- bpXH)

		if (!fMotion && !fOpen) : create-glyph "Rrotunda.\(suffix)" : glyph-proc
			include : MarkSet.capital
			include : RRotundaShape fStraight CAP (hook -- Hook) (pBar -- 0.9)

		if (!fMotion && !fOpen) : create-glyph "rrotunda.\(suffix)" : glyph-proc
			include : MarkSet.capital
			include : RRotundaShape fStraight XH (hook -- AHook) (pBar -- 0.9)

		create-glyph "cyrl/Ya.\(suffix)" : glyph-proc
			include : MarkSet.capital
			include : ROverlayAnchor
			include : difference
				RevRShape fStraight CAP (slab -- revSlabs) (bp -- bpCap)
				if fOpen [RevPShape.OpenGap CAP (mul -- 1) (bp -- bpCap)] [glyph-proc]

		create-glyph "cyrl/ya.\(suffix)" : glyph-proc
			include : MarkSet.capital
			include : ROverlayAnchor
			include : difference
				RevRShape fStraight XH (slab -- revSlabs) (bp -- bpXH) (italicShape -- para.isItalic)
				if fOpen [RevPShape.OpenGap XH (mul -- 1) (bp -- bpXH)] [glyph-proc]


	select-variant 'R' 'R'
	select-variant 'smcpR' 0x280 (follow -- 'R')
	turned 'turnSmapR' 0x1D1A 'smcpR' Middle (XH / 2)

	select-variant 'Rrotunda' 0xA75A
	select-variant 'rrotunda' 0xA75B (follow -- 'Rrotunda')

	select-variant 'cyrl/Ya' 0x42F
	select-variant 'cyrl/ya' 0x44F (follow -- 'cyrl/Ya')
	select-variant 'revSmcpR' 0x1D19 (shapeFrom -- 'cyrl/ya') (follow -- 'R')
	turned 'invSmcpR' 0x281 'revSmcpR' Middle (XH / 2)

	define [IndianRupeeShape suffix legShape] : glyph-proc
		define bp : RBarPos CAP 0
		include : intersection
			Rect CAP 0 SB Width
			WithTransform [Translate (-Width / 8) 0]
				PShape CAP (bp -- bp) (withBar -- false)
		local right : RightSB - O - [if legShape 0 (Width / 16)]
		include : RLegShapes.(legShape) [RLegTop CAP Stroke bp] 0 (Width * 0.375) right CAP false Stroke 0

		define sw : AdviceStroke2 2 4 CAP
		include : HBarTop SB RightSB CAP sw
		include : HBar SB RightSB [mix CAP [PBarPosY CAP Stroke bp] 0.5] sw

	create-glyph 'currency/indianRupeeSign.curly'    : IndianRupeeShape 'curly'    0
	create-glyph 'currency/indianRupeeSign.straight' : IndianRupeeShape 'straight' 1
	create-glyph 'currency/indianRupeeSign.standing' : IndianRupeeShape 'curly'    2

	select-variant 'currency/indianRupeeSign' 0x20B9 (follow -- 'Rrotunda')


	with-related-glyphs 'Rbar' 0x24C 'R' : lambda [src sel] : glyph-proc
		include [refer-glyph src] AS_BASE ALSO_METRICS
		include : HBar [mix 0 SB 0.3] (SB - O) ((CAP - Stroke) * [RBarPos CAP SLAB] + Stroke * 0.25)

	create-glyph : glyph-proc
		include : MarkSet.if
		local top : CAP * 0.85
		local bp 0.45
		include : PShape top (mul -- 1) (bp -- bp) (slab -- [if SLAB PShape.SlabSymmetric])
		include : VBarLeft SB (top - 1) CAP

		local legTop : RLegTop top Stroke bp
		local right (RightSB - O - [if SLAB (Jut / 8) 0])
		create-derived 'Yr.straight'
			RLegShape-Straight legTop Descender Middle right (top - Descender) SLAB Stroke 0
		create-derived 'Yr.curly'
			RLegShape-Curly    legTop Descender Middle right (top - Descender) SLAB Stroke 0
		create-derived 'Yr.standing'
			RLegShape-Standing legTop Descender Middle right (top - Descender) SLAB Stroke 0

	select-variant 'Yr' 0x1A6 (follow -- 'Rrotunda')


	create-glyph 'mathbb/R' 0x211D : glyph-proc
		define [SingleLeg] : RLegShape-Straight ((CAP - BBS) * HBarPos) 0 Middle (RightSB - O) CAP false BBS (BBD / 2)
		define terminalX : RLegTerminalX LEG-SHAPE-STRAIGHT (RightSB - O) BBS
		include : MarkSet.capital
		include : BBPShape (mul -- 1) (overshoot -- O)
		include : difference
			union
				SingleLeg
				new-glyph : glyph-proc
					include : SingleLeg
					include : Translate (0 - BBD) 0
				HBarBottom (terminalX - BBD) (terminalX - BBS / 2) 0 BBS
			PShapeOutline CAP (sw -- BBS) (mul -- 1) (overshoot -- O) (offset -- 1)
