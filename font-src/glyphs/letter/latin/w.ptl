$$include '../../../meta/macros.ptl'

import [mix linreg clamp fallback] from '../../../support/utils'
import [DesignParameters] from '../../../meta/aesthetics'
import [Dotless CvDecompose] from "../../../support/gr"

glyph-module

glyph-block Letter-Latin-W : begin
	glyph-block-import CommonShapes
	glyph-block-import Common-Derivatives
	glyph-block-import Letter-Latin-Upper-M : MShape

	define FORM-CURLY      0
	define FORM-STRAIGHT   1
	define FORM-FLAT-TOP   2
	define FORM-DOUBLE-V   3
	define FORM-ASYMMETRIC 4

	define SERIFS-AUTO       1
	define SERIFS-MOTION     2
	define SERIFS-DOUBLE-V   3
	define SERIFS-ASYMMETRIC 4
	define SERIFS-CURSIVE    5

	define CwFineOuter           5.0
	define CwFineInner           6.0
	define CwFineOuterStraight   5.5
	define CwFineInnerStraight   5.5
	define CwFineInnerDoubleV    4.25
	define CwOuterStrokeStraight 3.0
	define CwDoubleV             3.625
	define CwWCoCurly            5.0
	define CwWCoStraight         3.5
	define CwWCoDoubleV          3.5
	define PXBot1Curly           0.25
	define PXBot1Straight        0.235

	define [WDim df top bodyType] : begin
		local strokeOuter : match bodyType
			[Just FORM-CURLY]      : AdviceStroke CwOuterStrokeStraight [Math.sqrt df.div]
			[Just FORM-STRAIGHT]   : AdviceStroke CwOuterStrokeStraight [Math.sqrt df.div]
			[Just FORM-FLAT-TOP]   : AdviceStroke CwOuterStrokeStraight [Math.sqrt df.div]
			[Just FORM-DOUBLE-V]   : AdviceStroke CwDoubleV [Math.sqrt df.div]
			[Just FORM-ASYMMETRIC] : AdviceStroke CwDoubleV [Math.sqrt df.div]
		local fineOuter : match bodyType
			[Just FORM-CURLY]      : AdviceStroke CwFineOuter df.div
			[Just FORM-STRAIGHT]   : AdviceStroke CwFineOuterStraight [Math.sqrt df.div]
			[Just FORM-FLAT-TOP]   : AdviceStroke CwFineOuterStraight [Math.sqrt df.div]
			[Just FORM-DOUBLE-V]   : AdviceStroke CwDoubleV [Math.sqrt df.div]
			[Just FORM-ASYMMETRIC] : AdviceStroke CwDoubleV [Math.sqrt df.div]
		local fineInner : match bodyType
			[Just FORM-CURLY]      : AdviceStroke CwFineInner          df.div
			[Just FORM-STRAIGHT]   : AdviceStroke CwFineInnerStraight [Math.sqrt df.div]
			[Just FORM-FLAT-TOP]   : AdviceStroke CwFineInnerStraight [Math.sqrt df.div]
			[Just FORM-DOUBLE-V]   : AdviceStroke CwFineInnerDoubleV  [Math.sqrt df.div]
			[Just FORM-ASYMMETRIC] : AdviceStroke CwDoubleV  [Math.sqrt df.div]
		local fineHeight : (fineOuter + fineInner) / 2

		local wCo : match bodyType
			[Just FORM-CURLY]      : AdviceStroke CwWCoCurly    df.div
			[Just FORM-STRAIGHT]   : AdviceStroke CwWCoStraight df.div
			[Just FORM-FLAT-TOP]   : AdviceStroke CwWCoStraight df.div
			[Just FORM-DOUBLE-V]   : AdviceStroke CwWCoDoubleV df.div
			[Just FORM-ASYMMETRIC] : AdviceStroke CwWCoDoubleV df.div
		local kWCoShrink CThin
		local pxBot1 : match bodyType
			[Just FORM-CURLY]        0.25
			[Just FORM-STRAIGHT]     0.235
			[Just FORM-FLAT-TOP]     0.235
			[Just FORM-DOUBLE-V]     0.315
			[Just FORM-ASYMMETRIC]   0.3

		local botMixOffset : 0.5 * HVContrast * wCo
		local xBot1 : [if bodyType O 0] + [mix (df.leftSB + botMixOffset) (df.rightSB - botMixOffset) pxBot1]
		local xBot2 : df.width - xBot1
		local topWithoutSerif : if SLAB (top - Stroke) top
		local wMidHeight : match bodyType
			[Just FORM-CURLY]      : top * 0.6
			[Just FORM-STRAIGHT]   : topWithoutSerif * 0.72
			[Just FORM-FLAT-TOP]     topWithoutSerif
			[Just FORM-DOUBLE-V]     top
			[Just FORM-ASYMMETRIC]   top

		local xRight1 : match bodyType
			([Just FORM-DOUBLE-V] || [Just FORM-ASYMMETRIC]) : mix (df.leftSB + botMixOffset) (df.rightSB - botMixOffset) (2 * pxBot1)
			__                                             : if (bodyType < FORM-DOUBLE-V) df.middle
		local xLeft2  : df.width - xRight1

		local corWCo : DiagCorDs wMidHeight (xRight1 - xBot1) wCo
		local wCoCr : corWCo * wCo

		local corFOuter : DiagCorDs top (xBot1 - df.leftSB) fineOuter
		local fineOuterCr : Math.min (kWCoShrink * wCoCr) : corFOuter * fineOuter
		local strokeOuterCr : corFOuter * strokeOuter

		local corFInner : DiagCorDs wMidHeight (xRight1 - xBot1) fineInner
		local fineInnerCr : Math.min wCoCr : corFInner * fineInner
		local fineInnerCrTop : if (bodyType === FORM-DOUBLE-V || bodyType === FORM-ASYMMETRIC) fineInnerCr (kWCoShrink * wCoCr)

		return : object xBot1 xBot2 strokeOuter strokeOuterCr fineOuter fineOuterCr fineInner fineInnerCr fineInnerCrTop fineHeight wMidHeight xRight1 xLeft2 wCoCr

	### W and w
	define [WshapeImpl df top bodyType slabType] : glyph-proc
		local dim : WDim df top bodyType

		if bodyType : begin
			include : tagged 'strokeDown1' : dispiro
				widths.lhs dim.strokeOuterCr
				flat (df.leftSB + O * 2) top [heading Downward]
				g4   (dim.xBot1 - 0.5 * HVContrast * dim.wCoCr) 0 [widths.lhs.heading dim.fineOuterCr Downward]
		: else : begin
			include : tagged 'strokeDown1' : dispiro
				widths.lhs dim.strokeOuter
				flat df.leftSB top [heading Downward]
				curl df.leftSB (top * 0.75) [heading Downward]
				quadcontrols 0 0.3 6 unimportant
				g4   (dim.xBot1 - 0.5 * HVContrast * dim.wCoCr) 0 [widths.lhs.heading dim.fineOuterCr Downward]

		local kTopShift : if (bodyType === FORM-DOUBLE-V || bodyType === FORM-ASYMMETRIC) 0 1
		local k1 0.5
		local k2 : 1 - k1
		local shiftT  : kTopShift * HVContrast * (0.5 * dim.wCoCr - 0.5 * dim.fineInnerCrTop)
		local shiftB  :             HVContrast * (0.5 * dim.wCoCr - 0.5 * dim.fineOuterCr   )

		include : tagged 'strokeUp1' : difference
			dispiro
				flat (dim.xRight1 - shiftT) dim.wMidHeight [widths.center.heading dim.fineInnerCrTop Downward]
				curl (dim.xBot1 + shiftB)   0              [widths.center.heading dim.fineOuterCr    Downward]
			if (bodyType !== FORM-ASYMMETRIC) [glyph-proc] : spiro-outline
				corner dim.xLeft2 dim.wMidHeight
				corner dim.xBot2  0
				corner df.width  0
				corner df.width  dim.wMidHeight

		include : tagged 'strokeDown2' : dispiro
			flat (dim.xLeft2 + shiftT) dim.wMidHeight [widths.center.heading dim.fineInnerCrTop Downward]
			curl (dim.xBot2 - shiftB)   0             [widths.center.heading dim.fineOuterCr    Downward]

		if bodyType : begin
			include : tagged 'strokeUp2' : dispiro
				widths.rhs dim.strokeOuterCr
				flat (df.rightSB - O * 2) top [heading Downward]
				g4   (dim.xBot2 + 0.5 * HVContrast * dim.wCoCr) 0 [widths.rhs.heading dim.fineOuterCr Downward]
		: else : begin
			include : tagged 'strokeUp2' : dispiro
				widths.rhs dim.strokeOuter
				flat df.rightSB top [heading Downward]
				curl df.rightSB (top * 0.75) [heading Downward]
				quadcontrols 0 0.3 6 unimportant
				g4   (dim.xBot2 + 0.5 * HVContrast * dim.wCoCr) 0 [widths.rhs.heading dim.fineOuterCr Downward]

		local oSlabPos : if bodyType (O * 2) 0

		include : match slabType
			[Just SERIFS-AUTO]   : NeedSlab SLAB : VSerifs top df.leftSB df.rightSB
			[Just SERIFS-MOTION] : tagged 'serifLT' : LeftwardTopSerif (df.leftSB + oSlabPos) top SideJut
			[Just SERIFS-CURSIVE] : glyph-proc
				if SLAB : include : tagged 'serifLT' : LeftwardTopSerif (df.leftSB + oSlabPos) top SideJut
			[Just SERIFS-DOUBLE-V] : glyph-proc
				if SLAB : include : tagged 'serifLT' : LeftwardTopSerif  (df.leftSB + oSlabPos) top SideJut
				if SLAB : include : tagged 'serifRT' : RightwardTopSerif (df.rightSB - oSlabPos) top SideJut
			[Just SERIFS-ASYMMETRIC] : glyph-proc
				include : NeedSlab SLAB : VSerifs top df.leftSB df.rightSB
				eject-contour 'serifLT'
				if SLAB : include : tagged 'serifLT' : LeftwardTopSerif  (df.leftSB + oSlabPos) top SideJut

	define [WCursiveImpl df top bodyType slabType] : glyph-proc
		local fine : AdviceStroke 3.25 df.div
		local mfine : fine * CThin
		local x0 : mix df.leftSB df.rightSB 0.1
		local y0 : top - O
		local y1 : mix 0 top 0.8
		local x1 : df.leftSB + 0 * OX
		local y3 : top / 2
		local y4 : top - [Math.max (top * 0.1) (1.125 * [AdviceStroke 3.5])]

		include : dispiro
			widths.lhs fine
			flat x1 top [heading Downward]
			curl x1 (top / 2)
			arcvh 8
			g4 [mix x1 (df.middle + fine / 2 * HVContrast) 0.5] O [heading Rightward]
			archv 8
			flat (df.middle + (mfine - fine / 2) * HVContrast) y3 [widths.heading mfine 0 Upward]
			curl (df.middle + (mfine - fine / 2) * HVContrast) y4 [heading Upward]
		include : dispiro
			widths.rhs fine
			g4 (df.width - x0) y0
			g4 (df.width - x1 - OX) (top / 2)
			arcvh 8
			g4 [mix (df.width - x1) (df.middle - fine / 2 * HVContrast) 0.5] O [heading Leftward]
			archv 8
			flat (df.middle - (mfine - fine / 2) * HVContrast) y3 [widths.heading 0 mfine Upward]
			curl (df.middle - (mfine - fine / 2) * HVContrast) y4 [heading Upward]

		include : match slabType
			[Just SERIFS-CURSIVE] : if SLAB
				tagged 'serifLT' : LeftwardTopSerif x1 top SideJut
				glyph-proc
			[Just SERIFS-MOTION]  : tagged 'serifLT' : LeftwardTopSerif x1 top SideJut
			___                   : glyph-proc

	define [WVertSides df top bodyType slabType] : glyph-proc
		include : MShape top df (form -- 2) (slabType -- 0)
		include : FlipAround (df.width / 2) (top / 2)
		include : match slabType
			[Just SERIFS-AUTO]   : NeedSlab SLAB : VSerifs top df.leftSB df.rightSB
			[Just SERIFS-MOTION] : tagged 'serifLT' : LeftwardTopSerif df.leftSB top SideJut
			___                  : glyph-proc

	define WConfig : object
		straightAsymmetric                 { WshapeImpl   FORM-ASYMMETRIC   SERIFS-ASYMMETRIC }
		straightDoubleV                    { WshapeImpl   FORM-DOUBLE-V     SERIFS-DOUBLE-V   }
		straightFlatTop                    { WshapeImpl   FORM-FLAT-TOP     SERIFS-AUTO       }
		straight                           { WshapeImpl   FORM-STRAIGHT     SERIFS-AUTO       }
		straightVerticalSides              { WVertSides   FORM-STRAIGHT     SERIFS-AUTO       }
		curly                              { WshapeImpl   FORM-CURLY        SERIFS-AUTO       }
		cursive                            { WCursiveImpl FORM-CURLY        SERIFS-CURSIVE    }
		straightAsymmetricMotionSerifed    { WshapeImpl   FORM-ASYMMETRIC   SERIFS-MOTION     }
		straightDoubleVMotionSerifed       { WshapeImpl   FORM-DOUBLE-V     SERIFS-MOTION     }
		straightFlatTopMotionSerifed       { WshapeImpl   FORM-FLAT-TOP     SERIFS-MOTION     }
		straightVerticalSidesMotionSerifed { WVertSides   FORM-STRAIGHT     SERIFS-MOTION     }
		straightMotionSerifed              { WshapeImpl   FORM-STRAIGHT     SERIFS-MOTION     }
		curlyMotionSerifed                 { WshapeImpl   FORM-CURLY        SERIFS-MOTION     }
		cursiveMotionSerifed               { WCursiveImpl FORM-CURLY        SERIFS-MOTION     }

	foreach { suffix { implT bodyType slabType } } [Object.entries WConfig] : do
		create-glyph "W.\(suffix)" : glyph-proc
			local df : DivFrame para.diversityM 3
			set-width df.width
			include : df.markSet.capital
			include : implT df CAP bodyType slabType

		create-glyph "w.\(suffix)" : glyph-proc
			local df : DivFrame para.diversityM 3
			set-width df.width
			include : df.markSet.e
			include : implT df XH bodyType slabType

		create-glyph "ww.\(suffix)": glyph-proc
			local df : DivFrame 1 3
			set-width df.width
			include : df.markSet.capital
			include : implT df (CAP / 2) bodyType slabType
			include : ApparentTranslate 0 (CAP / 2)
			include : implT df (CAP / 2) bodyType slabType

	define [WHooktopShape df top bodyType] : glyph-proc
		include : WshapeImpl df top bodyType SERIFS-AUTO

		local dim : WDim df top bodyType

		# Eject unnecessary contours
		eject-contour 'strokeUp2'
		if SLAB : eject-contour 'serifRT'

		include : dispiro
			widths.rhs dim.strokeOuter
			straight.down.start df.rightSB (top - Hook) [heading Downward]
			quadcontrols 0 0.3 6 unimportant
			g4 (dim.xBot2 + 0.5 * HVContrast * dim.wCoCr) 0 [widths.rhs.heading dim.fineOuterCr Downward]

		include : VerticalHook (df.rightSB - HalfStroke * HVContrast) (top - Hook) (HookX * 0.65) (-Hook + HalfStroke)

	create-glyph : glyph-proc
		local df : DivFrame para.diversityM 3
		set-width df.width
		include : df.markSet.capital
		create-derived 'Whooktop.straightFlatTop' : WHooktopShape df CAP FORM-FLAT-TOP
		create-derived 'Whooktop.straight'        : WHooktopShape df CAP FORM-STRAIGHT
		create-derived 'Whooktop.curly'           : WHooktopShape df CAP FORM-CURLY

	create-glyph : glyph-proc
		local df : DivFrame para.diversityM 3
		set-width df.width
		include : df.markSet.e
		create-derived 'whooktop.straightFlatTop' : WHooktopShape df XH FORM-FLAT-TOP
		create-derived 'whooktop.straight'        : WHooktopShape df XH FORM-STRAIGHT
		create-derived 'whooktop.curly'           : WHooktopShape df XH FORM-CURLY

	select-variant 'W' 'W'
	select-variant 'Whooktop' 0x2C72
	select-variant 'w' 'w'
	select-variant 'smcpW' 0x1D21 (shapeFrom -- 'w') (follow -- 'W')
	select-variant 'ww' 0x2AC (follow -- 'w')
	select-variant 'whooktop' 0x2C73
	alias 'cyrl/We' 0x51C 'W'
	alias 'cyrl/we' 0x51D 'w'
	turned 'turnw' 0x28D 'w' (Middle * para.diversityM) (XH / 2)

	define [WonOverlay df] : glyph-proc
		include : HOverlayBar [mix SB 0 0.7] [mix df.rightSB df.width 0.7] (CAP * 0.6)
		include : HOverlayBar [mix SB 0 0.7] [mix df.rightSB df.width 0.7] (CAP * 0.35)

	with-related-glyphs 'symbols/currency/wonSign' 0x20A9 'W' : lambda [src sel] : glyph-proc
		local df : DivFrame para.diversityM 3
		include [refer-glyph src] AS_BASE ALSO_METRICS
		include [WonOverlay df]

	glyph-block-import Letter-Blackboard : BBS BBD
	glyph-block-import Letter-Latin-V : BBVShape BBVOuterMaskShape

	define [BBWShape top] : begin
		local offset : BBD * 2
		local ksW : [AdviceStroke2 8 2 top] / BBS
		local kdW : Math.min ksW (3 / 4)
		return : union
			difference
				BBVShape SB (RightSB - offset) kdW ksW top
				BBVOuterMaskShape (SB + offset) RightSB kdW ksW top
			BBVShape (SB + offset) RightSB kdW ksW top

	create-glyph 'mathbb/W' 0x1D54E : composite-proc [MarkSet.capital] [BBWShape CAP]
	create-glyph 'mathbb/w' 0x1D568 : composite-proc [MarkSet.e]       [BBWShape XH]