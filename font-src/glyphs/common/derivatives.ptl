$$include '../../meta/macros.ptl'

import [mix linreg clamp fallback] from"../../support/utils.mjs"
import [Cv AnyCv AnyDerivingCv Dotless SvInheritableRelations CvDecompose PseudoCvDecompose getGrMesh] from"../../support/gr.mjs"

extern Map

glyph-module

glyph-block Common-Derivatives : begin
	glyph-block-export ApplyCv
	define [ApplyCv g shapeFrom follow para] : begin
		foreach { kPrime prime } para.variants.primes : foreach h [prime.variants.values] : begin
			local suffix : h.resolveFor para follow
			if suffix : begin
				local dstName : shapeFrom + '.' + suffix
				local dst : glyphStore.queryByName dstName
				if dst : g.dependsOn dst
				if (para.enableCvSs && h.tag && h.rank) : begin
					[Cv h.tag h.rank].set g dstName
					if h.nonDeriving : [Cv h.tag h.rank].setPreventDeriving g

	glyph-block-export select-variant
	define [select-variant] : params [name unicode [shapeFrom name] [follow name] [reduction null]] : begin
		if [not : glyph-is-needed name] : return nothing

		local variant para.variantSelector.(follow)
		if reduction : set variant : reduction.(variant) || variant
		if [not variant] : throw : new Error "Variant for \(name) is not assigned."

		create-glyph name unicode : glyph-proc
			include [refer-glyph (shapeFrom + '.' + variant)] AS_BASE ALSO_METRICS
			ApplyCv currentGlyph shapeFrom follow para
			currentGlyph.cloneRankFromGlyph [query-glyph (shapeFrom + '.' + variant)]
			foreach gr [items-of SvInheritableRelations] : begin
				local v : gr.get [query-glyph (shapeFrom + '.' + variant)]
				if v : gr.set this v

	glyph-block-export orthographic-italic
	define [orthographic-italic name unicode] : if para.isItalic
		alias name unicode (name + '.italic')
		alias name unicode (name + '.upright')

	glyph-block-export orthographic-slanted
	define [orthographic-slanted name unicode] : if para.slopeAngle
		alias name unicode (name + '.slanted')
		alias name unicode (name + '.upright')

	glyph-block-export glyph-is-needed
	define [glyph-is-needed name] : [not pickHash] || pickHash.(name)

	glyph-block-export query-glyph
	define [query-glyph id] : return : glyphStore.queryByName id

	glyph-block-export refer-glyph
	define [refer-glyph id] : lambda [copyAnchors copyWidth] : begin
		local goal : query-glyph id
		if [not goal] : throw : new Error "Cannot find glyph '\(id)'"
		this.includeGlyph goal copyAnchors copyWidth

	glyph-block-export DeriveMeshT
	define [DeriveMeshT gnSources Query Fn FnLink] : begin
		local linksGnMap : new Map

		# Build principal
		local gndSource : Fn gnSources null null
		linksGnMap.set [keyOfGnList gnSources] gndSource

		local mesh : getGrMesh gnSources Query query-glyph

		# Build related derivatives
		foreach { gr from to } [items-of mesh] : begin
			local key : keyOfGnList to
			if [not : linksGnMap.has key] : begin
				local gnDerivedTo : Fn to gr from
				linksGnMap.set key gnDerivedTo

		# Link related derivatives
		foreach { gr from to } [items-of mesh] : begin
			local gndFrom : linksGnMap.get [keyOfGnList from]
			local gndTo   : linksGnMap.get [keyOfGnList to]
			if (gndFrom && gndTo && [query-glyph gndFrom] && [query-glyph gndTo]) : begin
				gr.set [query-glyph gndFrom] gndTo
				if FnLink : FnLink gndFrom gndTo from to

		return gndSource
	define [keyOfGnList gns] ".deriveKey{\[gns.join '}{']}"

	glyph-block-export derive-glyphs
	define [derive-glyphs dstGid dstUnicode srcGid Fn] : begin
		if [not : glyph-is-needed dstGid] : return nothing

		DeriveMeshT { srcGid } AnyDerivingCv : function [gns gr] : begin
			local gn gns.0
			local gnDst : if [not gr] dstGid "\(dstGid)/derive{\(gn)}"
			local unicodeDst : if [not gr] dstUnicode null
			create-glyph gnDst unicodeDst : Fn gn gr
			return gnDst

	glyph-block-export derive-multi-part-glyphs
	define [derive-multi-part-glyphs dstGid dstUnicode gnSoruces Fn] : begin
		if [not : glyph-is-needed dstGid] : return nothing

		DeriveMeshT gnSoruces AnyDerivingCv : function [gns gr] : begin
			local gnDst : if [not gr] dstGid "\(dstGid)/derive{\[gns.join '}{']}"
			local unicodeDst : if [not gr] dstUnicode null
			create-glyph gnDst unicodeDst : Fn gns gr
			return gnDst

	glyph-block-export alias
	define [alias dstGid dstUnicode srcGid] : begin
		if [not dstGid] : throw : new Error "Target ID not specified"
		if [not : glyph-is-needed srcGid] : return nothing

		local glyphSrc : glyphStore.queryByNameEnsured srcGid
		local glyphDst : create-glyph dstGid dstUnicode : glyph-proc
			include [refer-glyph srcGid] AS_BASE ALSO_METRICS

		if glyphDst : begin
			foreach [gr : items-of : AnyCv.query glyphSrc] : begin
				gr.set glyphDst [gr.get glyphSrc]
				if [gr.getPreventDeriving glyphSrc] : gr.setPreventDeriving glyphDst
			if [Dotless.get glyphSrc] : Dotless.set glyphDst [Dotless.get glyphSrc]

	glyph-block-export turned
	define [turned newid unicode id _x _y mark] : begin
		if [not newid] : throw : new Error "Target ID not specified"
		derive-glyphs newid unicode id
			lambda [src gr] : glyph-proc
				include [refer-glyph src] [if mark false AS_BASE] ALSO_METRICS
				if mark : include mark
				define x : if (_x <@ Function) [_x.call currentGlyph] _x
				define y : if (_y <@ Function) [_y.call currentGlyph] _y

				# This is [FlipAround], but we cannot use it because common-shapes depends on this
				# glyph block
				include [this.gizmo.inverse]
				include : Translate (-x) (-y)
				include : Scale (-1) (-1)
				include : Translate x y
				include this.gizmo

	glyph-block-export link-reduced-variant
	define [link-reduced-variant] : params [dstGid srcGid gr follow] : begin
		if [not : query-glyph dstGid] : select-variant dstGid (shapeFrom -- srcGid) (follow -- [fallback follow dstGid])
		if (gr && [query-glyph srcGid]) : gr.set [query-glyph srcGid] dstGid

	glyph-block-export alias-reduced-variant
	define [alias-reduced-variant dstGid srcGid shapeGid gr] : begin
		alias dstGid null shapeGid
		if (gr && [query-glyph srcGid]) : gr.set [query-glyph srcGid] dstGid

	glyph-block-export HalfAdvance
	define [HalfAdvance] : this.advanceWidth / 2

	glyph-block-export TurnMarks
	define [TurnMarks k fnOverride] : glyph-proc
		local df : DivFrame (currentGlyph.advanceWidth / Width)
		local mx : compositeBaseAnchors [df.markSet.(k)]
			if fnOverride [fnOverride df] {.baseAnchors {.}}
		include mx

	# Dual derivatives
	glyph-block-export HCombine
	define [HCombine newid unicode id1 id2 spacing] : begin
		if [not newid] : throw : new Error "Target ID not specified"
		create-glyph newid unicode : glyph-proc
			include [refer-glyph id1] AS_BASE ALSO_METRICS
			include : Translate (-spacing) 0
			include [refer-glyph id2]
			include : Translate (spacing / 2) 0

	glyph-block-export HDual
	define [HDual newid unicode id spacing] : begin
		if [not newid] : throw : new Error "Target ID not specified"
		return : HCombine newid unicode id id spacing

	glyph-block-export VCombine
	define [VCombine newid unicode id1 id2 spacing] : begin
		if [not newid] : throw : new Error "Target ID not specified"
		create-glyph newid unicode : glyph-proc
			include [refer-glyph id2] AS_BASE ALSO_METRICS
			include : Translate (-spacing * TanSlope) (-spacing)
			include [refer-glyph id1]
			include : Translate (spacing * TanSlope / 2) (spacing / 2)

	glyph-block-export VDual
	define [VDual newid unicode id spacing] : begin
		if [not newid] : throw : new Error "Target ID not specified"
		return : VCombine newid unicode id id spacing

	glyph-block-export derive-composites
	define [derive-composites dstGid dstUnicode srcGid] : begin
		if [not : glyph-is-needed dstGid] : return nothing
		local components : {}.slice.call arguments 2
		local refs {}
		foreach part [items-of components] : if ([typeof part] === 'string')
			then : refs.push part

		return : derive-multi-part-glyphs dstGid dstUnicode refs : function [srcs] : glyph-proc
			local rid 0
			foreach j [range 0 components.length] : begin
				local asBase      : if (j === 0) AS_BASE      nothing
				local alsoMetrics : if (j === 0) ALSO_METRICS nothing
				local part components.(j)

				if ([typeof part] === 'string')
				: then : begin
					include [refer-glyph srcs.(rid)] asBase alsoMetrics
					set rid : rid + 1
				: else : include part asBase alsoMetrics

glyph-block Recursive-Build : begin
	define [Fork gs ps] : begin
		# BFS construct ShouldBuildList
		local sbh : new Map
		local PENDING 1
		local CHECKED 2
		foreach [glyphid : items-of gs] : sbh.set glyphid PENDING

		local found true
		while found : begin
			set found false
			foreach glyphid [sbh.keys] : if ([sbh.get glyphid] === PENDING) : begin
				sbh.set glyphid CHECKED
				local g : glyphStore.queryByName glyphid
				if (g && g.dependencies) : foreach [k : items-of g.dependencies] : begin
					if [not : sbh.has k] : begin
						sbh.set k PENDING
						set found true

		local shouldBuildList     { }
		local shouldBuildUnicodes { }
		foreach gn [sbh.keys] : if gn : begin
			shouldBuildList.push gn
			if [glyphStore.queryUnicodeOfName gn] : begin
				shouldBuildUnicodes.push [glyphStore.queryUnicodeArrayOfName gn].0

		local p {.}
		foreach {k v} [pairs-of all ps] : set p.(k) v

		local gs : buildGlyphs p shouldBuildList shouldBuildUnicodes
		return gs.glyphStore

	define [MiniatureParaT] : params [pp crowd scale [sbscale (Width / UPM)] [mono false] [mono2 false] [forceUpright false]] : begin
		local forkedPara : Object.assign {.} pp
		forkedPara.stroke = [AdviceStroke crowd : Math.pow [clamp 0 1 (HalfUPM / Width)] 0.5] / scale
		forkedPara.dotSize = pp.dotSize * forkedPara.stroke / pp.stroke
		forkedPara.periodSize = pp.periodSize * forkedPara.stroke / pp.stroke
		forkedPara.sb = SB * sbscale
		if forceUpright : begin
			forkedPara.slopeAngle  = 0
		if mono : begin
			forkedPara.diversityM  = 1
		if mono2 : begin
			forkedPara.diversityF  = 1
			forkedPara.diversityI  = 1
			forkedPara.diversityII = 1

		return forkedPara

	define [Miniature glyphs] : begin
		return : Fork glyphs : MiniatureParaT.apply null { para :: [{}.slice.call arguments 1] }

	define [Thinner glyphs p fMono] : begin
		local forkedPara : Object.assign {.} para
		forkedPara.unitWidthScalar = para.unitWidthScalar * p
		forkedPara.width = Width * p
		forkedPara.accentWidth = AccentWidth * p
		forkedPara.jut = Jut * p
		forkedPara.longjut = LongJut * p
		forkedPara.diversityM = 1
		if fMono : begin
			forkedPara.diversityF  = 1
			forkedPara.diversityI  = 1
			forkedPara.diversityII = 1
		#forkedPara.hookx = HookX * p
		return : Fork glyphs forkedPara

	define [Widen glyphs p psb] : begin
		local forkedPara : Object.assign {.} para
		forkedPara.unitWidthScalar = para.unitWidthScalar * p
		forkedPara.width = Width * p
		forkedPara.sb = SB * [fallback psb p]
		forkedPara.accentWidth = AccentWidth * p
		forkedPara.jut = Jut * p
		forkedPara.longjut = LongJut * p
		forkedPara.hookx = HookX * p
		return : Fork glyphs forkedPara

	glyph-block-export Fork Miniature MiniatureParaT Thinner Widen
