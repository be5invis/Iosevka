import [add-common-feature add-feature ChainRuleBuilder query-related-glyphs BeginLookupBlock EndLookupBlock] from "./table-util"

extern Set

define-operator "~>" 880 'right' : syntax-rules
	`(@l ~> @r) `{.left @l .right @r}

define [lsx s] : lambda [t] : t.map : lambda [x] "\(x).lig.\(s)"
define [csx s] : lambda [t] : t.map : lambda [x] "\(x).\(s)"
define [just s] : lambda [t] : t.map : lambda [x] s
define preserved null
define advance : lambda [t] : t.map : lambda [x] x

export : define [buildLigations sink para plm glyphs] : begin
	local rec : BeginLookupBlock sink
	local rankedLookups {}
	foreach [ {featureName mappedFeature} : pairs-of plm] : buildLigationsImpl sink para glyphs featureName mappedFeature rankedLookups
	EndLookupBlock rec sink

define [buildLigationsImpl sink para glyphs featureName mappedFeature rankedLookups] : begin
	define {chain-rule reverse-rule} : ChainRuleBuilder sink

	define arrowStick  {'hyphen' 'equal'}
	define [stickClass style] : [lsx style] {'hyphen' 'equal'}
	define [stickTf style] : {'hyphen' 'equal'} ~> [lsx style]
	define anyWave {'asciitilde' 'asciitilde.low' 'asciitilde.high'}
	define [waveClass cl] : [just ('wave.' + cl)] anyWave
	define [waveTf cl] : anyWave ~> [waveClass cl]

	define [anyStartingWith names] : begin
		local result {}
		foreach [name : items-of names] : begin
			local re : new RegExp "^\(name)(?:\.|$)"
			foreach [name : items-of : Object.keys glyphs]
				if [re.test name] : result.push name
		return result

	define less {'less'}
	define lessAndEquiv {'less' 'less.lig.shift0' 'less.lig.shift0.anti'}
	define hyphen {'hyphen'}
	define equal {'equal'}
	define exclam {'exclam'}
	define greater {'greater'}
	define greaterAndEquiv {'greater' 'greater.lig.shift0' 'greater.lig.shift0.anti'}
	define anyBar {'bar' 'slash' 'backslash'}
	define slash {'slash'}
	define backslash {'backslash'}
	define bar {'bar'}
	define regexLookAround [less.concat hyphen equal exclam greater anyBar]

	define alphabet : query-related-glyphs glyphs para {
		'a' 'b' 'c' 'd' 'e' 'f' 'g' 'h' 'i' 'j' 'k' 'l' 'm'
		'n' 'o' 'p' 'q' 'r' 's' 't' 'u' 'v' 'w' 'x' 'y' 'z'
		'A' 'B' 'C' 'D' 'E' 'F' 'G' 'H' 'I' 'J' 'K' 'L' 'M'
		'N' 'O' 'P' 'Q' 'R' 'S' 'T' 'U' 'V' 'W' 'X' 'Y' 'Z'
		'zero.lnum' 'zero.onum' 'one.lnum' 'one.onum'
		'two.lnum' 'two.onum' 'three.lnum' 'three.onum'
		'four.lnum' 'four.onum' 'five.lnum' 'five.onum'
		'six.lnum' 'six.onum' 'seven.lnum' 'seven.onum'
		'eight.lnum' 'eight.onum' 'nine.lnum' 'nine.onum'
	}

	define [acops]  {'less' 'greater' 'hyphen' 'equal' 'plus'}
	define [acskip] {
		'slash' 'bar' 'at' 'ampersand' 'percent'
		'numbersign' 'numbersign.upright' 'numbersign.slanted'
	}
	define dottyPunctuations : list
		* "colon"
		* "colon.mid"
		* "semicolon"
		* "period"
		* "period.mid"
		* "comma"
		* "question"
		* "exclam"
	define widePunctuation : list
		* 'parenLeft'
		* 'parenRight'
		* 'bracketLeft'
		* 'bracketRight'
		* 'braceLeft'
		* 'braceRight'
		* 'equal'
		* 'hyphen'
		* 'plus'
		* 'less'
		* 'greater'
		* 'numbersign'
		* 'slash'
		* 'ampersand'
		* 'asterisk'
		* 'asciicaret'
		* 'asciitilde'

	define [asterisk_center]  ({'asterisk'}          ~> {'asterisk.dfltlow'})
	define [colon_center]     ({'colon' 'colon.mid'} ~> {'colon.mid' 'colon.mid'})
	define [caret_center]     ({'asciicaret'}        ~> {'asciicaret.low'})
	define [tilde_center]     ({'asciitilde'}        ~> {'asciitilde.low'})
	define [period_center]    ({'period'}            ~> {'period.mid'})
	define [no_period_center] ({'period.mid'}     ~> {'period'})

	local ligationLookupName : 'lig_' + featureName + '{' + mappedFeature + '}'

	define [hasLG ln] : [mappedFeature.indexOf ln] >= 0

	define [only ln f g] : lambda [left] : begin
		if [not : hasLG ln]
		: then : begin
			if g
			: then : piecewise
				(g <@ Function) : g left
				true : return g
			: else : return null
		: else : piecewise
			(f <@ Function) : f left
			true : return f

	define feature : add-common-feature sink : add-feature sink featureName

	local lastLookupName null
	local lookupRank 0
	local [dedup ln0 rank obj] : begin
		local h : JSON.stringify obj
		foreach [{name lookup} : items-of rankedLookups.(rank)] : begin
			local h1 : JSON.stringify lookup
			if (h == h1) : return {name false}
		return {ln0 true}

	local [AddRankedLookup obj] : begin
		if [not rankedLookups.(lookupRank)] : set rankedLookups.(lookupRank) {}
		local sameRankLookupBlob rankedLookups.(lookupRank)

		local {ln unique} : dedup (ligationLookupName + feature.lookups.length) lookupRank obj
		if [not sink.lookups.(ln)] : set sink.lookups.(ln) obj
		feature.lookups.push ln

		# In-feature ordering
		if lastLookupName : sink.lookupDep.push {lastLookupName ln}
		set lastLookupName ln

		# Cross-feature ordering
		if unique : begin
			if sameRankLookupBlob.length : begin
				local lastSameRank (sameRankLookupBlob.(sameRankLookupBlob.length - 1).0)
				if (lastSameRank != ln) : sink.lookupDep.push {lastSameRank ln}
			sameRankLookupBlob.push {ln obj}

	define [keep-if lg x] : if [hasLG lg] x null

	define [CreateLigationLookup rules] : begin
		if (rules && rules.length) : AddRankedLookup
			.type 'gsub_chaining'
			.subtables rules
		set lookupRank : lookupRank + 1

	define [CreateReverseLigationLookup rules] : begin
		if (rules && rules.length) : AddRankedLookup
			.type 'gsub_reverse'
			.subtables rules
		set lookupRank : lookupRank + 1

	do "Operator centering"
		define centerizeGroups : { asterisk_center caret_center tilde_center colon_center [if [hasLG 'dotoper'] period_center nothing]}.filter (x => x)

		do "Bracket-star"
			CreateLigationLookup : keep-if 'center-ops' : list
				chain-rule # (*
					{'parenLeft'}  ~> preserved
					{'asterisk'}   ~> [only 'brst' {'asterisk.dfltlow'}]
				chain-rule # *)
					{'asterisk'}   ~> [only 'brst' {'asterisk.dfltlow'}]
					{'parenRight'} ~> preserved

		do "Bracket-colon"
			CreateLigationLookup : keep-if 'center-ops' : list
				chain-rule # (:
					{'parenLeft' 'bracketLeft' 'braceLeft'}  ~> preserved
					colon_center
				chain-rule # :)
					colon_center
					{'parenRight' 'bracketRight' 'braceRight'} ~> preserved

		do "Operator Centering"
			local startRules {}
			foreach [g : items-of centerizeGroups] : begin
				startRules.push : chain-rule [g] [g] [acskip] [acskip] [acops] # ::+
				startRules.push : chain-rule [g] [g] [acskip] [acops]          # ::+
				startRules.push : chain-rule [g] [g] [acops]                   # ::+
				startRules.push : chain-rule [g] [acskip] [acskip] [acops]     # :+
				startRules.push : chain-rule [g] [acskip] [acops]              # :+
				startRules.push : chain-rule [g] [acops]                       # :+
				startRules.push : chain-rule [acops] [acskip] [acskip] [g]     # +:
				startRules.push : chain-rule [acops] [acskip] [g]              # +:
				startRules.push : chain-rule [acops] [g]                       # +:
			CreateLigationLookup : keep-if 'center-ops' startRules

		do "Operator centering : Trailing"
			local centerizeL : centerizeGroups.map : x => [x].left.0
			local centerizeR : centerizeGroups.map : lambda [x] : begin
				local left [x].left
				local right [x].right
				while (right <@ Function) : set right [right left]
				return : if right (right.0 || left.0) (left.0)

			CreateLigationLookup : keep-if 'center-ops' : list
				chain-rule centerizeR [acskip] [acskip] (centerizeL ~> centerizeR)
				chain-rule centerizeR [acskip] (centerizeL ~> centerizeR)
				chain-rule centerizeR (centerizeL ~> centerizeR)
			CreateReverseLigationLookup : keep-if 'center-ops' : list
				reverse-rule
					centerizeL ~> centerizeR
					[acskip]   ~> preserved
					[acskip]   ~> preserved
					centerizeR ~> preserved
				reverse-rule
					centerizeL ~> centerizeR
					[acskip]   ~> preserved
					centerizeR ~> preserved
				reverse-rule
					centerizeL ~> centerizeR
					centerizeR ~> preserved

		do "Operator centering : Avoid 'letter-point' being centered"
			CreateLigationLookup : keep-if 'center-ops' : list
				chain-rule alphabet [no_period_center]
				chain-rule [no_period_center] alphabet

	do "Colon chains" : begin
		define dLeft      : [lsx 'dLeft'] dottyPunctuations
		define dRight     : [lsx 'dRight'] dottyPunctuations
		define dLeftHalf  : [lsx 'dLeftHalf'] dottyPunctuations
		define dRightHalf : [lsx 'dRightHalf'] dottyPunctuations
		define dMid       : [lsx 'dMid'] dottyPunctuations
		define boundary   : anyStartingWith widePunctuation

		CreateLigationLookup : keep-if 'kern-dotty' : list
			chain-rule
				[dRight.concat dMid] ~> preserved
				dottyPunctuations ~> dMid
				dottyPunctuations ~> preserved
			chain-rule
				[dRight.concat dMid] ~> preserved
				dottyPunctuations ~> dLeft
			chain-rule
				dottyPunctuations ~> dRight
				dottyPunctuations ~> preserved
		CreateLigationLookup : keep-if 'kern-dotty' : list
			chain-rule
				boundary             ~> preserved
				dRight               ~> dMid
				dLeft                ~> dMid
				boundary             ~> preserved
			chain-rule
				boundary             ~> preserved
				dRight               ~> dMid
				[dMid.concat dLeft]  ~> preserved
			chain-rule
				[dMid.concat dRight] ~> preserved
				dLeft                ~> dMid
				boundary             ~> preserved
		CreateLigationLookup : keep-if 'kern-dotty' : list
			chain-rule
				dRight ~> dRightHalf
				dLeft  ~> dLeftHalf

	do "Plus chains" : begin
		CreateLigationLookup : keep-if 'plusplus' : list
			# +++++++++
			chain-rule
				{'plus.lig.cr' 'plus.lig.jr'} ~> preserved
				{'plus'} ~> [lsx 'jr']
			chain-rule
				{'plus'} ~> [lsx 'lc']
				{'plus'} ~> [lsx 'cc']
				{'plus'} ~> [lsx 'cc']
				{'plus'} ~> [lsx 'cr']
			chain-rule
				{'plus'} ~> [lsx 'lc']
				{'plus'} ~> [lsx 'cc']
				{'plus'} ~> [lsx 'cr']
			chain-rule
				{'plus'} ~> [lsx 'lc']
				{'plus'} ~> [lsx 'cr']
		CreateLigationLookup : keep-if 'plusplus' : list
			chain-rule # +++
				{'plus.lig.lc'} ~> {'plus.lig.lc.s'}
				{'plus.lig.cc'} ~> preserved
				{'plus.lig.cr'} ~> {'plus.lig.cr.s'}
			chain-rule # ++
				{'plus.lig.lc'} ~> {'plus.lig.lc.s'}
				{'plus.lig.cr'} ~> {'plus.lig.cr.s'}

	do "triangles" : begin
		CreateLigationLookup : keep-if 'trig' : list
			chain-rule # <|
				less    ~> [lsx 'trig']
				{'bar'} ~> [lsx 'trig']
			chain-rule # |>
				{'bar'} ~> [lsx 'trig']
				greater ~> [lsx 'trig']
			chain-rule # |>
				[[lsx 'trig'] {'bar'}] ~> preserved
				greater                ~> [lsx 'trig']
			chain-rule # <||||
				[[lsx 'trig'] {'bar'}] ~> preserved
				{'bar'}                ~> [lsx 'trig']
		CreateReverseLigationLookup : keep-if 'trig' : list
			reverse-rule
				{'bar'}                ~> [[lsx 'trig'] {'bar'}]
				[[lsx 'trig'] {'bar'}] ~> preserved

	do "shift-eq operators (<<= and >>=)"
		CreateLigationLookup : keep-if "llggeq" : list
			chain-rule # =<<<=
				equal      ~> preserved
				less       ~> advance
				less       ~> advance
				less       ~> advance
				equal      ~> preserved
			chain-rule # <<<==
				less       ~> advance
				less       ~> advance
				less       ~> advance
				equal      ~> preserved
				equal      ~> preserved
			chain-rule # <<<=
				less       ~> [lsx 'shiftN0h']
				less       ~> preserved
				less       ~> [lsx 'shift0h']
				equal      ~> preserved
			chain-rule # =>>>=
				equal      ~> preserved
				greater    ~> advance
				greater    ~> advance
				greater    ~> advance
				equal      ~> preserved
			chain-rule # >>>==
				greater    ~> advance
				greater    ~> advance
				greater    ~> advance
				equal      ~> preserved
				equal      ~> preserved
			chain-rule # >>>=
				greater    ~> [lsx 'shift0h']
				greater    ~> preserved
				greater    ~> [lsx 'shiftN0h']
				equal      ~> preserved
			chain-rule # =<<=
				equal      ~> preserved
				less       ~> advance
				less       ~> advance
				equal      ~> preserved
			chain-rule # <<==
				less       ~> advance
				less       ~> advance
				equal      ~> preserved
				equal      ~> preserved
			chain-rule # <<=
				less       ~> [lsx 'shiftN0h']
				less       ~> [lsx 'shift0h']
				equal      ~> preserved
			chain-rule # =>>=
				equal      ~> preserved
				greater    ~> advance
				greater    ~> advance
				equal      ~> preserved
			chain-rule # >>==
				greater    ~> advance
				greater    ~> advance
				equal      ~> preserved
				equal      ~> preserved
			chain-rule # >>=
				greater    ~> [lsx 'shift0h']
				greater    ~> [lsx 'shiftN0h']
				equal      ~> preserved

	do "HTML Comment"
		CreateLigationLookup : keep-if 'html-comment' : list
			chain-rule # <!---
				less   ~> preserved
				exclam ~> [lsx 'htmlcommentstart']
				hyphen ~> [lsx 'lxc']
				hyphen ~> [lsx 'cc']
				hyphen ~> [lsx 'cf']
			chain-rule # <!--
				less   ~> preserved
				exclam ~> [lsx 'htmlcommentstart']
				hyphen ~> [lsx 'lxc']
				hyphen ~> [lsx 'cf']

	do "Arrows"
		define singular : hyphen.concat anyWave
		define [CBarSingular k] : begin
			local hyphenSet : new Set hyphen
			local fHyphen : lsx k
			local fWave   : just "wave.\(k)"
			return : lambda [t] : begin
				local hyphenResults : fHyphen t
				local waveResults   : fWave   t
				return : t.map : lambda [x j]
					if [hyphenSet.has x] hyphenResults.(j) waveResults.(j)

		define ArrowFormationConfig : list
			#    rod      dbl  ltgt  FBarCls
			list equal    1    1     lsx
			list singular 0    0     CBarSingular

		define ArrowExtensionConfig : list
			#    rod      FBarCls
			list equal    lsx
			list hyphen   lsx
			list anyWave  [lambda [k] [just "wave.\(k)"]]

		foreach [{barCls dbl ltgt CBar} : items-of ArrowFormationConfig] : do
			define heads {}
			define rods  {}

			# Disable regular expression lookaheads
			heads.push : chain-rule
				{'parenLeft'} ~> preserved
				{'question'}  ~> preserved
				regexLookAround ~> advance
				regexLookAround ~> advance
				regexLookAround ~> advance
				regexLookAround ~> advance
			heads.push : chain-rule
				{'parenLeft'} ~> preserved
				{'question'}  ~> preserved
				regexLookAround ~> advance
				regexLookAround ~> advance
				regexLookAround ~> advance
			heads.push : chain-rule
				{'parenLeft'} ~> preserved
				{'question'}  ~> preserved
				regexLookAround ~> advance
				regexLookAround ~> advance

			define [CJoinHeadForce]         : if dbl [lsx 'dblhead']        [lsx 'shift0']
			define [CJoinHeadMid]           : if dbl [lsx 'dblhead.mid']    [lsx 'shift0.mid']
			define [CJoinHeadForceShift1]   : if dbl [lsx 'dblhead.shift1'] [lsx 'shift1']
			define [CAntiHeadForce]         : if dbl [lsx 'hole']           [lsx 'shift0.anti']
			define [CAntiHeadForceShiftN1]  : if dbl [lsx 'hole.shiftN1']   [lsx 'shiftN1']

			define [TJoinHeadForce]         : only 'arrow'  : CJoinHeadForce
			define [TJoinHeadAmbig]         : if [not ltgt] [TJoinHeadForce] advance
			define [TJoinHeadForceShift1]   : only 'arrow'  : CJoinHeadForceShift1
			define [TMiddleHead]            : only 'arrow2' [CJoinHeadMid] [TJoinHeadForce]
			define [TAntiHeadForce]         : only 'arrow2' : CAntiHeadForce
			define [TAntiHeadAmbig]         : if [not ltgt] [TAntiHeadForce] advance
			define [TAntiHeadForceShiftN1]  : only 'arrow2' : CAntiHeadForceShiftN1

			define [CollectApply ck fns] : begin
				local a : new Set
				foreach [fnT : items-of fns] : begin
					define fn [fnT]
					define transformed : if fn [fn ck] ck
					foreach [g : items-of transformed] : begin
						a.add g
				return : Array.from a

			local arrowIndicator : anyBar.concat : CollectApply [less.concat greater] : list [lambda [] advance] CJoinHeadForce CJoinHeadForceShift1 CAntiHeadForce CAntiHeadForceShiftN1 CJoinHeadMid

			# Arrow head identifier
			begin "Central"
				heads.push : chain-rule # = > > =
					barCls  ~> preserved
					greater ~> [TJoinHeadForceShift1]
					greater ~> [TAntiHeadForce]
					barCls  ~> preserved
				heads.push : chain-rule # = < < =
					barCls  ~> preserved
					less    ~> [TAntiHeadForce]
					less    ~> [TJoinHeadForceShift1]
					barCls  ~> preserved
				heads.push : chain-rule # = > < =
					barCls  ~> preserved
					greater ~> [TJoinHeadForce]
					less    ~> [TJoinHeadForce]
					barCls  ~> preserved
				heads.push : chain-rule # = < > =
					barCls  ~> preserved
					less    ~> [TAntiHeadForce]
					greater ~> [TAntiHeadForce]
					barCls  ~> preserved
				heads.push : chain-rule # = > =
					barCls  ~> preserved
					greater ~> [TMiddleHead]
					barCls  ~> preserved
				heads.push : chain-rule # = < =
					barCls  ~> preserved
					less    ~> [TMiddleHead]
					barCls  ~> preserved
			begin "Boundary"
				heads.push : chain-rule # = > >
					barCls  ~> preserved
					greater ~> [TJoinHeadForceShift1]
					greater ~> preserved
				heads.push : chain-rule # = < <
					barCls  ~> preserved
					less    ~> [TAntiHeadForceShiftN1]
					less    ~> preserved
				heads.push : chain-rule # > > =
					greater ~> preserved
					greater ~> [TAntiHeadForceShiftN1]
					barCls  ~> preserved
				heads.push : chain-rule # < < =
					less    ~> preserved
					less    ~> [TJoinHeadForceShift1]
					barCls  ~> preserved
				heads.push : chain-rule # = = >
					barCls  ~> preserved
					barCls  ~> preserved
					greater ~> [TJoinHeadForce]
				heads.push : chain-rule # = = <
					barCls  ~> preserved
					barCls  ~> preserved
					less    ~> [TAntiHeadForce]
				heads.push : chain-rule # > = =
					greater ~> [TAntiHeadForce]
					barCls  ~> preserved
					barCls  ~> preserved
				heads.push : chain-rule # < = =
					less    ~> [TJoinHeadForce]
					barCls  ~> preserved
					barCls  ~> preserved
				heads.push : chain-rule # * = >
					arrowIndicator ~> preserved
					barCls  ~> preserved
					greater ~> [TJoinHeadForce]
				heads.push : chain-rule # * = <
					arrowIndicator ~> preserved
					barCls  ~> preserved
					less    ~> [TAntiHeadForce]
				heads.push : chain-rule # > = *
					greater ~> [TAntiHeadForce]
					barCls  ~> preserved
					arrowIndicator ~> preserved
				heads.push : chain-rule # < = *
					less    ~> [TJoinHeadForce]
					barCls  ~> preserved
					arrowIndicator ~> preserved
			begin "Ambiguous"
				heads.push : chain-rule # = >
					barCls  ~> preserved
					greater ~> [TJoinHeadAmbig]
				heads.push : chain-rule # = <
					barCls  ~> preserved
					less    ~> [TAntiHeadAmbig]
				heads.push : chain-rule # > =
					greater ~> [TAntiHeadAmbig]
					barCls  ~> preserved
				heads.push : chain-rule # < =
					less    ~> [TJoinHeadAmbig]
					barCls  ~> preserved

			# Rod transformer
			define kindLeftMid  : list
				list false 'l'  : CollectApply less    : list CJoinHeadForce CJoinHeadMid
				list false 'l1' : CollectApply less    : list CJoinHeadForceShift1
				list false 'j'  : CollectApply greater : list CAntiHeadForce CJoinHeadMid
				list true  'j'    anyBar
				list false 'j1' : CollectApply greater : list CAntiHeadForceShiftN1
			define kindRightMid : list
				list false 'r'  : CollectApply greater : list CJoinHeadForce CJoinHeadMid
				list false 'r1' : CollectApply greater : list CJoinHeadForceShift1
				list false 'j'  : CollectApply less    : list CAntiHeadForce CJoinHeadMid
				list true  'j'    anyBar
				list false 'j1' : CollectApply less    : list CAntiHeadForceShiftN1

			# console.log featureName barCls kindLeftMid kindRightMid

			foreach [{ lBar kl envL } : items-of kindLeftMid] : begin
				foreach [{ rBar kr envR } : items-of kindRightMid] : begin
					if (!lBar || !rBar) : begin
						rods.push : chain-rule	# < - >
							envL   ~> preserved
							barCls ~> [CBar "\(kl)\(kr)"]
							envR   ~> preserved

			foreach [{ lBar kl envL } : items-of kindLeftMid] : begin
				if (!lBar) : begin
					rods.push : chain-rule	# < -
						envL   ~> preserved
						barCls ~> [CBar "\(kl)f"]

			foreach [{ rBar kr envR } : items-of kindRightMid] : begin
				if (!rBar) : begin
					rods.push : chain-rule	# - >
						barCls ~> [CBar "f\(kr)"]
						envR   ~> preserved

			CreateLigationLookup heads
			CreateLigationLookup rods

		foreach [{barCls CBar} : items-of ArrowExtensionConfig] : do
			do "Extended arrow sticks"
				# Extension
				local stickTermLeft  {}
				local stickConnLeft  {}
				foreach [lc : items-of {'l' 'l1' 'j' 'j1' 'c' 'z'}] : begin
					set stickTermLeft : stickTermLeft.concat : [CBar "\(lc)f"] barCls
					set stickConnLeft : stickConnLeft.concat : [CBar "\(lc)c"] barCls

				local stickTermRight {}
				local stickConnRight {}
				foreach [rc : items-of {'r' 'r1' 'j' 'j1' 'c' 'z'}] : begin
					set stickTermRight : stickTermRight.concat : [CBar "f\(rc)"] barCls
					set stickConnRight : stickConnRight.concat : [CBar "c\(rc)"] barCls

				CreateLigationLookup : list
					chain-rule
						stickTermLeft  ~> preserved
						barCls         ~> [CBar 'zf']

				CreateReverseLigationLookup : list
					reverse-rule
						barCls         ~> [CBar 'fz']
						stickTermRight ~> preserved

				# Fix for Apple : arrow bar at line start won't join with the rest (#548)
				CreateLigationLookup : list
					chain-rule
						barCls         ~> [CBar 'fz']
						stickTermRight ~> preserved

				CreateLigationLookup : list
					chain-rule
						stickTermLeft        ~> preserved
						[[CBar 'zf'] barCls] ~> [[CBar 'zj'] barCls]
						anyBar               ~> preserved
					chain-rule
						anyBar               ~> preserved
						[[CBar 'fz'] barCls] ~> [[CBar 'jz'] barCls]
						stickTermRight       ~> preserved

				# Gap filling
				CreateLigationLookup : list
					chain-rule
						stickTermLeft  ~> stickConnLeft
						stickTermRight ~> stickConnRight

		foreach [{barCls dbl ltgt CBar} : items-of ArrowFormationConfig] : do
			define [CJoinHeadForce]         : if dbl [lsx 'dblhead']        [lsx 'shift0']
			define [CAntiHeadForce]         : if dbl [lsx 'hole']           [lsx 'shift0.anti']

			do "Ambiguous rods"
				define ambiguousRods {}
				ambiguousRods.push : chain-rule # =>
					barCls     ~> [only 'arrow' : CBar 'fr']
					greater    ~> [only 'arrow' : CJoinHeadForce]
				if ltgt
					ambiguousRods.push : chain-rule # <=
						less       ~> [only 'arrowZALE' : CJoinHeadForce]
						barCls     ~> [only 'arrowZALE' : CBar 'lf']
					ambiguousRods.push : chain-rule # <-
						less       ~> [only 'arrow' : CJoinHeadForce]
						barCls     ~> [only 'arrow' : CBar 'lf']

				ambiguousRods.push : chain-rule # =<
					barCls     ~> [only 'arrow2' : CBar 'fj']
					less       ~> [only 'arrow2' : CAntiHeadForce]
				if ltgt
					ambiguousRods.push : chain-rule # >=
						greater    ~> [only 'arrowZAGE' : CAntiHeadForce]
						barCls     ~> [only 'arrowZAGE' : CBar 'jf']
					ambiguousRods.push : chain-rule # >-
						greater    ~> [only 'arrow2' : CAntiHeadForce]
						barCls     ~> [only 'arrow2' : CBar 'jf']

				CreateLigationLookup ambiguousRods

	# <<, >>, <<<, >>>
	do "Less/Greater Chaining"
		CreateLigationLookup : keep-if 'llgg' : list
			chain-rule
				{'parenLeft'} ~> preserved
				{'question'}  ~> preserved
				regexLookAround ~> advance
				regexLookAround ~> advance
				regexLookAround ~> advance
				regexLookAround ~> advance
			chain-rule
				{'parenLeft'} ~> preserved
				{'question'}  ~> preserved
				regexLookAround ~> advance
				regexLookAround ~> advance
				regexLookAround ~> advance
			chain-rule
				{'parenLeft'} ~> preserved
				{'question'}  ~> preserved
				regexLookAround ~> advance
				regexLookAround ~> advance
			chain-rule # <<<
				less       ~> [lsx 'shiftN0h']
				less       ~> preserved
				less       ~> [lsx 'shift0h']
			chain-rule # >>>
				greater    ~> [lsx 'shift0h']
				greater    ~> preserved
				greater    ~> [lsx 'shiftN0h']
			chain-rule # <<
				less       ~> [lsx 'shiftN0h']
				less       ~> [lsx 'shift0h']
			chain-rule # >>
				greater    ~> [lsx 'shift0h']
				greater    ~> [lsx 'shiftN0h']

		# Cancel alternates of < and > shapes if there are more after them
		local lAll  {"less" "less.lig.shift0h" "less.lig.shiftN0h"}
		local lNeut {"less" "less"            "less"}
		local gAll  {"greater" "greater.lig.shift0h" "greater.lig.shiftN0h"}
		local gNeut {"greater" "greater"            "greater"}
		local lgAll  : lAll.concat  gAll
		local lgNeut : lNeut.concat gNeut
		CreateLigationLookup : keep-if 'llgg' : list
			chain-rule
				lgAll ~> lgNeut
				lgAll ~> lgNeut
				lgAll ~> lgNeut
				lgAll ~> preserved
		CreateLigationLookup : keep-if 'llgg' : list
			chain-rule
				lgAll ~> preserved
				lgAll ~> preserved
				lgAll ~> preserved
				lgAll ~> lgNeut
		CreateLigationLookup : keep-if 'llgg' : list
			chain-rule
				lAll ~> lNeut
				lAll ~> lNeut
				gAll ~> gNeut
			chain-rule
				gAll ~> gNeut
				gAll ~> gNeut
				lAll ~> lNeut
			chain-rule
				lAll ~> lNeut
				gAll ~> gNeut
				gAll ~> gNeut
			chain-rule
				gAll ~> gNeut
				lAll ~> lNeut
				lAll ~> lNeut

	# /\ and \/
	do "logical" : CreateLigationLookup : keep-if 'logic' : list
		chain-rule
			slash         ~> [csx 'left']
			backslash     ~> [csx 'right']
		chain-rule
			backslash     ~> [csx 'left']
			slash         ~> [csx 'right']
		chain-rule
			bar           ~> preserved
			hyphen        ~> [lsx 'jf']
		chain-rule
			hyphen        ~> [lsx 'fj']
			bar           ~> preserved

	# == and !=, and other related
	do "equals" : CreateLigationLookup : list
		# Disable regular expression lookaheads
		chain-rule
			{'parenLeft'} ~> preserved
			{'question'}  ~> preserved
			regexLookAround ~> advance
			regexLookAround ~> advance
			regexLookAround ~> advance
			regexLookAround ~> advance
		chain-rule
			{'parenLeft'} ~> preserved
			{'question'}  ~> preserved
			regexLookAround ~> advance
			regexLookAround ~> advance
			regexLookAround ~> advance
		chain-rule
			{'parenLeft'} ~> preserved
			{'question'}  ~> preserved
			regexLookAround ~> advance
			regexLookAround ~> advance
		chain-rule
			{'equal.cf' 'equal.jf'} ~> preserved
			equal       ~> [only 'eqeq' {'equal.jf'}]
		chain-rule
			exclam      ~> [only 'exeq' {'equal.fc.neg4'}]
			equal       ~> [only 'exeq' {'equal.cc'}]
			equal       ~> [only 'exeq' {'equal.cc'}]
			equal       ~> [only 'exeq' {'equal.cf'}]
		chain-rule
			equal       ~> [only 'eqeq' {'equal.fc'}]
			equal       ~> [only 'eqeq' {'equal.cc'}]
			equal       ~> [only 'eqeq' {'equal.cc'}]
			equal       ~> [only 'eqeq' {'equal.cf'}]
		chain-rule
			exclam      ~> [only 'exeqeq' {'ident.fc.neg3'} : only 'exeq' {'equal.fc.neg3'}]
			equal       ~> [only 'exeqeq' {'ident.cc'} : only 'exeq' {'equal.cc'}]
			equal       ~> [only 'exeqeq' {'ident.cf'} : only 'exeq' {'equal.cf'}]
		chain-rule
			equal       ~> [only 'eqexeq' {'ident.fc.neg3'}]
			exclam      ~> [only 'eqexeq' {'ident.cc'}]
			equal       ~> [only 'eqexeq' {'ident.cf'}]
		chain-rule
			equal       ~> [only 'eqslasheq' {'ident.fc.neg3'} : only 'slasheq' {'equal.fc.neg3'}]
			slash       ~> [only 'eqslasheq' {'ident.cc'} : only 'slasheq' {'equal.cc'}]
			equal       ~> [only 'eqslasheq' {'ident.cf'} : only 'slasheq' {'equal.cf'}]
		chain-rule
			equal       ~> [only 'eqeqeq' {'ident.fc'} : only 'eqeq' {'equal.fc'}]
			equal       ~> [only 'eqeqeq' {'ident.cc'} : only 'eqeq' {'equal.cc'}]
			equal       ~> [only 'eqeqeq' {'ident.cf'} : only 'eqeq' {'equal.cf'}]
		chain-rule
			exclam      ~> [only 'exeq' {'equal.fc.neg'}]
			equal       ~> [only 'exeq' {'equal.cf'}]
		chain-rule
			anyWave     ~> [only 'tildeeq' : just 'equal.fc.neg']
			equal       ~> [only 'tildeeq' {'equal.cf'}]
		chain-rule
			slash       ~> [only 'slasheq' {'equal.fc.neg'}]
			equal       ~> [only 'slasheq' {'equal.cf'}]
		chain-rule
			lessAndEquiv    ~> [only 'ltgt-diamond' [just 'less.lig.diamond'] : only 'ltgt-ne' [just 'equal.fc.neg']]
			greaterAndEquiv ~> [only 'ltgt-diamond' [just 'greater.lig.diamond'] : only 'ltgt-ne' [just 'equal.cf']]
		chain-rule
			equal       ~> [only 'eqeq' {'equal.fc'}]
			equal       ~> [only 'eqeq' {'equal.cf'}]

	# <= and >=
	do "inequals" : CreateLigationLookup : keep-if 'ineq' : list
		# Disable regular expression lookaheads
		chain-rule
			{'parenLeft'} ~> preserved
			{'question'}  ~> preserved
			regexLookAround ~> advance
			regexLookAround ~> advance
			regexLookAround ~> advance
			regexLookAround ~> advance
		chain-rule
			{'parenLeft'} ~> preserved
			{'question'}  ~> preserved
			regexLookAround ~> advance
			regexLookAround ~> advance
			regexLookAround ~> advance
		chain-rule
			{'parenLeft'} ~> preserved
			{'question'}  ~> preserved
			regexLookAround ~> advance
			regexLookAround ~> advance
		chain-rule
			[acops]            ~> preserved
			{'less' 'greater'} ~> advance
			equal              ~> advance
		chain-rule
			{'less' 'greater'} ~> advance
			equal              ~> advance
			[acops]            ~> preserved
		chain-rule
			{'less'}    ~> {'less.lig2'}
			equal       ~> {'eq.at-lteq.lig2'}
		chain-rule
			{'greater'} ~> {'greater.lig2'}
			equal       ~> {'eq.at-gteq.lig2'}

